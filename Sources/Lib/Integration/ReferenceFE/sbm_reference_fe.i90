! Copyright (C) 2014 Santiago Badia, Alberto F. Mart√≠n and Javier Principe
!
! This file is part of FEMPAR (Finite Element Multiphysics PARallel library)
!
! FEMPAR is free software: you can redistribute it and/or modify
! it under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version.
!
! FEMPAR is distributed in the hope that it will be useful,
! but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
! GNU General Public License for more details.
!
! You should have received a copy of the GNU General Public License
! along with FEMPAR. If not, see <http://www.gnu.org/licenses/>.
!
! Additional permission under GNU GPL version 3 section 7
!
! If you modify this Program, or any covered work, by linking or combining it 
! with the Intel Math Kernel Library and/or the Watson Sparse Matrix Package 
! and/or the HSL Mathematical Software Library (or a modified version of them), 
! containing parts covered by the terms of their respective licenses, the
! licensors of this Program grant you additional permission to convey the 
! resulting work. 
!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!  
subroutine reference_fe_free( this )
  implicit none
  class(reference_fe_t), intent(inout) :: this

  integer(ip) :: i, istat

  if(allocated(this%topology)) deallocate(this%topology)
  if(allocated(this%fe_type)) deallocate(this%fe_type)
  if(allocated(this%field_type)) deallocate(this%field_type)

  if (allocated(this%num_rotations_x_dim)) &
       call memfree(this%num_rotations_x_dim,__FILE__,__LINE__)

  if (allocated(this%num_orientations_x_dim)) &
       call memfree(this%num_orientations_x_dim,__FILE__,__LINE__)

  if (allocated(this%own_dof_permutations)) then
     do i = 1, this%num_dims-1
        call this%own_dof_permutations(i)%free()
     end do
     deallocate(this%own_dof_permutations, stat=istat)
     check(istat==0)
  end if

  call this%own_dofs_n_face%free()
  call this%dofs_n_face%free()
  call this%vertices_n_face%free()
  call this%facets_n_face%free()
  call this%orientation%free()
  call this%polytope%free()
  call this%node_array%free()
  call this%vertex_array%free()

  this%num_dims = 0
  this%order = 0
  this%num_field_components = 0
  this%num_n_faces = 0
  this%num_shape_functions = 0
  this%ptr_n_faces_x_dim  = 0
  this%conformity = .true.

end subroutine reference_fe_free

subroutine reference_fe_print ( reference_fe )
  implicit none
  !     Parameters
  class(reference_fe_t),  intent(in) :: reference_fe

  integer(ip) :: i

  write(*,*) 'topology: ', reference_fe%topology
  write(*,*) 'fe_type: ', reference_fe%fe_type
  write(*,*) 'fe_type: ', reference_fe%field_type
  write(*,*) 'fe_type: ', reference_fe%num_field_components
  write(*,*) 'num_dims: ', reference_fe%num_dims
  write(*,*) 'order: ', reference_fe%order
  write(*,*) 'conformity: ',reference_fe%conformity
  write(*,*) 'num_n_faces', reference_fe%num_n_faces
  write(*,*) 'num_nodes', reference_fe%num_shape_functions
  write(*,*) 'num_n_faces_dim', reference_fe%ptr_n_faces_x_dim

  write(*,*) 'orientation', reference_fe%orientation%a


  write(*,*) 'nodes_n_face'
  call reference_fe%dofs_n_face%print(6)

  write(*,*) 'vertices_n_face'
  call reference_fe%vertices_n_face%print(6)

  write(*,*) 'n_faces_n_face'
  call reference_fe%facets_n_face%print(6)

  write(*,*) 'own_nodes_n_face'
  call reference_fe%own_dofs_n_face%print(6)

end subroutine reference_fe_print

subroutine reference_fe_set_common_data( this, num_dims, order, field_type, conformity, continuity )
  implicit none 
  class(reference_fe_t), intent(inout) :: this 
  integer(ip)          , intent(in)    :: num_dims
  integer(ip)          , intent(in)    :: order              
  character(*)         , intent(in)    :: field_type
  logical              , intent(in)    :: conformity
  logical    , optional, intent(in)    :: continuity
  
  this%num_dims = num_dims
  this%order             = order
  this%conformity    = conformity
  
  if ( present(continuity) ) then
    this%continuity = continuity
  else
    this%continuity = .true.
  end if
   
  massert ( .not. ((this%conformity .eqv. .false.) .and. (this%continuity .eqv. .false.)), "reference_fe_set_common_data: the discrete function space cannot be non-conforming and discontinuous" )
  

  this%field_type = field_type
  if( this%field_type == field_type_scalar ) then
     this%num_field_components = 1
  else if ( this%field_type == field_type_vector ) then
     this%num_field_components = num_dims
  else if ( this%field_type == field_type_tensor ) then
     this%num_field_components = num_dims**2
  else if ( this%field_type == field_type_symmetric_tensor ) then
     this%num_field_components = ((num_dims)*(num_dims+1))/2
  else
     write(0,'(a)') 'Error: reference_fe_t%set_common_data :: undefined field_type'
     check (.false.)
  end if
end subroutine reference_fe_set_common_data

subroutine reference_fe_set_topology( this, topology)
  implicit none
  class(reference_fe_t), intent(inout) :: this 
  character(*), intent(in) :: topology
  this%topology = topology
end subroutine reference_fe_set_topology

subroutine reference_fe_set_fe_type( this, fe_type)
  implicit none
  class(reference_fe_t), intent(inout) :: this 
  character(*), intent(in) :: fe_type
  this%fe_type = fe_type
end subroutine reference_fe_set_fe_type

function reference_fe_get_topology( this )
  implicit none
  class(reference_fe_t), target, intent(in) :: this
  character(:), pointer :: reference_fe_get_topology
  reference_fe_get_topology => this%topology
end function reference_fe_get_topology

function reference_fe_get_field_type( this )
  implicit none
  class(reference_fe_t), target, intent(in) :: this
  character(:), pointer :: reference_fe_get_field_type
  reference_fe_get_field_type => this%field_type
end function reference_fe_get_field_type

function reference_fe_get_num_dims( this )
  implicit none
  class(reference_fe_t), intent(in) :: this
  integer(ip) :: reference_fe_get_num_dims
  reference_fe_get_num_dims = this%num_dims
end function reference_fe_get_num_dims

function reference_fe_get_order( this )
  implicit none
  class(reference_fe_t), intent(in) :: this
  integer(ip) :: reference_fe_get_order
  reference_fe_get_order = this%order
end function reference_fe_get_order

function reference_fe_get_continuity( this )
  implicit none
  class(reference_fe_t), intent(in) :: this
  logical :: reference_fe_get_continuity
  reference_fe_get_continuity = this%continuity
end function reference_fe_get_continuity

function reference_fe_get_conformity( this )
  implicit none
  class(reference_fe_t), intent(in) :: this
  logical :: reference_fe_get_conformity
  reference_fe_get_conformity = this%conformity
end function reference_fe_get_conformity

function reference_fe_get_num_field_components ( this )
  implicit none
  class(reference_fe_t), intent(in) :: this
  integer(ip) :: reference_fe_get_num_field_components
  reference_fe_get_num_field_components = this%num_field_components
end function reference_fe_get_num_field_components

function reference_fe_get_num_n_faces ( this )
  implicit none
  class(reference_fe_t), intent(in) :: this
  integer(ip) :: reference_fe_get_num_n_faces
  reference_fe_get_num_n_faces = this%num_n_faces
end function reference_fe_get_num_n_faces

function reference_fe_get_num_vertices ( this )
  implicit none
  class(reference_fe_t), intent(in) :: this
  integer(ip) :: reference_fe_get_num_vertices
  reference_fe_get_num_vertices = this%ptr_n_faces_x_dim(2) -          &
       &                             this%ptr_n_faces_x_dim(1)
end function reference_fe_get_num_vertices

function reference_fe_get_num_facets ( this )
  implicit none
  class(reference_fe_t), intent(in) :: this
  integer(ip) :: reference_fe_get_num_facets
  reference_fe_get_num_facets = this%ptr_n_faces_x_dim(this%num_dims+1) -          &
       &                          this%ptr_n_faces_x_dim(this%num_dims)
end function reference_fe_get_num_facets

function reference_fe_get_first_facet_id ( this )
  implicit none
  class(reference_fe_t), intent(in) :: this
  integer(ip) :: reference_fe_get_first_facet_id
  reference_fe_get_first_facet_id = this%ptr_n_faces_x_dim(this%num_dims)
end function reference_fe_get_first_facet_id

function reference_fe_get_num_n_faces_of_dim ( this, n_face_dim )
  implicit none
  class(reference_fe_t), intent(in) :: this
  integer(ip)          , intent(in) :: n_face_dim
  integer(ip) :: reference_fe_get_num_n_faces_of_dim
  reference_fe_get_num_n_faces_of_dim = this%ptr_n_faces_x_dim(n_face_dim+2) -          &
       &                                      this%ptr_n_faces_x_dim(n_face_dim+1)
end function reference_fe_get_num_n_faces_of_dim

function reference_fe_get_first_n_face_id_of_dim ( this, n_face_dim )
  implicit none
  class(reference_fe_t), intent(in) :: this
  integer(ip)          , intent(in) :: n_face_dim
  integer(ip) :: reference_fe_get_first_n_face_id_of_dim
  reference_fe_get_first_n_face_id_of_dim = this%ptr_n_faces_x_dim(n_face_dim+1)
end function reference_fe_get_first_n_face_id_of_dim

function reference_fe_get_num_shape_functions ( this )
  implicit none
  class(reference_fe_t), intent(in) :: this
  integer(ip) :: reference_fe_get_num_shape_functions
  reference_fe_get_num_shape_functions = this%num_shape_functions
end function reference_fe_get_num_shape_functions

function reference_fe_get_n_face_dim ( this, n_face_id )
  implicit none
  class(reference_fe_t), intent(in) :: this
  integer(ip)          , intent(in) :: n_face_id
  integer(ip) :: reference_fe_get_n_face_dim

  integer(ip) :: i 
  assert ( n_face_id >= 1 .and. n_face_id <= this%num_n_faces ) 
  do i = 1, this%num_dims
     if ( n_face_id < this%ptr_n_faces_x_dim(i+1) ) then
        reference_fe_get_n_face_dim = i - 1 
        exit
     end if
  end do
end function reference_fe_get_n_face_dim

function reference_fe_get_dofs_n_face ( this )
  implicit none
  class(reference_fe_t), target, intent(in) :: this
  type(list_t), pointer :: reference_fe_get_dofs_n_face
  reference_fe_get_dofs_n_face => this%dofs_n_face
end function reference_fe_get_dofs_n_face

function reference_fe_create_dofs_n_face_iterator ( this, n_face_id) result(list_iterator)
  implicit none
  class(reference_fe_t), intent(in) :: this
  integer(ip)          , intent(in) :: n_face_id
  type(list_iterator_t) :: list_iterator
  assert ( n_face_id >= 1 .and. n_face_id <= this%get_num_n_faces() )
  list_iterator = this%dofs_n_face%create_iterator(n_face_id)
end function reference_fe_create_dofs_n_face_iterator

function reference_fe_get_vertices_n_face ( this )
  implicit none
  class(reference_fe_t), target, intent(in) :: this
  type(list_t), pointer :: reference_fe_get_vertices_n_face
  reference_fe_get_vertices_n_face => this%vertices_n_face
end function reference_fe_get_vertices_n_face

function reference_fe_create_vertices_n_face_iterator ( this, n_face_id) result(list_iterator)
  implicit none
  class(reference_fe_t), intent(in) :: this
  integer(ip)          , intent(in) :: n_face_id
  type(list_iterator_t) :: list_iterator
  assert ( n_face_id >= 1 .and. n_face_id <= this%get_num_n_faces() )
  list_iterator = this%vertices_n_face%create_iterator(n_face_id)
end function reference_fe_create_vertices_n_face_iterator

function reference_fe_get_facets_n_face ( this )
  implicit none
  class(reference_fe_t), target, intent(in) :: this
  type(list_t), pointer :: reference_fe_get_facets_n_face
  reference_fe_get_facets_n_face => this%facets_n_face
end function reference_fe_get_facets_n_face

function reference_fe_get_node_n_face ( this, i, j )
  implicit none
  class(reference_fe_t), intent(in) :: this
  integer(ip), intent(in) :: i, j
  integer(ip) :: reference_fe_get_node_n_face
  type(list_iterator_t)   :: iterator
  iterator = this%dofs_n_face%create_iterator(j)
  reference_fe_get_node_n_face = iterator%get_from_current(i-1)
end function reference_fe_get_node_n_face

function reference_fe_get_num_dofs_on_n_face ( this, i )
  implicit none
  class(reference_fe_t), intent(in) :: this
  integer(ip), intent(in) :: i
  integer(ip) :: reference_fe_get_num_dofs_on_n_face
  reference_fe_get_num_dofs_on_n_face = this%dofs_n_face%get_sublist_size(i)
end function reference_fe_get_num_dofs_on_n_face

function reference_fe_create_dofs_on_n_face_iterator ( this, i )
  implicit none
  class(reference_fe_t), intent(in) :: this
  integer(ip), intent(in) :: i
  type(list_iterator_t) :: reference_fe_create_dofs_on_n_face_iterator
  reference_fe_create_dofs_on_n_face_iterator = this%dofs_n_face%create_iterator(i)    
end function reference_fe_create_dofs_on_n_face_iterator

function rfe_translate_dof_lid_n_face_to_dof_lid_cell ( this, i, j )
  implicit none
  class(reference_fe_t), intent(in) :: this
  integer(ip), intent(in) :: i, j
  integer(ip) :: rfe_translate_dof_lid_n_face_to_dof_lid_cell
  type(list_iterator_t)   :: iterator
  iterator = this%own_dofs_n_face%create_iterator(j)
  rfe_translate_dof_lid_n_face_to_dof_lid_cell = iterator%get_from_current(i-1)
end function rfe_translate_dof_lid_n_face_to_dof_lid_cell

function reference_fe_get_num_own_dofs_n_face ( this, i )
  implicit none
  class(reference_fe_t), intent(in) :: this
  integer(ip), intent(in) :: i
  integer(ip) :: reference_fe_get_num_own_dofs_n_face
  reference_fe_get_num_own_dofs_n_face = this%own_dofs_n_face%get_sublist_size(i)
end function reference_fe_get_num_own_dofs_n_face

function reference_fe_create_own_dofs_on_n_face_iterator ( this, i )
  implicit none
  class(reference_fe_t), intent(in) :: this
  integer(ip), intent(in) :: i
  type(list_iterator_t) :: reference_fe_create_own_dofs_on_n_face_iterator
  reference_fe_create_own_dofs_on_n_face_iterator = this%own_dofs_n_face%create_iterator(i)    
end function reference_fe_create_own_dofs_on_n_face_iterator

function reference_fe_get_num_vertices_n_face ( this, i )
  implicit none
  class(reference_fe_t), intent(in) :: this
  integer(ip), intent(in) :: i
  integer(ip) :: reference_fe_get_num_vertices_n_face
  reference_fe_get_num_vertices_n_face = this%vertices_n_face%get_sublist_size(i)
end function reference_fe_get_num_vertices_n_face

! This query should return .true. only for interpolatory FEs.
! By default, we assume that the reference fe is NOT interpolatory.
! Interpolatory FEs should overwrite this function and return .true.
function reference_fe_has_nodal_quadrature ( this )
  implicit none
  class(reference_fe_t), intent(in) :: this
  logical :: reference_fe_has_nodal_quadrature
  reference_fe_has_nodal_quadrature = .false.
end function reference_fe_has_nodal_quadrature

! This query can only be succesfully resolved by interpolatory FEs.
! By default, we assume that the reference fe is NOT interpolatory.
! Interpolatory FEs should overwrite this function and return the
! coordinates of its support points
function reference_fe_get_nodal_quadrature ( this )
  implicit none
  class(reference_fe_t), target, intent(in) :: this
  type(quadrature_t), pointer :: reference_fe_get_nodal_quadrature
  assert(.false.)
end function reference_fe_get_nodal_quadrature


!==================================================================================================
function reference_fe_compute_permutation_index(source_reference_fe, target_reference_fe,          &
     &                                          source_n_face_id, target_n_face_id,         &
     &                                          source_n_faces, target_n_faces)
  implicit none
  class(reference_fe_t), intent(in)  :: source_reference_fe
  class(reference_fe_t), intent(in)  :: target_reference_fe
  integer(ip)          , intent(in)  :: source_n_face_id ! Local n-face ID
  integer(ip)          , intent(in)  :: target_n_face_id ! Local n-face ID
  integer(ip)          , intent(in)  :: source_n_faces(:)
  integer(ip)          , intent(in)  :: target_n_faces(:) 
  integer(ip) :: reference_fe_compute_permutation_index
  integer(ip)                        :: relative_orientation, relative_rotation 
  type(list_iterator_t)              :: source_n_faces_iterator
  type(list_iterator_t)              :: target_n_faces_iterator

  integer(ip) :: target_first_vertex_global_id, i, source_vertex_global_id 

  ! Relative permutation
  if (target_reference_fe%get_n_face_dim(target_n_face_id) < 2) then
     relative_orientation = 0
  else
     relative_orientation = modulo( 1 +                                    &
          &                             source_reference_fe%orientation%a(source_n_face_id) -      &
          &                             target_reference_fe%orientation%a(target_n_face_id),2)
  end if
  
  ! Relative Rotation
  source_n_faces_iterator = source_reference_fe%vertices_n_face%create_iterator(source_n_face_id)
  target_n_faces_iterator = target_reference_fe%vertices_n_face%create_iterator(target_n_face_id)
  target_first_vertex_global_id = target_n_faces(target_n_faces_iterator%get_current())
  relative_rotation = 1
  do while(.not. source_n_faces_iterator%is_upper_bound())
     source_vertex_global_id = source_n_faces(source_n_faces_iterator%get_current())
     if (source_vertex_global_id ==  target_first_vertex_global_id) exit
     relative_rotation = relative_rotation + 1
     call source_n_faces_iterator%next()
  end do
  
  reference_fe_compute_permutation_index = relative_rotation +                                        &
         & source_reference_fe%num_rotations_x_dim(source_reference_fe%num_dims-1) * relative_orientation
  
end function reference_fe_compute_permutation_index

!==================================================================================================
! Given a permutation index for a pair of elements K1 (source) and K2 (target) that share a vef, a reference fe in which one has the 
! permutations table, the LID-in-vef of a node in cell K1, and the LID of the face in K1, it returns 
! the LID-in-cell of the node wrt K2
!==================================================================================================
function reference_fe_permute_dof_LID_n_face(this,source_dof_LID_n_face,n_face_dim,permutation_index)
  implicit none
  class(reference_fe_t), intent(in)  :: this
  integer(ip)          , intent(in)  :: source_dof_LID_n_face
  integer(ip)          , intent(in)  :: n_face_dim
  integer(ip)          , intent(in)  :: permutation_index
  integer(ip) :: reference_fe_permute_dof_LID_n_face
  if ( n_face_dim == 0 ) then
     reference_fe_permute_dof_LID_n_face = source_dof_LID_n_face
  else
     reference_fe_permute_dof_LID_n_face = this%own_dof_permutations          &
          & (n_face_dim)%a(source_dof_LID_n_face,permutation_index)
  end if
end function reference_fe_permute_dof_LID_n_face

!==================================================================================================
subroutine p_reference_fe_free( this )
  implicit none
  class(p_reference_fe_t), intent(inout) :: this
  if (associated ( this%p )) then
     call this%p%free()
     deallocate ( this%p )
  end if
end subroutine p_reference_fe_free

!==================================================================================================
function reference_fe_get_normal_orientation_factor( this, facet_lid, cell_map )          &
  result(orientation_factor)
  implicit none
  class(reference_fe_t), intent(in)    :: this
  integer(ip)                     , intent(in)    :: facet_lid
  type(cell_map_t)       , optional , intent(in)    :: cell_map
  real(rp)    :: orientation_factor
  real(rp)    :: jacobian_reorientation_factor
  integer(ip) :: num_cell_dims, orientation_face, local_vef_id
  jacobian_reorientation_factor = 1.0_rp
  if (present(cell_map)) then
     if (.not. cell_map%is_det_jacobian_positive()) jacobian_reorientation_factor = -1.0_rp
  end if
  num_cell_dims = this%num_dims
  if (num_cell_dims.ne.1) then
     local_vef_id = this%ptr_n_faces_x_dim(num_cell_dims) + facet_lid - 1
     orientation_face  = this%orientation%a(local_vef_id)
     orientation_factor = (-1)**orientation_face*jacobian_reorientation_factor
  else ! 1D special case
     orientation_factor = -1.0_rp**(real(facet_lid))
  end if
end function reference_fe_get_normal_orientation_factor
