! Copyright (C) 2014 Santiago Badia, Alberto F. Mart√≠n and Javier Principe
!
! This file is part of FEMPAR (Finite Element Multiphysics PARallel library)
!
! FEMPAR is free software: you can redistribute it and/or modify
! it under the terms of the GNU General Public License as published byfill_inter
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version.
!
! FEMPAR is distributed in the hope that it will be useful,
! but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
! GNU General Public License for more details.
!
! You should have received a copy of the GNU General Public License
! along with FEMPAR. If not, see <http://www.gnu.org/licenses/>.
!
! Additional permission under GNU GPL version 3 section 7
!
! If you modify this Program, or any covered work, by linking or combining it 
! with the Intel Math Kernel Library and/or the Watson Sparse Matrix Package 
! and/or the HSL Mathematical Software Library (or a modified version of them), 
! containing parts covered by the terms of their respective licenses, the
! licensors of this Program grant you additional permission to convey the 
! resulting work. 
!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

!**************************************************************************************************
! Deferred TBP implementors from reference_fe_t
!**************************************************************************************************

!==================================================================================================
function hex_nedelec_reference_fe_check_compatibility_of_n_faces                   &
     &                      (target_reference_fe,source_reference_fe, source_n_face_id,target_n_face_id)
  implicit none
  class(hex_nedelec_reference_fe_t), intent(in)  :: target_reference_fe
  class(reference_fe_t)               , intent(in)  :: source_reference_fe
  integer(ip)                         , intent(in)  :: source_n_face_id ! Local n-face ID
  integer(ip)                         , intent(in)  :: target_n_face_id ! Local n-face ID
  logical :: hex_nedelec_reference_fe_check_compatibility_of_n_faces 
  integer(ip) :: idime

  select type(source_reference_fe)
     class is(hex_nedelec_reference_fe_t)
        ! Get number nodes per dimension
     assert (target_reference_fe%num_dims == source_reference_fe%num_dims)
     do idime = 1, target_reference_fe%num_dims
        assert (target_reference_fe%order_vector(idime) == source_reference_fe%order_vector(idime))
     end do
     assert (target_reference_fe%get_n_face_dim(target_n_face_id) == source_reference_fe%get_n_face_dim(source_n_face_id))
     assert (target_reference_fe%get_n_face_dim(target_n_face_id) < 3)
     assert (target_reference_fe%num_field_components == source_reference_fe%num_field_components)
     assert (target_reference_fe%get_num_own_dofs_n_face(target_n_face_id) == source_reference_fe%get_num_own_dofs_n_face(source_n_face_id))
     hex_nedelec_reference_fe_check_compatibility_of_n_faces = .true.
     class default
     hex_nedelec_reference_fe_check_compatibility_of_n_faces = .false.
  end select

end function hex_nedelec_reference_fe_check_compatibility_of_n_faces

!==================================================================================================
function hex_nedelec_reference_fe_get_characteristic_length ( this )
  implicit none 
  class(hex_nedelec_reference_fe_t), intent(in) :: this 
  real(rp)  :: hex_nedelec_reference_fe_get_characteristic_length 
  hex_nedelec_reference_fe_get_characteristic_length =  2.0_rp
end function hex_nedelec_reference_fe_get_characteristic_length

!**************************************************************************************************
! Deferred TBP implementors from nedelec_reference_fe_t
!**************************************************************************************************
subroutine hex_nedelec_reference_fe_fill (this)
  implicit none
  class(hex_nedelec_reference_fe_t), intent(inout) :: this
  integer(ip) :: c, i, j, dimension
  integer(ip) :: column, inode, idime, inode_scalar, num_columns
  ! Here we put a k-1,k,k order_vector, which is the one for the x-component.
  ! In fill_vector, we use order_vector (and other arrays) using a permutation 
  this%order_vector = this%order
  this%order_vector(1) = this%order-1
  call this%fill_vector()  
end subroutine hex_nedelec_reference_fe_fill

!==================================================================================================
subroutine hex_nedelec_reference_fe_fill_quadrature ( this, quadrature ) 
  implicit none
  class(hex_nedelec_reference_fe_t), intent(in)    :: this
  type(quadrature_t)               , intent(inout) :: quadrature
  call quadrature%fill_hex_gauss_legendre()
end subroutine hex_nedelec_reference_fe_fill_quadrature

!==================================================================================================
subroutine hex_nedelec_reference_fe_fill_interpolation( this, quadrature, interpolation, order_vector )
  implicit none
  class(hex_nedelec_reference_fe_t), intent(in) :: this
  type(quadrature_t)              , intent(in)         :: quadrature
  type(interpolation_t)           , intent(inout)      :: interpolation    
  integer(ip)        , optional   , intent(in)         :: order_vector(SPACE_DIM)
  
  integer(ip)           :: i, j, idime, jdime, qpoint
  type(interpolation_t) :: interpolation_pre_basis
  
  if ( .not. this%basis_changed ) then
    call this%fill_interpolation_pre_basis ( quadrature, interpolation, order_vector )
  else
    call interpolation_pre_basis%clone(interpolation)
    call this%fill_interpolation_pre_basis ( quadrature, interpolation_pre_basis, order_vector ) 
    call this%apply_change_basis_matrix_to_interpolation ( interpolation_pre_basis, interpolation )
    call interpolation_pre_basis%free()
  end if
  
end subroutine hex_nedelec_reference_fe_fill_interpolation

! This subroutine fills the interpolation using the polynomial_names machinery
!==================================================================================================
subroutine hex_nedelec_reference_fe_fill_interpolation_pre_basis( this, quadrature, interpolation, order_vector )
  implicit none
  class(hex_nedelec_reference_fe_t), intent(in) :: this
  type(quadrature_t)              , intent(in)         :: quadrature
  type(interpolation_t)           , intent(inout)      :: interpolation    
  integer(ip)        , optional   , intent(in)         :: order_vector(SPACE_DIM)
  
  type(tensor_product_polynomial_space_t) :: tensor_product_polynomial_space
  type(polynomial_basis_t)    :: polynomial_1D_basis(SPACE_DIM)
  type(lagrange_polynomial_t)             :: polynomial_1D

  real(rp), allocatable :: tensor_product_values(:)
  real(rp), allocatable :: tensor_product_gradients(:,:)
  integer(ip) :: order_vector_(SPACE_DIM)
  integer(ip) :: perm_order_vector_(SPACE_DIM)
  integer(ip) :: idime, jdime, q_point, i, c, aux

  ! Initialize both shape functions and derivatives to zero.
  ! This initialization step CANNOT be avoided. Shape-functions
  ! in the RT pre-basis have only one non-zero component. Clients
  ! of type(interpolation_t) may consume all components of each shape 
  ! function (even if they have a single non-zero component), therefore 
  ! they must be initialized provided the code below just computes the
  ! single non-zero component of each shape function
  interpolation%shape_functions   = 0.0_rp
  interpolation%shape_derivatives = 0.0_rp

  if ( present(order_vector) ) then
    order_vector_ = order_vector
  else
    order_vector_  = this%order_vector
  end if

  do idime=1, interpolation%num_dims
     perm_order_vector_   = order_vector_ 

     ! Swap positions 1 and idime of perm_order_vector_
     aux                       = perm_order_vector_(1)
     perm_order_vector_(1)     = perm_order_vector_(idime)
     perm_order_vector_(idime) = aux 

     ! Generate a basis of 1D polynomials per coordinate direction 
     ! (X,Y,(Z)) with order given by order_vector_(SPACE_DIM)
     do jdime=1,interpolation%num_dims
       call polynomial_1D%generate_basis( perm_order_vector_(jdime), & 
                                          polynomial_1D_basis(jdime) )
     end do

     ! Create an scalar-valued space of  2/3D polynomials from the 
     ! tensor product of the basis of 1D polynomials
     call tensor_product_polynomial_space%create(interpolation%num_dims,&
                                                 polynomial_1D_basis)
     call tensor_product_polynomial_space%fill( quadrature%get_coordinates() )

     call memalloc(tensor_product_polynomial_space%get_num_polynomials(),&
                   tensor_product_values,__FILE__,__LINE__)
     call memalloc(SPACE_DIM,&
                   tensor_product_polynomial_space%get_num_polynomials(),&
                   tensor_product_gradients,__FILE__,__LINE__)

     do q_point=1, quadrature%get_num_quadrature_points()
        c=(idime-1)*tensor_product_polynomial_space%get_num_polynomials()
        call tensor_product_polynomial_space%evaluate(q_point, &
                                                      tensor_product_values, &
                                                      tensor_product_gradients)
        do i=1, tensor_product_polynomial_space%get_num_polynomials()
           c = c+1
           interpolation%shape_functions(idime,c,q_point) = tensor_product_values(i)
           do jdime = 1,interpolation%num_dims
              interpolation%shape_derivatives(idime,jdime,c,q_point) = tensor_product_gradients(jdime,i)
           end do
        end do
     end do
  
   
     ! Free auxiliary memory
     do jdime=1,interpolation%num_dims
        call polynomial_1D_basis(jdime)%free()
     end do
     call tensor_product_polynomial_space%free()
     call polynomial_1D%free()
  
     call memfree(tensor_product_values,__FILE__,__LINE__)
     call memfree(tensor_product_gradients,__FILE__,__LINE__)
  end do

end subroutine hex_nedelec_reference_fe_fill_interpolation_pre_basis

!==================================================================================================
subroutine hex_nedelec_reference_fe_fill_interp_restricted_to_facet( this, local_quadrature, & 
     & facet_lid, subfacet_lid, facet_interpolation )
  implicit none
  class(hex_nedelec_reference_fe_t), intent(in)    :: this
  type(quadrature_t)                      , intent(in)    :: local_quadrature
  integer(ip)                             , intent(in)    :: facet_lid
  integer(ip)                             , intent(in)    :: subfacet_lid
  type(interpolation_t)                   , intent(inout) :: facet_interpolation
  type(quadrature_t) :: aux_quadrature
  integer(ip) :: c,idime,iface,n_face_index
  
  ! Exit if attempts to fill the interpolation of a subface
  if ( subfacet_lid >= 0 ) return
  
  ! Add offset to have same numbering as in polytope
  n_face_index = facet_lid+this%get_first_facet_id()-1
  
  ! Create a d-dimensional quadrature as follows
  call aux_quadrature%create( this%num_dims, local_quadrature%num_quadrature_points )
  c = 0
  do idime = 0,this%num_dims-1
     if ( this%polytope%n_face_dir_is_fixed( n_face_index, idime ) == 1 ) then
        c = c+1
        aux_quadrature%coordinates(idime+1,:) = local_quadrature%coordinates(c,:)
     else if ( this%polytope%n_face_dir_coordinate( n_face_index, idime ) == 0 ) then
        aux_quadrature%coordinates(idime+1,:) = -1.0_rp
     else
        aux_quadrature%coordinates(idime+1,:) = 1.0_rp
     end if
  end do
  call this%fill_interpolation( aux_quadrature, facet_interpolation ) 
  call aux_quadrature%free()
end subroutine hex_nedelec_reference_fe_fill_interp_restricted_to_facet

!==================================================================================================
subroutine hex_nedelec_reference_fe_fill_interpolation_restricted_to_edget( this, local_edge_id, local_quadrature, edget_interpolation )
  implicit none
  class(hex_nedelec_reference_fe_t)       , intent(in)    :: this
  integer(ip)                             , intent(in)    :: local_edge_id
  type(quadrature_t)                      , intent(in)    :: local_quadrature
  type(interpolation_t)                   , intent(inout) :: edget_interpolation
  type(quadrature_t) :: aux_quadrature
  integer(ip) :: c, idime, n_face_index
  
  ! Add offset to have same numbering as in polytope
  n_face_index = local_edge_id+this%get_first_n_face_id_of_dim(1)-1
  
  ! Create a d-dimensional quadrature as follows
  call aux_quadrature%create( this%num_dims, local_quadrature%num_quadrature_points )
  c = 0
  do idime = 0,this%num_dims-1
     if ( this%polytope%n_face_dir_is_fixed( n_face_index, idime ) == 1 ) then
        c = c+1
        aux_quadrature%coordinates(idime+1,:) = local_quadrature%coordinates(c,:)
     else if ( this%polytope%n_face_dir_coordinate( n_face_index, idime ) == 0 ) then
        aux_quadrature%coordinates(idime+1,:) = -1.0_rp
     else
        aux_quadrature%coordinates(idime+1,:) = 1.0_rp
     end if
  end do
  call this%fill_interpolation( aux_quadrature, edget_interpolation ) 
  call aux_quadrature%free()
end subroutine hex_nedelec_reference_fe_fill_interpolation_restricted_to_edget

subroutine hex_nedelec_reference_fe_change_basis(this) 
  implicit none
  class(hex_nedelec_reference_fe_t), intent(inout) ::  this
  
  ! Local variables required by edge boundary moments integration
  type(hex_lagrangian_reference_fe_t) :: fe_1D
  type(quadrature_t)                  :: edge_quadrature
  type(interpolation_t)               :: interpolation_1D, edget_interpolation
  type(edge_map_t)                    :: edge_map
  type(point_t), pointer              :: edge_map_coordinates(:)
  real(rp)                            :: shape_test
  type(vector_field_t)                :: tangent
  
  ! Local variables required by face boundary moments integration
  type(hex_nedelec_reference_fe_t)    :: fe_2D
  type(quadrature_t)                  :: facet_quadrature
  type(interpolation_t)               :: interpolation_2D, facet_interpolation
  type(facet_map_t)                    :: facet_map
  type(point_t), pointer              :: facet_map_coordinates(:)
  type(vector_field_t)                :: normal
  type(vector_field_t)                :: cross_product_v_shape_trial_normal
  type(vector_field_t)                :: cross_product_v_shape_trial_normal_proj
  type(vector_field_t)                :: col_jacobian
  
  ! Local variables required by interior moments integration
  type(quadrature_t)                  :: cell_quadrature
  type(interpolation_t)               :: reduced_order_cell_interpolation, cell_interpolation
  type(point_t), pointer              :: cell_map_coordinates(:)
  type(cell_map_t)                    :: cell_map
  integer(ip)                         :: reduced_order_vector(SPACE_DIM)
  integer(ip)                         :: num_interior_moments
  type(vector_field_t)                :: v_shape_test
  
  ! Local variables requires for both boundary/interior moments integration
  type(hex_lagrangian_reference_fe_t) :: d_fe_geo
  type(vector_field_t)                :: v_shape_trial
  integer(ip)                         :: ishape, jshape, qpoint
  real(rp)                            :: factor
  integer(ip)                         :: idime
  
  ! Local variable required for normals reorientation calcs (required to update_facet_map)
  integer(ip)                         :: facet_lid
  
  ! Rest of local variables
  integer(ip)                         :: c, d, i, j, istat
  integer(ip)                         :: num_rows_own_node_permutations
  integer(ip)                         :: num_cols_own_node_permutations
  type(list_iterator_t)               :: own_dofs_iterator
  
  ! Facets and vertices within reference cell traversal-related data structures
  type(list_iterator_t)               :: vertex_iterator
  integer(ip)                         :: vertex_within_cell_id
  integer(ip)                         :: vertex_within_edge_id
  integer(ip)                         :: vertex_within_face_id
  integer(ip)                         :: edge_id, facet_id, n_face_id
  
  assert ( .not. this%basis_changed )
  assert ( this%conformity .and. this%continuity )
 
  ! Allocate change_basis_matrix
  if ( allocated(this%change_basis_matrix) ) & 
       call memfree(this%change_basis_matrix, __FILE__, __LINE__)    
      
  call memalloc(this%num_shape_functions, &
                this%num_shape_functions, &
                this%change_basis_matrix, &
                __FILE__, &
                __LINE__)
  
 ! Create a 1D scalar lagrangian reference FE of order k-1
 call fe_1D%create(topology          = this%get_topology(), &
                   num_dims = 1, &
                   order             = this%order-1, &
                   field_type        = field_type_scalar, &
                   conformity        = .true. )


 ! Create a d-dim scalar lagrangian reference FE of order 1 (geometry)
 call d_fe_geo%create(topology          = this%get_topology(), &
                      num_dims = this%num_dims, &
                      order             = 1, &
                      field_type        = field_type_scalar, &
                      conformity        = .true. )

 call this%create_edge_quadrature( edge_quadrature )
 call fe_1D%create_interpolation( edge_quadrature, interpolation_1D ) 
 call edge_map%create( edge_quadrature, d_fe_geo )

 ! Initialize change of basis matrix
 this%change_basis_matrix = 0.0_rp
  
 d = 0
 do edge_id = this%get_first_n_face_id_of_dim(1), & 
               this%get_first_n_face_id_of_dim(1) + this%get_num_n_faces_of_dim(1)-1
  
     ! Get face map coordinates
     edge_map_coordinates => edge_map%get_coordinates()
   
     ! Extract coordinates of the vertices within the face
     vertex_within_edge_id = 1
     vertex_iterator = this%vertices_n_face%create_iterator(edge_id)
     do while ( .not. vertex_iterator%is_upper_bound () )
        vertex_within_cell_id = vertex_iterator%get_current()
        call edge_map_coordinates(vertex_within_edge_id)%init(d_fe_geo%nodal_quadrature%coordinates(:, vertex_within_cell_id))
        call vertex_iterator%next()
        vertex_within_edge_id = vertex_within_edge_id + 1
     end do

     ! Update edge_map on current edge
     call edge_map%update(local_edge_id = edge_id - d_fe_geo%get_first_n_face_id_of_dim(1)+1, &
                          reference_fe  = d_fe_geo, &
                          quadrature    = edge_quadrature ) 
     
     call this%create_interpolation_restricted_to_edget (edge_id - d_fe_geo%get_first_n_face_id_of_dim(1)+1, &
                                          edge_quadrature, &
                                          edget_interpolation )
     
     ! Integrate edge boundary moments int_edge(u.t q), q \in P_k-1
     do qpoint = 1, edge_quadrature%num_quadrature_points
        factor = edge_map%get_det_jacobian(qpoint) * edge_quadrature%get_weight(qpoint)
        call edge_map%get_tangent(qpoint, tangent)

        do ishape=1, interpolation_1D%num_shape_functions
           call fe_1D%get_value(interpolation_1D, ishape, qpoint, shape_test)
           do jshape=1, edget_interpolation%num_shape_functions
              call this%get_value(edget_interpolation, jshape, qpoint, v_shape_trial)
              this%change_basis_matrix(d+ishape,jshape) = this%change_basis_matrix(d+ishape,jshape) &
                                                          + shape_test * v_shape_trial * tangent * factor
           end do
        end do
     end do
     d = d + interpolation_1D%num_shape_functions
 end do

 if ( this%num_dims == 3 .and. this%order > 1 ) then
    ! Create a 2D nedelec reference FE of order k-1.
    ! Not that conformity = .false. as we are interested into 
    ! pre_basis_interpolation. Also because of computational reasons,
    ! with conformity = .false. we avoid evaluating the change of
    ! basis matrix (i.e., evaluation of functionals)
    call fe_2D%create(topology          = this%get_topology(), &
                      num_dims = 2, &
                      order             = this%order-1, &
                      field_type        = field_type_vector, &
                      conformity        = .false. )
    
    call this%create_facet_quadrature( facet_quadrature )
    call fe_2D%create_interpolation( facet_quadrature, interpolation_2D )
    call facet_map%create( facet_quadrature, d_fe_geo )
 
    do facet_id = this%get_first_facet_id(), this%get_first_facet_id() + this%get_num_facets()-1
  
     ! Get face map coordinates
     facet_map_coordinates => facet_map%get_coordinates()
   
     ! Extract coordinates of the vertices within the face
     vertex_within_face_id = 1
     vertex_iterator = this%vertices_n_face%create_iterator(facet_id)
     do while ( .not. vertex_iterator%is_upper_bound () )
        vertex_within_cell_id = vertex_iterator%get_current()
        call facet_map_coordinates(vertex_within_face_id)%init(d_fe_geo%nodal_quadrature%coordinates(:, vertex_within_cell_id))
        call vertex_iterator%next()
        vertex_within_face_id = vertex_within_face_id + 1
     end do

     ! Update facet_map on current face
     facet_lid = facet_id - d_fe_geo%get_first_facet_id()+1
     call facet_map%update(reorientation_factor   = d_fe_geo%get_normal_orientation_factor(facet_lid), &
                           quadrature             = facet_quadrature ) 
     
     call this%create_interpolation_restricted_to_facet ( facet_id - d_fe_geo%get_first_facet_id()+1, &
                                           -1, facet_quadrature, facet_interpolation )
     
     call cross_product_v_shape_trial_normal_proj%init(0.0_rp)
     
     ! Integrate face boundary moments int_Face(u x n q), q \in Q_k-2,k-1 x Q_k-1,k-2
     do qpoint = 1, facet_quadrature%num_quadrature_points
        factor = facet_map%get_det_jacobian(qpoint) * facet_quadrature%get_weight(qpoint)
        call facet_map%get_normal(qpoint, normal)
        
        !!!!!!!!!!!
        !! IMPORTANT NOTE: trick to have normals associated to each pair of faces pointing
        !!                 to the same direction. This only works provided we have an "oriented"
        !!                 triangulation.
        !!!!!!!!!!!
        normal = (-1.0_rp)**(this%orientation%a(facet_id)) * normal
        
        do ishape=1, interpolation_2D%num_shape_functions
           call fe_2D%get_value(interpolation_2D, ishape, qpoint, v_shape_test)
           do jshape=1, facet_interpolation%num_shape_functions
              call this%get_value(facet_interpolation, jshape, qpoint, v_shape_trial)
              
              ! Compute u x n
              cross_product_v_shape_trial_normal = cross_product(v_shape_trial,normal)
              
              ! Multiply the transpose of the jacobian with normalized columns by u x n
              ! This is required to transform u x n in a vector contained within the face
              do i=1,this%num_dims-1
                call facet_map%get_jacobian_column(i,qpoint,col_jacobian)
			            	col_jacobian = col_jacobian * (1.0_rp/col_jacobian%nrm2()) 
                call cross_product_v_shape_trial_normal_proj%set(i, col_jacobian*cross_product_v_shape_trial_normal)
              end do
              
              this%change_basis_matrix(d+ishape,jshape) = this%change_basis_matrix(d+ishape,jshape) &
                                                         + cross_product_v_shape_trial_normal_proj * v_shape_test * factor
           end do
        end do
     end do
     d = d + interpolation_2D%num_shape_functions
  end do
 end if
 
 
 if ( this%num_dims == 2 ) then
   ! Q_{k-1,k-2} x Q_{k-2,k-1}
   reduced_order_vector(1)   = this%order_vector(1)
   reduced_order_vector(2)   = this%order_vector(2)-2
 else if ( this%num_dims == 3 ) then
   ! Q_{k-1,k-2,k-2} x Q_{k-2,k-1,k-2} x Q_{k-2,k-2,k-1}
   reduced_order_vector(1)   = this%order_vector(1)
   reduced_order_vector(2:3) = this%order_vector(2:3)-2
 end if
  
  
 ! compute number shape functions for reduced order element
 num_interior_moments = 1
 do idime = 1, this%num_dims
     num_interior_moments = num_interior_moments*(reduced_order_vector(idime)+1)
 end do
 num_interior_moments = num_interior_moments*this%num_dims
  
 if ( num_interior_moments > 0 ) then 
    ! create cell quadrature
    call this%create_quadrature(cell_quadrature)

    ! create reduced order interpolation
    call reduced_order_cell_interpolation%create( this%num_dims, &
         num_interior_moments, &
         cell_quadrature%num_quadrature_points, &
         num_entries_symmetric_tensor=0, &
         compute_hessian = .false. )

    call this%fill_interpolation_pre_basis( cell_quadrature, reduced_order_cell_interpolation, reduced_order_vector)

    ! Create PRE-BASIS cell-interpolation from this RT REF FE 
    call this%create_interpolation( cell_quadrature, cell_interpolation )

    ! Create + Update cell map 
    call cell_map%create(cell_quadrature, d_fe_geo )
    cell_map_coordinates => cell_map%get_coordinates()
    do vertex_within_cell_id=1,d_fe_geo%get_num_vertices()
       call cell_map_coordinates(vertex_within_cell_id)%init(d_fe_geo%nodal_quadrature%coordinates(:, vertex_within_cell_id))
    end do
    call cell_map%update(cell_quadrature)

    ! ! Integration of interior moments int_K (u.q), q \in Q_{k-1,k-2,k-2} x Q_{k-2,k-1,k-2} x Q_{k-2,k-2,k-1}     
    do qpoint = 1, cell_quadrature%num_quadrature_points
       factor = cell_map%get_det_jacobian(qpoint) * cell_quadrature%get_weight(qpoint)
       do ishape=1, reduced_order_cell_interpolation%num_shape_functions
          call this%get_value(reduced_order_cell_interpolation, ishape, qpoint, v_shape_test)
          do jshape=1, cell_interpolation%num_shape_functions
             call this%get_value(cell_interpolation, jshape, qpoint, v_shape_trial)
             this%change_basis_matrix(d+ishape,jshape) = this%change_basis_matrix(d+ishape,jshape) + &
                  v_shape_test * v_shape_trial * factor
          end do
       end do
    end do
 end if
   
  ! Invert change_basis_matrix
  call this%invert_change_basis_matrix()
  
 ! Transform type(list_t) member variables of this to reflect change of basis
  call this%own_dofs_n_face%create( this%num_n_faces + 1 )

  if ( this%conformity .and. this%continuity ) then
     do edge_id = this%get_first_n_face_id_of_dim(1), & 
          this%get_first_n_face_id_of_dim(1) + this%get_num_n_faces_of_dim(1)-1
        call this%own_dofs_n_face%sum_to_pointer_index(edge_id, interpolation_1D%num_shape_functions )           
     end do

     if (this%num_dims == 3 .and. this%order > 1) then
        do facet_id = this%get_first_facet_id(), this%get_first_facet_id() + this%get_num_facets()-1
           call this%own_dofs_n_face%sum_to_pointer_index(facet_id, interpolation_2D%num_shape_functions )
        end do
     end if

     call this%own_dofs_n_face%sum_to_pointer_index(this%num_n_faces + 1, num_interior_moments)
     call this%own_dofs_n_face%calculate_header()
     call this%own_dofs_n_face%allocate_list_from_pointer()

     c=1
     do idime=1, this%num_dims
        do n_face_id = this%get_first_n_face_id_of_dim(idime), & 
             this%get_first_n_face_id_of_dim(idime) + this%get_num_n_faces_of_dim(idime)-1
           own_dofs_iterator = this%own_dofs_n_face%create_iterator(n_face_id)
           do while ( .not. own_dofs_iterator%is_upper_bound() )
              call own_dofs_iterator%set_current(c)
              c = c + 1
              call own_dofs_iterator%next()
           end do
           call own_dofs_iterator%next()
        end do
     end do

  else 

     call this%own_dofs_n_face%sum_to_pointer_index( this%own_dofs_n_face%get_num_pointers(), this%num_shape_functions)
     call this%own_dofs_n_face%calculate_header()
     call this%own_dofs_n_face%allocate_list_from_pointer()
     own_dofs_iterator = this%own_dofs_n_face%create_iterator()
     i=1
     do while(.not. own_dofs_iterator%is_upper_bound())
        call own_dofs_iterator%set_current(i)
        call own_dofs_iterator%next()
        i = i + 1 
     enddo

  end if
  this%dofs_n_face      = this%own_dofs_n_face
 
  
  ! Re-define own_node_permutations-related stuff
  if ( allocated(this%num_rotations_x_dim) )  call memfree (this%num_rotations_x_dim,__FILE__,__LINE__)
  if ( allocated(this%num_orientations_x_dim) ) call memfree (this%num_orientations_x_dim,__FILE__,__LINE__)
  if (allocated(this%own_dof_permutations)) then
     do i = 1, this%num_dims-1
        call this%own_dof_permutations(i)%free()
     end do
     deallocate(this%own_dof_permutations, stat=istat)
     check(istat==0)
  end if
  
  call memalloc (this%num_dims-1,this%num_rotations_x_dim,__FILE__,__LINE__)
  call memalloc (this%num_dims-1,this%num_orientations_x_dim,__FILE__,__LINE__)
  
  do idime = 1, this%num_dims-1
     call this%node_array_vector(1)%compute_num_rot_and_perm( idime, &
                                                              this%polytope, &
                                                              this%num_rotations_x_dim(idime), &
                                                              this%num_orientations_x_dim(idime))
  end do
  
  allocate(this%own_dof_permutations(this%num_dims-1)) 
  do idime = 1, this%num_dims-1
    num_cols_own_node_permutations = this%num_rotations_x_dim(idime)*this%num_orientations_x_dim(idime)
    if ( idime == 1 ) then
      num_rows_own_node_permutations = interpolation_1D%num_shape_functions
    else if ( idime == 2 ) then
	  if ( this%order == 1 ) then 
	  num_rows_own_node_permutations = 0
	  else 
      num_rows_own_node_permutations = interpolation_2D%num_shape_functions
	  end if 
    end if   
  
   call this%own_dof_permutations(idime)%create(num_rows_own_node_permutations, &
                                                 num_cols_own_node_permutations)

   
   ! Warning: temporarily initializing this%own_node_permutations(num_dims-1)%a(:)
   !          to the identity. This only will work provided that for all faces F within the 
   !          triangulation, F is oriented the same way in both cells around it
   if (num_rows_own_node_permutations > 0) then
     do j=1, num_cols_own_node_permutations
       do i=1, num_rows_own_node_permutations
         this%own_dof_permutations(idime)%a(i,j) = i
       end do
     end do
   end if
 end do  
  

 ! Flag change of basis
 this%basis_changed = .true.

  
 ! Free all local data structures required for edge moments integration
 call d_fe_geo%free()
 call fe_1D%free()
 call edge_quadrature%free()
 call interpolation_1D%free()
 call edget_interpolation%free()
 call edge_map%free()
 
 ! Free all local data structures required for face moments integration
 call fe_2D%free()
 call facet_quadrature%free()
 call interpolation_2D%free()
 call facet_interpolation%free()
 call facet_map%free()
 
 ! Free all  
 call cell_quadrature%free()
 call reduced_order_cell_interpolation%free()
 call cell_interpolation%free()
 call cell_map%free()
  
end subroutine hex_nedelec_reference_fe_change_basis 

! @sbadia: It could be replaced by an anisotropic quadrature
!==================================================================================================
function hex_nedelec_reference_fe_compute_num_quadrature_points( this, degree, dimension )
  implicit none
  class(hex_nedelec_reference_fe_t), intent(in)    :: this
  integer(ip)                         , intent(in)    :: degree
  integer(ip)                         , intent(in)    :: dimension
  integer(ip) :: hex_nedelec_reference_fe_compute_num_quadrature_points
  hex_nedelec_reference_fe_compute_num_quadrature_points = int(ceiling(real(degree + 1,rp)/2.0_rp)**real(dimension,rp),ip)
 end function hex_nedelec_reference_fe_compute_num_quadrature_points
 
 !==================================================================================================
subroutine hex_nedelec_reference_fe_fill_qpoints_permutations(this, quadrature, qpoints_perm)
  implicit none
  class(hex_nedelec_reference_fe_t), intent(in)    :: this
  type(quadrature_t)                  , intent(in)    :: quadrature
  type(allocatable_array_ip2_t)       , intent(inout) :: qpoints_perm
  integer(ip)    :: ndime, n_q_points, order
  
  ndime      = quadrature%get_num_dims()
  n_q_points = quadrature%get_num_quadrature_points()
  order      = int(real(n_q_points,rp)**(1.0_rp/real(ndime,rp)),ip)+1
  
  call this%node_array%fill_permutations( order, &
                                          ndime, &
                                          this%polytope, &
                                          qpoints_perm )
  
end subroutine hex_nedelec_reference_fe_fill_qpoints_permutations

 !==================================================================================================
subroutine hex_nedelec_reference_fe_evaluate_vector_function_moments(this, cell_map, vector_function, dof_values)
  implicit none
  class(hex_nedelec_reference_fe_t), intent(in)    :: this
  type(cell_map_t)             , intent(in)        :: cell_map
  class(vector_function_t)     , intent(in)        :: vector_function
  real(rp) , allocatable       , intent(inout)     :: dof_values(:) 

  type(point_t)                , pointer    :: coordinates(:)
  type(hex_lagrangian_reference_fe_t)       :: d_fe_geo

  ! Edges 
  type(hex_lagrangian_reference_fe_t) :: fe_1D
  type(interpolation_t)               :: interpolation_1D
  type(quadrature_t)                  :: edge_quadrature
  type(edge_map_t)                    :: edge_map
  type(vector_field_t)                :: tangent
  type(list_iterator_t)               :: vertex_iterator 
  type(point_t), pointer              :: edge_map_coordinates(:)
  integer(ip)                         :: edge_id  
  integer(ip)                         :: vertex_within_edge_id  

  ! Faces 
  type(hex_nedelec_reference_fe_t)    :: fe_2D
  type(quadrature_t)                  :: facet_quadrature
  type(interpolation_t)               :: interpolation_2D, facet_interpolation
  type(facet_map_t)                   :: facet_map
  type(point_t), pointer              :: facet_map_coordinates(:)
  type(vector_field_t)                :: normal
  type(vector_field_t)                :: cross_product_function_normal
  type(vector_field_t)                :: cross_product_function_normal_proj
  type(vector_field_t)                :: col_jacobian
  integer(ip)                         :: facet_id, facet_lid
  integer(ip)                         :: vertex_within_facet_id

  ! Local variables required by interior moments integration
  type(hex_raviart_thomas_reference_fe_t) :: fe_raviart_thomas 
  type(quadrature_t)                  :: cell_quadrature
  type(interpolation_t)               :: reduced_order_cell_interpolation
  type(interpolation_t)               :: cell_interpolation
  integer(ip)                         :: reduced_order_vector(SPACE_DIM)
  integer(ip)                         :: num_interior_moments
  type(vector_field_t)                :: v_shape_test

  ! Common integration 
  integer(ip)                         :: vertex_within_cell_id
  real(rp)                            :: shape_test, factor 
  integer(ip)                         :: idime, ishape
  integer(ip)                         :: qpoint, num_quad_points 
  type(vector_field_t), allocatable   :: function_values(:) 
  type(point_t), pointer              :: quad_points_coordinates(:)

  integer(ip)  :: d, istat 

  coordinates => cell_map%get_coordinates()

  ! Create a 1D scalar lagrangian reference FE of order k-1
  call fe_1D%create(topology          = this%get_topology(), &
       num_dims          = 1, &
       order             = this%order-1, &
       field_type        = field_type_scalar, &
       conformity        = .true. )

  ! Create a d-dim scalar lagrangian reference FE of order 1 (geometry)
  call d_fe_geo%create(topology    = this%get_topology(),    &
       num_dims    = this%num_dims,          &
       order       = 1,                      &
       field_type  = field_type_scalar,      &
       conformity  = .false. )

  call this%create_edge_quadrature( edge_quadrature )
  call edge_map%create( edge_quadrature, d_fe_geo )
  call fe_1D%create_interpolation( edge_quadrature, interpolation_1D )

  num_quad_points  = edge_quadrature%get_num_quadrature_points()
  allocate (function_values(num_quad_points), stat=istat); check(istat==0)

  d = 0
  dof_values = 0.0_rp 
  do edge_id = this%get_first_n_face_id_of_dim(1), this%get_first_n_face_id_of_dim(1) + this%get_num_n_faces_of_dim(1)-1

     edge_map_coordinates => edge_map%get_coordinates()   

     vertex_within_edge_id = 1
     vertex_iterator = this%vertices_n_face%create_iterator(edge_id)
     do while ( .not. vertex_iterator%is_upper_bound () )
        vertex_within_cell_id = vertex_iterator%get_current()
        do idime=1,this%num_dims  
           call edge_map_coordinates(vertex_within_edge_id)%set(idime, coordinates(vertex_within_cell_id)%get(idime) )
        end do
        call vertex_iterator%next()
        vertex_within_edge_id = vertex_within_edge_id + 1
     end do

     ! Update edge_map on current edge
     call edge_map%update(local_edge_id = edge_id - this%get_first_n_face_id_of_dim(1)+1, &
          reference_fe  = this,                                           &
          quadrature    = edge_quadrature )

     ! Extract quadrature points coordinates 
     quad_points_coordinates => edge_map%get_quadrature_points_coordinates()

     ! Fill vector function values restricted to edge 
     call vector_function%get_values_set(quad_points_coordinates, function_values)

     ! Integrate edge boundary moments int_edge(f.tau q), q \in P_k-1
     do qpoint = 1, num_quad_points
        factor = edge_map%get_det_jacobian(qpoint) * edge_quadrature%get_weight(qpoint)
        call edge_map%get_tangent(qpoint, tangent)
        do ishape=1, interpolation_1D%num_shape_functions
           call fe_1D%get_value(interpolation_1D, ishape, qpoint, shape_test)
           dof_values(d+ishape) = dof_values(d+ishape) + 1.0_rp / tangent%nrm2() * function_values(qpoint) * shape_test * tangent * factor 
        end do
     end do

     d = d + interpolation_1D%num_shape_functions
  end do

  deallocate (function_values, stat=istat); check(istat==0)

  if ( this%num_dims == 3 .and. this%order > 1 ) then

     call fe_2D%create(topology   = this%get_topology(), &
          num_dims   = 2,                   &
          order      = this%order-1,        &
          field_type = field_type_vector,   &
          conformity = .false. )

     call this%create_facet_quadrature( facet_quadrature )
     call fe_2D%create_interpolation( facet_quadrature, interpolation_2D )
     call facet_map%create( facet_quadrature, d_fe_geo )

     num_quad_points  = facet_quadrature%get_num_quadrature_points()
     allocate (function_values(num_quad_points), stat=istat); check(istat==0)

     do facet_id = this%get_first_facet_id(), this%get_first_facet_id() + this%get_num_facets()-1

        ! Get face map coordinates
        facet_map_coordinates => facet_map%get_coordinates()

        ! Extract coordinates of the vertices within the face
        vertex_within_facet_id = 1
        vertex_iterator = this%vertices_n_face%create_iterator(facet_id)
        do while ( .not. vertex_iterator%is_upper_bound () )
           vertex_within_cell_id = vertex_iterator%get_current()
           do idime=1,this%num_dims 
              call facet_map_coordinates(vertex_within_facet_id)%set(idime, coordinates(vertex_within_cell_id)%get(idime) )
           end do
           call vertex_iterator%next()
           vertex_within_facet_id = vertex_within_facet_id + 1
        end do

        ! Update facet_map on current face
        facet_lid = facet_id - d_fe_geo%get_first_facet_id()+1
        call facet_map%update(reorientation_factor   = d_fe_geo%get_normal_orientation_factor(facet_lid), &
             quadrature             = facet_quadrature ) 

        ! Extract quadrature points coordinates 
        quad_points_coordinates => facet_map%get_quadrature_points_coordinates()

        ! Fill vector function values restricted to edge 
        call vector_function%get_values_set(quad_points_coordinates, function_values)

        ! Is it possible to obtain test functions with this idea? 
        !call this%create_interpolation_restricted_to_facet ( facet_id - d_fe_geo%get_first_facet_id()+1, &
        !                                                     -1, facet_quadrature, facet_interpolation )

        call cross_product_function_normal_proj%init(0.0_rp)

        ! Integrate face boundary moments int_Face(u x n q), q \in Q_k-2,k-1 x Q_k-1,k-2
        do qpoint = 1, facet_quadrature%num_quadrature_points
           factor = facet_map%get_det_jacobian(qpoint) * facet_quadrature%get_weight(qpoint)
           call facet_map%get_normal(qpoint, normal)   
           normal = (-1.0_rp)**(this%orientation%a(facet_id)) * normal

           ! Compute g x n
           cross_product_function_normal = cross_product(function_values(qpoint),normal)
           do idime=1,this%num_dims-1 
              call facet_map%get_jacobian_column(idime,qpoint,col_jacobian)
              col_jacobian = col_jacobian * (1.0_rp/col_jacobian%nrm2()) 
              call cross_product_function_normal_proj%set(idime, col_jacobian*cross_product_function_normal)
           end do

           do ishape=1, interpolation_2D%num_shape_functions
              call fe_2D%get_value(interpolation_2D, ishape, qpoint, v_shape_test)   

              ! WARNING: Update 2D interpolation for the Nedelec test functions ( J**{-T} will only work in HEX structured meshes! )
              do idime=1, this%num_dims-1
                 call facet_map%get_jacobian_column(idime,qpoint,col_jacobian)
                 call v_shape_test%set(idime, v_shape_test%get(idime)/col_jacobian%nrm2() )
              end do

              dof_values(d+ishape) = dof_values(d+ishape) + cross_product_function_normal_proj * v_shape_test * factor
           end do
        end do
        d = d + interpolation_2D%num_shape_functions
     end do

     call facet_map%free() 
     call facet_quadrature%free() 
     call fe_2D%free() 
     call interpolation_2D%free() 
     deallocate ( function_values , stat=istat); check(istat==0) 
  end if


  if ( this%num_dims == 2 ) then
     ! Q_{k-1,k-2} x Q_{k-2,k-1}
     reduced_order_vector(1)   = this%order_vector(1)
     reduced_order_vector(2)   = this%order_vector(2)-2
  else if ( this%num_dims == 3 ) then
     ! Q_{k-1,k-2,k-2} x Q_{k-2,k-1,k-2} x Q_{k-2,k-2,k-1}
     reduced_order_vector(1)   = this%order_vector(1)
     reduced_order_vector(2:3) = this%order_vector(2:3)-2
  end if


  ! compute number shape functions for reduced order element
  num_interior_moments = 1
  do idime = 1, this%num_dims
     num_interior_moments = num_interior_moments*(reduced_order_vector(idime)+1)
  end do
  num_interior_moments = num_interior_moments*this%num_dims

  if ( num_interior_moments > 0 ) then 

     ! Create a 1D scalar lagrangian reference FE of order k-1
     call fe_raviart_thomas%create(topology          = this%get_topology(), &
          num_dims          = this%num_dims,       &
          order             = this%order-2,        &
          field_type        = field_type_vector,   &
          conformity        = .false. )

     ! create cell quadrature
     call this%create_quadrature(cell_quadrature) 

     num_quad_points  = cell_quadrature%get_num_quadrature_points()
     allocate (function_values(num_quad_points), stat=istat); check(istat==0)

     ! create reduced order interpolation
     call reduced_order_cell_interpolation%create( this%num_dims,                         &
          num_interior_moments,                  &
          cell_quadrature%num_quadrature_points, &
          num_entries_symmetric_tensor=0,        &
          compute_hessian = .false. )

     call this%fill_interpolation_pre_basis( cell_quadrature, reduced_order_cell_interpolation, reduced_order_vector)

     ! Extract quadrature points coordinates 
     quad_points_coordinates => cell_map%get_quadrature_points_coordinates()

     ! Fill vector function values restricted to edge 
     call vector_function%get_values_set(quad_points_coordinates, function_values)

     ! Update interpolation on the physical space 
     call cell_interpolation%clone( reduced_order_cell_interpolation ) 
     call fe_raviart_thomas%apply_cell_map( cell_map, reduced_order_cell_interpolation, cell_interpolation) 

     ! Integration of interior moments int_K (u.q), q \in Q_{k-1,k-2,k-2} x Q_{k-2,k-1,k-2} x Q_{k-2,k-2,k-1}     
     do qpoint = 1, cell_quadrature%num_quadrature_points
        factor = cell_map%get_det_jacobian(qpoint) * cell_quadrature%get_weight(qpoint)
        do ishape=1, reduced_order_cell_interpolation%num_shape_functions
           call this%get_value(cell_interpolation, ishape, qpoint, v_shape_test)
           dof_values(d+ishape) = dof_values(d+ishape) + function_values(qpoint) * v_shape_test * factor
        end do
     end do

     call cell_quadrature%free() 	
     call reduced_order_cell_interpolation%free() 
     call cell_interpolation%free() 
     deallocate (function_values, stat=istat); check(istat==0)
  end if

  ! Free all local data structures required for edge moments integration
  call d_fe_geo%free()
  call fe_raviart_thomas%free()
  call fe_1D%free()
  call edge_quadrature%free()
  call interpolation_1D%free()
  call edge_map%free()		

end subroutine hex_nedelec_reference_fe_evaluate_vector_function_moments

!==================================================================================================
subroutine hex_nedelec_reference_fe_evaluate_boundary_function_moments(this, cell_map, vef_lid, function_scalar_components, dof_values, time)
  class(hex_nedelec_reference_fe_t)       , intent(in)    :: this
  class(cell_map_t)                       , intent(in)    :: cell_map
  integer(ip)                             , intent(in)    :: vef_lid 
  type(p_scalar_function_t), allocatable  , intent(in)    :: function_scalar_components(:)
  real(rp) , allocatable                  , intent(inout) :: dof_values(:)  
  real(rp) , optional                     , intent(in)    :: time 

  type(point_t)                , pointer    :: coordinates(:)
  type(hex_lagrangian_reference_fe_t)       :: d_fe_geo

  ! Edges 
  type(hex_lagrangian_reference_fe_t) :: fe_1D
  type(interpolation_t)               :: interpolation_1D
  type(quadrature_t)                  :: edge_quadrature
  type(edge_map_t)                    :: edge_map
  type(vector_field_t)                :: tangent
  type(list_iterator_t)               :: vertex_iterator 
  type(point_t), pointer              :: edge_map_coordinates(:)
  integer(ip)                         :: edge_id  
  integer(ip)                         :: vertex_within_edge_id  

  ! Faces 
  integer(ip)                         :: facet_lid
  type(hex_nedelec_reference_fe_t)    :: fe_2D
  type(interpolation_t)               :: interpolation_2D 
  type(quadrature_t)                  :: facet_quadrature
  type(facet_map_t)                   :: facet_map
  type(point_t), pointer              :: facet_map_coordinates(:)
  type(vector_field_t)                :: normal
  type(vector_field_t)                :: cross_product_function_normal
  type(vector_field_t)                :: cross_product_function_normal_proj
  type(vector_field_t)                :: col_jacobian
  integer(ip)                         :: vertex_within_facet_id 

  ! Common integration 
  real(rp), allocatable               :: scalar_function_values(:,:) ! Work-space
  real(rp)                            :: time_(1)  
  integer(ip)                         :: vertex_within_cell_id
  real(rp)                            :: shape_test, factor 
  type(vector_field_t)                :: v_shape_test 
  integer(ip)                         :: idime, ishape, idof 
  integer(ip)                         :: qpoint, num_quad_points 
  type(vector_field_t), allocatable   :: function_values(:,:) 
  type(point_t), pointer              :: quad_points_coordinates(:)
  type(list_iterator_t)               :: own_dofs

  integer(ip)                         :: i, iedge, icomp, istat

  ! Nedelec elements do not contain DOFs on vertices 
  if ( this%get_n_face_dim(vef_lid) == 0) then 
     return 
  end if

  dof_values = 0.0_rp		
  if ( present(time) ) time_(1) = time 	
  coordinates => cell_map%get_coordinates()

  ! Create a d scalar lagrangian reference FE of order 1 (face geometry)
  call d_fe_geo%create(topology    = this%get_topology(),    &
       num_dims    = this%num_dims,          &
       order       = 1,                      &
       field_type  = field_type_scalar,      &
       conformity  = .false. )

  if ( this%get_n_face_dim(vef_lid) == 1) then 
     !								
     !! Create a 1D scalar lagrangian reference FE of order k-1
     call fe_1D%create(topology          = this%get_topology(), &
          num_dims          = 1, &
          order             = this%order-1, &
          field_type        = field_type_scalar, &
          conformity        = .true. )

     call this%create_edge_quadrature( edge_quadrature )
     call edge_map%create( edge_quadrature, d_fe_geo )
     call fe_1D%create_interpolation( edge_quadrature, interpolation_1D )

     num_quad_points = edge_quadrature%get_num_quadrature_points()
     call memalloc ( num_quad_points, 1, scalar_function_values, __FILE__, __LINE__)
     allocate ( function_values(num_quad_points,1), stat=istat); check(istat==0)
     edge_map_coordinates => edge_map%get_coordinates()

     vertex_within_edge_id = 1
     vertex_iterator = this%vertices_n_face%create_iterator(vef_lid)
     do while ( .not. vertex_iterator%is_upper_bound () )
        vertex_within_cell_id = vertex_iterator%get_current()
        do idime=1,this%num_dims  
           call edge_map_coordinates(vertex_within_edge_id)%set(idime, coordinates(vertex_within_cell_id)%get(idime) )
        end do
        call vertex_iterator%next()
        vertex_within_edge_id = vertex_within_edge_id + 1
     end do

     ! Update edge_map on current edge
     call edge_map%update(local_edge_id = vef_lid - this%get_first_n_face_id_of_dim(1)+1, &
          reference_fe  = this,                                           &
          quadrature    = edge_quadrature )

     ! Extract quadrature points coordinates 
     quad_points_coordinates => edge_map%get_quadrature_points_coordinates()

     ! Fill vector function values restricted to edge 
     select case(this%get_field_type())
     case ( field_type_scalar )
        ! reference_fe MUST BE a vector-valued FE
        assert (.false.)
     case ( field_type_vector )
        do icomp=1, this%get_num_field_components()
           if(present(time)) then
              call function_scalar_components(icomp)%p%get_values_set_space_time(quad_points_coordinates,time_,scalar_function_values(1:num_quad_points,:))
           else
              call function_scalar_components(icomp)%p%get_values_set_space(quad_points_coordinates,scalar_function_values(1:num_quad_points,1))
           end if
           do qpoint = 1, num_quad_points
              call function_values(qpoint,1)%set(icomp,scalar_function_values(qpoint,1))
           end do
        end do
     case ( field_type_tensor )
        ! reference_fe MUST BE a vector-valued FE
        assert(.false.)
     end select

     own_dofs = this%own_dofs_n_face%create_iterator(vef_lid)
     ! Integrate edge boundary moments int_edge(g¬∑tau q), q \in P_k-1
     do qpoint = 1, num_quad_points
        factor = edge_map%get_det_jacobian(qpoint) * edge_quadrature%get_weight(qpoint)
        call edge_map%get_tangent(qpoint, tangent)								
        call own_dofs%begin() 
        do ishape = 1, interpolation_1D%num_shape_functions 
           idof = own_dofs%get_current() 
           call fe_1D%get_value(interpolation_1D, ishape, qpoint, shape_test)
           dof_values(idof) = dof_values(idof) + 1.0_rp / tangent%nrm2() * function_values(qpoint,1) * shape_test * tangent * factor 
           call own_dofs%next() 
        end do
     end do

     call fe_1D%free()
     call edge_quadrature%free()
     call interpolation_1D%free()
     call edge_map%free()
     call memfree(scalar_function_values, __FILE__, __LINE__) 
     deallocate(function_values, stat=istat); check(istat==0)

  elseif ( this%get_n_face_dim(vef_lid) == 2 ) then 

     if ( this%num_dims == 3 .and. this%order > 1 ) then

        call fe_2D%create(topology   = this%get_topology(), &
             num_dims   = 2,                   &
             order      = this%order-1,        &
             field_type = field_type_vector,   &
             conformity = .false. )

        call this%create_facet_quadrature( facet_quadrature )
        num_quad_points  =  facet_quadrature%get_num_quadrature_points()
        call memalloc (num_quad_points, 1, scalar_function_values, __FILE__, __LINE__)
        allocate (function_values(num_quad_points,1), stat=istat); check(istat==0)
        call fe_2D%create_interpolation( facet_quadrature, interpolation_2D )
        call facet_map%create( facet_quadrature, d_fe_geo )

        ! Get face map coordinates
        facet_map_coordinates   => facet_map%get_coordinates()

        ! Extract coordinates of the vertices within the face
        vertex_within_facet_id = 1
        vertex_iterator = this%vertices_n_face%create_iterator(vef_lid)
        do while ( .not. vertex_iterator%is_upper_bound () )
           vertex_within_cell_id = vertex_iterator%get_current()
           do idime=1,this%num_dims 
              call facet_map_coordinates(vertex_within_facet_id)%set(idime, coordinates(vertex_within_cell_id)%get(idime) )
           end do
           call vertex_iterator%next()
           vertex_within_facet_id = vertex_within_facet_id + 1
        end do

        ! Update facet_map on current face
        facet_lid = vef_lid - d_fe_geo%get_first_facet_id()+1
        call facet_map%update(reorientation_factor   = d_fe_geo%get_normal_orientation_factor(facet_lid), &
             quadrature             = facet_quadrature ) 

        ! Extract quadrature points coordinates 
        quad_points_coordinates => facet_map%get_quadrature_points_coordinates()

        ! Fill vector function values restricted to face 
        select case(this%get_field_type())
        case ( field_type_scalar )
           ! reference_fe MUST BE a vector-valued FE
           assert (.false.)
        case ( field_type_vector )
           do icomp=1, this%get_num_field_components()
              if(present(time)) then
                 call function_scalar_components(icomp)%p%get_values_set_space_time(quad_points_coordinates,time_,scalar_function_values(1:num_quad_points,:))
              else
                 call function_scalar_components(icomp)%p%get_values_set_space(quad_points_coordinates,scalar_function_values(1:num_quad_points,1))
              end if
              do qpoint = 1, num_quad_points
                 call function_values(qpoint,1)%set(icomp,scalar_function_values(qpoint,1))
              end do
           end do
        case ( field_type_tensor )
           ! reference_fe MUST BE a vector-valued FE
           assert(.false.)
        end select

        ! Integrate face boundary moments int_Face(g x n q), q \in Q_k-2,k-1 x Q_k-1,k-2
        do qpoint = 1, facet_quadrature%num_quadrature_points
           factor = facet_map%get_det_jacobian(qpoint) * facet_quadrature%get_weight(qpoint)
           call facet_map%get_normal(qpoint, normal)

!!!!!!!!!!!
           !! IMPORTANT NOTE: trick to have normals associated to each pair of faces pointing
           !!                 to the same direction. This only works provided we have an "oriented"
           !!                 triangulation.
!!!!!!!!!!!
           normal = (-1.0_rp)**(this%orientation%a(vef_lid)) * normal

           ! Compute g x n
           call cross_product_function_normal%init(0.0_rp) 
           cross_product_function_normal = cross_product(function_values(qpoint,1),normal)

           ! Multiply the transpose of the jacobian with normalized columns by g x n
           ! This is required to transform g x n in a vector contained within the face
           call cross_product_function_normal_proj%init(0.0_rp)
           do i=1,this%num_dims-1
              call facet_map%get_jacobian_column(i,qpoint,col_jacobian)
              col_jacobian = col_jacobian * (1.0_rp/col_jacobian%nrm2())
              call cross_product_function_normal_proj%set(i, col_jacobian*cross_product_function_normal)
           end do

           ! Apply face_map to v_shape_test 
           own_dofs = this%own_dofs_n_face%create_iterator(vef_lid)
           do ishape=1, interpolation_2D%num_shape_functions
              idof = own_dofs%get_current() 
              call fe_2D%get_value(interpolation_2D, ishape, qpoint, v_shape_test)    
              ! WARNING: Update 2D interpolation for the Nedelec test functions ( J**{-T} will only work in HEX structured meshes! )
              do i=1, this%num_dims-1
                 call facet_map%get_jacobian_column(i,qpoint,col_jacobian)
                 call v_shape_test%set(i, v_shape_test%get(i)/col_jacobian%nrm2() )
              end do

              dof_values(idof) = dof_values(idof) + cross_product_function_normal_proj * v_shape_test * factor
              call own_dofs%next() 
           end do
        end do

        ! Free Face moments integration
        call fe_2D%free() 
        call facet_map%free() 
        call facet_quadrature%free() 
        call interpolation_2D%free()
        call memfree(scalar_function_values, __FILE__, __LINE__ ) 
        deallocate(function_values, stat=istat); check(istat==0)
     end if

  end if

  call d_fe_geo%free()	
end subroutine hex_nedelec_reference_fe_evaluate_boundary_function_moments
