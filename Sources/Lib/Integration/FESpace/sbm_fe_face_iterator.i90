! Copyright (C) 2014 Santiago Badia, Alberto F. Mart√≠n and Javier Principe
!
! This file is part of FEMPAR (Finite Element Multiphysics PARallel library)
!
! FEMPAR is free software: you can redistribute it and/or modify
! it under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version.
!
! FEMPAR is distributed in the hope that it will be useful,
! but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
! GNU General Public License for more details.
!
! You should have received a copy of the GNU General Public License
! along with FEMPAR. If not, see <http://www.gnu.org/licenses/>.
!
! Additional permission under GNU GPL version 3 section 7
!
! If you modify this Program, or any covered work, by linking or combining it 
! with the Intel Math Kernel Library and/or the Watson Sparse Matrix Package 
! and/or the HSL Mathematical Software Library (or a modified version of them), 
! containing parts covered by the terms of their respective licenses, the
! licensors of this Program grant you additional permission to convey the 
! resulting work. 
!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

subroutine fe_facet_iterator_create ( this, fe_space, vef )
  implicit none
  class(fe_facet_iterator_t), target, intent(inout) :: this
  class(serial_fe_space_t) , target, intent(in)    :: fe_space
  class(vef_iterator_t)    , intent(in)            :: vef
  call this%free()
  call this%fe_vef_iterator_t%create(fe_space,vef)
  call this%fe_space%create_fe_iterator(this%fe)
  call this%first()
  call this%allocate_scratch_data()
end subroutine fe_facet_iterator_create

subroutine fe_facet_iterator_free ( this )
  implicit none
  class(fe_facet_iterator_t) , intent(inout)      :: this
  integer(ip) :: istat
  call this%fe_vef_iterator_t%free()
  if (allocated(this%fe)) then
    call this%fe%free()
    deallocate(this%fe, stat=istat); check(istat==0);
  end if
  this%facet_lid = -1
  call this%free_scratch_data()
end subroutine fe_facet_iterator_free

subroutine fe_facet_iterator_allocate_scratch_data ( this )
  implicit none
  class(fe_facet_iterator_t), target, intent(inout) :: this
  integer(ip) :: istat
  call memalloc ( 2, this%fe_space%number_fields,      &
                  this%number_dofs_per_cell_and_field, &
                  __FILE__, __LINE__ )
  allocate( this%elem2dof_per_cell(2,this%fe_space%number_fields), stat=istat); check(istat==0);
end subroutine fe_facet_iterator_allocate_scratch_data

subroutine fe_facet_iterator_free_scratch_data ( this )
  implicit none
  class(fe_facet_iterator_t), target, intent(inout) :: this
  integer(ip) :: istat
  if ( allocated(this%number_dofs_per_cell_and_field) ) then
    call memfree ( this%number_dofs_per_cell_and_field, __FILE__, __LINE__ )
  end if
  if ( allocated(this%elem2dof_per_cell) ) then
    deallocate ( this%elem2dof_per_cell, stat=istat ); check(istat==0);
  end if
end subroutine fe_facet_iterator_free_scratch_data

subroutine fe_facet_iterator_first ( this )
  implicit none
  class(fe_facet_iterator_t) , intent(inout)      :: this
  this%facet_lid = 1
  if ( .not. this%has_finished() ) then
    call this%base_fe_vef_iterator_t%set_lid( &
      this%fe_space%vef_lids_of_fe_faces%get(this%facet_lid) )
  end if
end subroutine fe_facet_iterator_first

subroutine fe_facet_iterator_next ( this )
  implicit none
  class(fe_facet_iterator_t) , intent(inout)      :: this
  this%facet_lid = this%facet_lid + 1
  if ( .not. this%has_finished() ) then
    call this%base_fe_vef_iterator_t%set_lid( &
      this%fe_space%vef_lids_of_fe_faces%get(this%facet_lid) )
  end if
end subroutine fe_facet_iterator_next

function fe_facet_iterator_has_finished ( this )
  implicit none
  class(fe_facet_iterator_t) , intent(in)         :: this
  logical :: fe_facet_iterator_has_finished
  fe_facet_iterator_has_finished = ( this%facet_lid > this%fe_space%vef_lids_of_fe_faces%size() )
end function fe_facet_iterator_has_finished

subroutine fe_facet_iterator_set_lid ( this, lid )
  implicit none
  class(fe_facet_iterator_t), intent(inout) :: this
  integer(ip)              , intent(in)    :: lid
  assert ( lid >= 1 .and. lid <= this%fe_space%vef_lids_of_fe_faces%size() )
  this%facet_lid = lid
  call this%base_fe_vef_iterator_t%set_lid(this%fe_space%vef_lids_of_fe_faces%get(lid))
end subroutine fe_facet_iterator_set_lid

function fe_facet_iterator_get_lid (this) result(facet_lid)
  implicit none
  class(fe_facet_iterator_t), intent(in)    :: this
  integer(ip) :: facet_lid
  facet_lid = this%facet_lid
end function fe_facet_iterator_get_lid

subroutine fe_facet_iterator_update_number_dofs_per_field( this, icell_around )
  implicit none
  class(fe_facet_iterator_t), intent(inout) :: this 
  integer(ip)              , intent(in)    :: icell_around
  call this%get_cell_around(icell_around, this%fe)
  call this%fe%update_number_dofs_per_field(this%number_dofs_per_cell_and_field(icell_around,:))
end subroutine fe_facet_iterator_update_number_dofs_per_field

subroutine fe_facet_iterator_update_elem2dof_per_cell( this )
  implicit none
  class(fe_facet_iterator_t), intent(inout) :: this 
  integer(ip) :: icell_around
  do icell_around = 1,this%get_num_cells_around()
    call this%get_cell_around(icell_around,this%fe)  
    call this%fe%get_elem2dof(this%elem2dof_per_cell(icell_around,:))
  end do
end subroutine fe_facet_iterator_update_elem2dof_per_cell

subroutine fe_facet_iterator_update_integration( this )
  implicit none
  class(fe_facet_iterator_t), intent(inout) :: this 
  integer(ip)                      :: icell_around, field_id
  integer(ip)                      :: face_lpos_within_cells_around(2)
  integer(ip)                      :: subfacet_lid_within_cells_around(2)
  type(point_t)          , pointer :: coords(:)
  type(quadrature_t)     , pointer :: quadrature
  type(facet_maps_t)       , pointer :: facet_map
  type(facet_integrator_t), pointer :: facet_integrator
  
  class(lagrangian_reference_fe_t), pointer :: reference_fe_geo
  
  quadrature => this%get_quadrature()
  facet_map   => this%get_facet_maps()
  
  coords => facet_map%get_face_coordinates()
  call this%get_coordinates(coords)

  do icell_around=1, this%get_num_cells_around()
    call this%get_cell_around(icell_around,this%fe)  
    reference_fe_geo => this%fe%get_reference_fe_geo()
    face_lpos_within_cells_around(icell_around) = &
      this%get_lpos_within_cell_around(icell_around) - reference_fe_geo%get_first_face_id() + 1
    subfacet_lid_within_cells_around(icell_around) = this%get_subfacet_lid_cell_around(icell_around)
    coords => facet_map%get_coordinates_neighbour(icell_around,                                &
                                                 face_lpos_within_cells_around(icell_around), &
                                                 subfacet_lid_within_cells_around(icell_around))
    call this%fe%get_coordinates(coords)    
    call this%update_number_dofs_per_field(icell_around)
  end do

  call this%get_cell_around(1, this%fe)
  reference_fe_geo => this%fe%get_reference_fe_geo()
  
  call facet_map%update(face_lpos_within_cells_around,   &
                       subfacet_lid_within_cells_around, &
                       quadrature,                      &
                       reference_fe_geo)
  
  do field_id = 1, this%fe_space%get_number_fields()
     facet_integrator => this%get_facet_integrator(field_id)
     call facet_integrator%update(face_lpos_within_cells_around,   &
                                 subfacet_lid_within_cells_around, &
                                 this%get_facet_maps(),            &
                                 this%get_face_permutation_index())
  end do
  
end subroutine fe_facet_iterator_update_integration

subroutine fe_facet_iterator_assembly_array ( this,    &
                                             facevec, &
                                             matrix_array_assembler )
  implicit none
  class(fe_facet_iterator_t)      , intent(inout) :: this
  real(rp)                       , intent(in)    :: facevec(:,:)
  class(matrix_array_assembler_t), intent(inout) :: matrix_array_assembler
  
  integer(ip) :: ineigh
  
  call this%update_elem2dof_per_cell()
  do ineigh = 1, this%get_num_cells_around()
    call matrix_array_assembler%assembly_array( this%fe_space%number_fields,                    &
                                                this%fe_space%get_field_blocks(),               &
                                                this%fe_space%get_field_coupling(),             &
                                                this%number_dofs_per_cell_and_field(ineigh,:),  &
                                                this%elem2dof_per_cell(ineigh,:),               &
                                                facevec(:,ineigh) )
  end do
  
end subroutine fe_facet_iterator_assembly_array

subroutine fe_facet_iterator_assembly_matrix ( this,    &
                                              facemat, &
                                              matrix_array_assembler )
  implicit none
  class(fe_facet_iterator_t)      , intent(inout) :: this
  real(rp)                       , intent(in)    :: facemat(:,:,:,:)
  class(matrix_array_assembler_t), intent(inout) :: matrix_array_assembler
  
  integer(ip) :: ineigh, jneigh
  
  call this%update_elem2dof_per_cell()
  do ineigh = 1, this%get_num_cells_around()
    do jneigh = 1, this%get_num_cells_around()
      call matrix_array_assembler%assembly_matrix( this%fe_space%number_fields,                   &
                                                   this%fe_space%get_field_blocks(),              &
                                                   this%fe_space%get_field_coupling(),            &
                                                   this%number_dofs_per_cell_and_field(ineigh,:), &
                                                   this%number_dofs_per_cell_and_field(jneigh,:), &
                                                   this%elem2dof_per_cell(ineigh,:),              &
                                                   this%elem2dof_per_cell(jneigh,:),              &
                                                   facemat(:,:,ineigh,jneigh) )
    end do
  end do
  
end subroutine fe_facet_iterator_assembly_matrix

subroutine fe_facet_iterator_assembly_matrix_array ( this,    &
                                                    facemat, &
                                                    facevec, &
                                                    matrix_array_assembler )
  implicit none
  class(fe_facet_iterator_t)      , intent(inout) :: this
  real(rp)                       , intent(in)    :: facemat(:,:,:,:)
  real(rp)                       , intent(in)    :: facevec(:,:)
  class(matrix_array_assembler_t), intent(inout) :: matrix_array_assembler
  
  integer(ip) :: ineigh, jneigh
  
  call this%update_elem2dof_per_cell()
  do ineigh = 1, this%get_num_cells_around()
    do jneigh = 1, this%get_num_cells_around()
      call matrix_array_assembler%assembly_matrix( this%fe_space%number_fields,                   &
                                                   this%fe_space%get_field_blocks(),              &
                                                   this%fe_space%get_field_coupling(),            &
                                                   this%number_dofs_per_cell_and_field(ineigh,:), &
                                                   this%number_dofs_per_cell_and_field(jneigh,:), &
                                                   this%elem2dof_per_cell(ineigh,:),              &
                                                   this%elem2dof_per_cell(jneigh,:),              &
                                                   facemat(:,:,ineigh,jneigh) )
    end do
    call matrix_array_assembler%assembly_array( this%fe_space%number_fields,                    &
                                                  this%fe_space%get_field_blocks(),               &
                                                  this%fe_space%get_field_coupling(),             &
                                                  this%number_dofs_per_cell_and_field(ineigh,:),  &
                                                  this%elem2dof_per_cell(ineigh,:),               &
                                                  facevec(:,ineigh) )
  end do
  
end subroutine fe_facet_iterator_assembly_matrix_array

function fe_facet_iterator_get_fe_space(this)
  implicit none
  class(fe_facet_iterator_t), target, intent(in) :: this
  class(serial_fe_space_t), pointer :: fe_facet_iterator_get_fe_space
  fe_facet_iterator_get_fe_space => this%fe_space
end function fe_facet_iterator_get_fe_space

!==================================================================================================
subroutine fe_facet_iterator_assemble(this,facemat,facevec,matrix_array_assembler)
  implicit none
  class(fe_facet_iterator_t)       , intent(in)    :: this
  real(rp)                        , intent(in)    :: facemat(:,:,:,:)
  real(rp)                        , intent(in)    :: facevec(:,:)
  class(matrix_array_assembler_t) , intent(inout) :: matrix_array_assembler
  check(.false.)
end subroutine fe_facet_iterator_assemble 

!==================================================================================================
function fe_facet_iterator_get_number_dofs_per_field(this, icell_around) result (number_dofs_per_field)
  implicit none
  class(fe_facet_iterator_t), target, intent(in) :: this
  integer(ip)                      , intent(in) :: icell_around
  integer(ip), pointer :: number_dofs_per_field(:)
  number_dofs_per_field => this%number_dofs_per_cell_and_field(icell_around,:)
end function fe_facet_iterator_get_number_dofs_per_field

!==================================================================================================
subroutine fe_facet_iterator_get_elem2dof(this, icell_around, elem2dof)
  implicit none
  class(fe_facet_iterator_t), target, intent(inout)  :: this
  integer(ip)                      , intent(in)     :: icell_around
  type(i1p_t)                      , intent(out)    :: elem2dof(:)
  call this%get_cell_around(icell_around, this%fe)
  call this%fe%get_elem2dof(elem2dof)
end subroutine fe_facet_iterator_get_elem2dof

!==================================================================================================
function fe_facet_iterator_get_quadrature_degree ( this )
  implicit none
  class(fe_facet_iterator_t), intent(in) :: this
  integer(ip) :: fe_facet_iterator_get_quadrature_degree
  if ( allocated(this%fe_space%fe_facet_quadratures_degree) ) then
     fe_facet_iterator_get_quadrature_degree = this%fe_space%fe_facet_quadratures_degree(this%facet_lid)
  end if
  if ( .not. allocated(this%fe_space%fe_facet_quadratures_degree) .or. &
       fe_facet_iterator_get_quadrature_degree == fe_space_default_quadrature_degree_flag ) then
     fe_facet_iterator_get_quadrature_degree = this%get_default_quadrature_degree()
  end if
end function fe_facet_iterator_get_quadrature_degree

!==================================================================================================
function fe_facet_iterator_get_default_quadrature_degree ( this )
  implicit none
  class(fe_facet_iterator_t), intent(in) :: this
  integer(ip) :: fe_facet_iterator_get_default_quadrature_degree
  integer(ip) :: reference_fe_id
  assert( allocated(this%fe_space%max_order_reference_fe_id_per_fe_face) )
  reference_fe_id = this%fe_space%max_order_reference_fe_id_per_fe_face(this%facet_lid)
  fe_facet_iterator_get_default_quadrature_degree = & 
    this%fe_space%reference_fes(reference_fe_id)%p%get_default_quadrature_degree()
end function fe_facet_iterator_get_default_quadrature_degree

!==================================================================================================
subroutine fe_facet_iterator_set_quadrature_degree ( this, quadrature_degree )
  implicit none
  class(fe_facet_iterator_t), intent(inout) :: this
  integer(ip)              , intent(in)    :: quadrature_degree
  assert ( quadrature_degree >= -1 )
  if ( .not. allocated ( this%fe_space%fe_facet_quadratures_degree) ) then
     call this%fe_space%allocate_fe_facet_quadratures_degree()
     call this%fe_space%clear_fe_facet_quadratures_degree()
  end if
  this%fe_space%fe_facet_quadratures_degree(this%facet_lid) = quadrature_degree
end subroutine fe_facet_iterator_set_quadrature_degree

!=================================================================================================
function fe_facet_iterator_get_quadrature ( this )
  implicit none
  class(fe_facet_iterator_t), intent(inout) :: this
  type(quadrature_t), pointer :: fe_facet_iterator_get_quadrature
  
  integer(ip) :: icell_around, istat
  integer(ip) :: fe_facet_quadratures_position_key
  integer(ip) :: fe_facet_quadratures_position
  integer(ip) :: reference_fes_geo_id(2)
   
  reference_fes_geo_id(2) = 0 
  do icell_around = 1, this%get_num_cells_around()
     call this%get_cell_around(icell_around, this%fe)
     reference_fes_geo_id(icell_around) = this%fe%get_reference_fe_geo_id()
  end do
  
  fe_facet_quadratures_position_key = &
       this%fe_space%generate_fe_facet_quadratures_position_key(this%get_quadrature_degree(), &
                                                               reference_fes_geo_id(1), &
                                                               reference_fes_geo_id(2) )
       
  call this%fe_space%fe_facet_quadratures_position%get(key=fe_facet_quadratures_position_key, &
                                                      val=fe_facet_quadratures_position, &
                                                      stat=istat)
  assert ( .not. istat == key_not_found )
  fe_facet_iterator_get_quadrature => this%fe_space%fe_facet_quadratures(fe_facet_quadratures_position)
end function fe_facet_iterator_get_quadrature

!==================================================================================================
function fe_facet_iterator_get_facet_map(this)
  implicit none
  class(fe_facet_iterator_t), intent(inout) :: this
  type(facet_maps_t)         , pointer    :: fe_facet_iterator_get_facet_map
  integer(ip)                           :: icell_around, istat
  integer(ip)                           :: fe_facet_maps_position_key
  integer(ip)                           :: fe_facet_maps_position
  integer(ip)                           :: reference_fes_geo_id(2)
   
  reference_fes_geo_id(2) = 0 
  do icell_around = 1, this%get_num_cells_around()
     call this%get_cell_around(icell_around, this%fe)
     reference_fes_geo_id(icell_around) = this%fe%get_reference_fe_geo_id()
  end do
  
  fe_facet_maps_position_key = &
       this%fe_space%generate_fe_facet_quadratures_position_key(this%get_quadrature_degree(), &
                                                               reference_fes_geo_id(1), &
                                                               reference_fes_geo_id(2) )
       
  call this%fe_space%fe_facet_quadratures_position%get(key=fe_facet_maps_position_key, &
                                                      val=fe_facet_maps_position, &
                                                      stat=istat)
  assert ( .not. istat == key_not_found )
  fe_facet_iterator_get_facet_map => this%fe_space%fe_facet_maps(fe_facet_maps_position)
end function fe_facet_iterator_get_facet_map

!==================================================================================================
function fe_facet_iterator_get_facet_integrator(this, field_id)
  implicit none
  class(fe_facet_iterator_t), intent(inout) :: this
  integer(ip)              , intent(in)    :: field_id
  type(facet_integrator_t)  , pointer    :: fe_facet_iterator_get_facet_integrator
  integer(ip)                           :: icell_around, istat
  integer(ip)                           :: fe_facet_integrators_position_key
  integer(ip)                           :: fe_facet_integrators_position
  integer(ip)                           :: reference_fes_id(2)
  
  assert ( field_id >= 1 .and. field_id <= this%fe_space%get_number_fields() )
 
  reference_fes_id(2) = 0 
  do icell_around = 1, this%get_num_cells_around()
     call this%get_cell_around(icell_around, this%fe)
     reference_fes_id(icell_around) = this%fe_space%reference_fe_id_per_fe(field_id, this%fe%get_lid())
  end do
  
  fe_facet_integrators_position_key = &
       this%fe_space%generate_fe_facet_integrators_position_key(this%get_quadrature_degree(), &
                                                               reference_fes_id(1), &
                                                               reference_fes_id(2) )
       
  call this%fe_space%fe_facet_integrators_position%get(key=fe_facet_integrators_position_key, &
                                                      val=fe_facet_integrators_position, &
                                                      stat=istat)
  assert ( .not. istat == key_not_found )
  
  fe_facet_iterator_get_facet_integrator => this%fe_space%fe_facet_integrators(fe_facet_integrators_position)
end function fe_facet_iterator_get_facet_integrator

!==================================================================================================
function fe_facet_iterator_compute_surface ( this )
  implicit none
  class(fe_facet_iterator_t), intent(inout) :: this
  real(rp)                         :: fe_facet_iterator_compute_surface
  integer(ip) :: qpoin,num_quadrature_points
  type(facet_maps_t)  , pointer :: facet_map
  type(quadrature_t), pointer :: quad
  
  facet_map => this%get_facet_maps()
  quad     => this%get_quadrature()
  num_quadrature_points = quad%get_num_quadrature_points()
  
  fe_facet_iterator_compute_surface = 0.0_rp
  do qpoin = 1, num_quadrature_points
     fe_facet_iterator_compute_surface = fe_facet_iterator_compute_surface + &
          &                        facet_map%get_det_jacobian(qpoin) * &
          &                        quad%get_weight(qpoin)
  end do
  
end function fe_facet_iterator_compute_surface

function fe_facet_iterator_compute_face_permutation_index (this,first_fe,second_fe) result (face_permutation_index)
  implicit none
  class(fe_facet_iterator_t), intent(inout) :: this
  class(fe_iterator_t)     , intent(inout) :: first_fe
  class(fe_iterator_t)     , intent(inout) :: second_fe
  integer(ip)                              :: face_permutation_index
  class(lagrangian_reference_fe_t), pointer     :: first_reference_fe_geo
  class(lagrangian_reference_fe_t), pointer     :: second_reference_fe_geo
  assert( .not. this%is_ghost() .and. this%get_num_cells_around() == 2 )
  call this%get_cell_around(1,first_fe)
  first_reference_fe_geo => first_fe%get_reference_fe_geo()
  call this%get_cell_around(2,second_fe)
  second_reference_fe_geo => second_fe%get_reference_fe_geo()
  face_permutation_index = &
    first_reference_fe_geo%compute_permutation_index( second_reference_fe_geo,             &
                                                      this%get_lpos_within_cell_around(1), &
                                                      this%get_lpos_within_cell_around(2), &
                                                      first_fe%get_vef_lids(),             &
                                                      second_fe%get_vef_lids() )
end function fe_facet_iterator_compute_face_permutation_index

function fe_facet_iterator_get_face_permutation_index ( this ) result(face_permutation_index)
  implicit none
  class(fe_facet_iterator_t), intent(in) :: this
  integer(ip)                           :: face_permutation_index
  face_permutation_index = this%fe_space%face_permutation_indices%get(this%facet_lid)
end function fe_facet_iterator_get_face_permutation_index 

function fe_facet_iterator_get_lpos_within_cell_around(this, icell_around) result(face_lpos_within_cell_around)
  implicit none
  class(fe_facet_iterator_t) , intent(inout) :: this
  integer(ip)               , intent(in)    :: icell_around
  integer(ip)                               :: face_lpos_within_cell_around
  class(lagrangian_reference_fe_t), pointer :: reference_fe_geo
  integer(ip)                               :: vef_lid_of_face
  assert ( allocated(this%fe) )
  call this%get_cell_around(icell_around, this%fe)
  reference_fe_geo => this%fe%get_reference_fe_geo()
  vef_lid_of_face = this%base_fe_vef_iterator_t%get_lid()
  assert ( this%fe%find_lpos_vef_lid (vef_lid_of_face) /= -1 )
  face_lpos_within_cell_around = this%fe%find_lpos_vef_lid(vef_lid_of_face)
end function fe_facet_iterator_get_lpos_within_cell_around

function fe_facet_iterator_get_subfacet_lid_cell_around ( this, icell_around ) result( subfacet_lid )
  implicit none
  class(fe_facet_iterator_t), intent(in) :: this
  integer(ip)              , intent(in) :: icell_around
  integer(ip)                           :: subfacet_lid
  subfacet_lid = 0
end function fe_facet_iterator_get_subfacet_lid_cell_around 
