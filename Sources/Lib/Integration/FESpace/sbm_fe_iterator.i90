! Copyright (C) 2014 Santiago Badia, Alberto F. Mart√≠n and Javier Principe
!
! This file is part of FEMPAR (Finite Element Multiphysics PARallel library)
!
! FEMPAR is free software: you can redistribute it and/or modify
! it under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version.
!
! FEMPAR is distributed in the hope that it will be useful,
! but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
! GNU General Public License for more details.
!
! You should have received a copy of the GNU General Public License
! along with FEMPAR. If not, see <http://www.gnu.org/licenses/>.
!
! Additional permission under GNU GPL version 3 section 7
!
! If you modify this Program, or any covered work, by linking or combining it 
! with the Intel Math Kernel Library and/or the Watson Sparse Matrix Package 
! and/or the HSL Mathematical Software Library (or a modified version of them), 
! containing parts covered by the terms of their respective licenses, the
! licensors of this Program grant you additional permission to convey the 
! resulting work. 
!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

subroutine fe_iterator_create ( this, fe_space )
  implicit none
  class(fe_iterator_t)            , intent(inout) :: this
  class(serial_fe_space_t), target, intent(in)    :: fe_space
  call this%free()
  call this%set_fe_space(fe_space)
  call this%create_cell(fe_space%triangulation)
end subroutine fe_iterator_create

subroutine fe_iterator_create_cell ( this, triangulation )
  implicit none
  class(fe_iterator_t)              , intent(inout) :: this
  class(base_static_triangulation_t), intent(in)    :: triangulation
  call triangulation%create_cell_iterator(this%cell)
end subroutine fe_iterator_create_cell

subroutine fe_iterator_free_cell ( this, triangulation )
  implicit none
  class(fe_iterator_t)              , intent(inout) :: this
  class(base_static_triangulation_t), intent(in)    :: triangulation
  call triangulation%free_cell_iterator(this%cell)
end subroutine fe_iterator_free_cell

subroutine fe_iterator_free (this)
  implicit none
  class(fe_iterator_t), intent(inout) :: this
  if ( associated(this%fe_space) ) then
    if ( associated(this%fe_space%triangulation) ) then
      call this%free_cell(this%fe_space%triangulation)
    end if
  end if
  call this%nullify_fe_space()
end subroutine fe_iterator_free

subroutine fe_iterator_free_final(this)
  implicit none
  type(fe_iterator_t), intent(inout) :: this
  assert ( .not. allocated(this%cell) )
  assert ( .not. associated(this%fe_space))
end subroutine fe_iterator_free_final

subroutine fe_iterator_set_fe_space(this,fe_space)
  implicit none
  class(fe_iterator_t)             , intent(inout) :: this
  class(serial_fe_space_t), target, intent(in)    :: fe_space
  this%fe_space => fe_space
end subroutine fe_iterator_set_fe_space

subroutine fe_iterator_nullify_fe_space(this)
  implicit none
  class(fe_iterator_t)             , intent(inout) :: this
  nullify(this%fe_space)
end subroutine fe_iterator_nullify_fe_space

function fe_iterator_count_own_dofs ( this, field_id )
  implicit none
  ! Parameters
  class(fe_iterator_t), intent(in)   :: this
  integer(ip)         , intent(in)   :: field_id
  integer(ip) :: fe_iterator_count_own_dofs 
  
  ! Local variables
  integer(ip) :: ivef
  class(reference_fe_t), pointer :: reference_fe
  type(list_iterator_t) :: own_dofs_on_vef_iterator
  
  assert ( field_id>= 1 .and. field_id <= this%fe_space%number_fields )
  
  reference_fe => this%get_reference_fe(field_id)
  ivef = this%get_num_vefs()+1
  own_dofs_on_vef_iterator = reference_fe%create_own_dofs_on_n_face_iterator(ivef)
  fe_iterator_count_own_dofs = own_dofs_on_vef_iterator%get_size() 
end function fe_iterator_count_own_dofs

subroutine fe_iterator_fill_own_dofs ( this, field_id, current_dof )
  implicit none
  ! Parameters
  class(fe_iterator_t), intent(inout)   :: this
  integer(ip)         , intent(in)      :: field_id
  integer(ip)         , intent(inout)   :: current_dof

  ! Local variables
  integer(ip) :: ivef
  integer(ip) :: base_pos_lst_dofs_lids
  class(reference_fe_t), pointer :: reference_fe
  type(list_iterator_t) :: own_dofs_on_vef_iterator
  
  assert ( field_id>= 1 .and. field_id <= this%fe_space%number_fields )
  ivef = this%get_num_vefs()+1
  
  reference_fe => this%get_reference_fe(field_id)
  
  ! Determine position in lst_dofs_lids of the first dof associated to the
  ! field "field_id" within the current fe
  base_pos_lst_dofs_lids = this%fe_space%ptr_dofs_per_fe(field_id, this%get_lid())-1
  
  ! Iterate over all DoFs associated to field "field_id" on top of the vef
  ! with local identifier ivef within current fe
  own_dofs_on_vef_iterator = reference_fe%create_own_dofs_on_n_face_iterator(ivef)
  do while (.not. own_dofs_on_vef_iterator%is_upper_bound())
    current_dof = current_dof + 1
    this%fe_space%lst_dofs_lids(base_pos_lst_dofs_lids+own_dofs_on_vef_iterator%get_current()) = current_dof
    call own_dofs_on_vef_iterator%next()
  end do
end subroutine fe_iterator_fill_own_dofs

subroutine fe_iterator_fill_own_dofs_on_vef ( this, ivef, field_id, current_dof, free_dofs_loop )
  implicit none
  class(fe_iterator_t) , intent(inout) :: this
  integer(ip)          , intent(in)    :: ivef
  integer(ip)          , intent(in)    :: field_id
  integer(ip)          , intent(inout) :: current_dof
  logical              , intent(in)    :: free_dofs_loop

  integer(ip) :: base_pos_lst_dofs_lids
  class(reference_fe_t), pointer :: reference_fe
  type(list_iterator_t) :: own_dofs_on_vef_iterator
  
  assert ( ivef >=1 .and. ivef <= this%get_num_vefs() )
  assert ( field_id >= 1 .and. field_id <= this%fe_space%number_fields )
  
  ! Determine position in lst_dofs_lids of the first dof associated to the
  ! field "field_id" within the current fe
  base_pos_lst_dofs_lids = this%fe_space%ptr_dofs_per_fe(field_id, this%get_lid())-1
  
  reference_fe => this%get_reference_fe(field_id)
  
  ! Iterate over all DoFs associated to field "field_id" on top of the vef
  ! with local identifier ivef within current fe
  own_dofs_on_vef_iterator = reference_fe%create_own_dofs_on_n_face_iterator(ivef)
  do while (.not. own_dofs_on_vef_iterator%is_upper_bound())
    if ( this%fe_space%lst_dofs_lids(base_pos_lst_dofs_lids+own_dofs_on_vef_iterator%get_current()) == 0 ) then
      current_dof = current_dof + 1
      if ( free_dofs_loop ) then ! A free dofs loop is surrounding this call 
        this%fe_space%lst_dofs_lids(base_pos_lst_dofs_lids+own_dofs_on_vef_iterator%get_current()) = current_dof
      else                       ! A fixed dofs loop is surrounding this call 
        this%fe_space%lst_dofs_lids(base_pos_lst_dofs_lids+own_dofs_on_vef_iterator%get_current()) = -current_dof
      end if
    end if
    call own_dofs_on_vef_iterator%next()
  end do
end subroutine fe_iterator_fill_own_dofs_on_vef

function fe_iterator_count_own_dofs_on_vef ( this, ivef, field_id )
  implicit none
  class(fe_iterator_t) , intent(in) :: this
  integer(ip)          , intent(in) :: ivef
  integer(ip)          , intent(in) :: field_id
  integer(ip) :: fe_iterator_count_own_dofs_on_vef

  integer(ip) :: base_pos_lst_dofs_lids
  class(reference_fe_t), pointer :: reference_fe
  type(list_iterator_t) :: own_dofs_on_vef_iterator
  
  assert ( ivef >=1 .and. ivef <= this%get_num_vefs() )
  assert ( field_id >= 1 .and. field_id <= this%fe_space%number_fields )
  
  ! Determine position in lst_dofs_lids of the first dof associated to the
  ! field "field_id" within the current fe
  base_pos_lst_dofs_lids = this%fe_space%ptr_dofs_per_fe(field_id, this%get_lid())-1
  
  reference_fe => this%get_reference_fe(field_id)
  
  fe_iterator_count_own_dofs_on_vef = 0
  own_dofs_on_vef_iterator = reference_fe%create_own_dofs_on_n_face_iterator(ivef)
  do while (.not. own_dofs_on_vef_iterator%is_upper_bound())
    if ( this%fe_space%lst_dofs_lids(base_pos_lst_dofs_lids+own_dofs_on_vef_iterator%get_current()) == 0 ) then
      fe_iterator_count_own_dofs_on_vef = fe_iterator_count_own_dofs_on_vef + 1
    end if
    call own_dofs_on_vef_iterator%next()
  end do
end function fe_iterator_count_own_dofs_on_vef

subroutine fe_iterator_fill_own_dofs_on_vef_component_wise ( this, ivef, field_id, current_dof, components_code, free_dofs_loop )
  implicit none
  class(fe_iterator_t) , intent(inout) :: this
  integer(ip)          , intent(in)    :: ivef
  integer(ip)          , intent(in)    :: field_id
  integer(ip)          , intent(inout) :: current_dof
  logical              , intent(in)    :: components_code(:)
  logical              , intent(in)    :: free_dofs_loop
  integer(ip) :: base_pos_lst_dofs_lids, component_id, local_within_ref_fe_dof_id
  class(reference_fe_t), pointer :: reference_fe
  type(list_iterator_t) :: own_dofs_on_vef_iterator
  
  assert ( ivef >=1 .and. ivef <= this%get_num_vefs() )
  assert ( field_id >= 1 .and. field_id <= this%get_number_fields() )
  
  ! Determine position in lst_dofs_lids of the first dof associated to the
  ! field "field_id" within the current fe
  base_pos_lst_dofs_lids = this%fe_space%ptr_dofs_per_fe(field_id, this%get_lid())-1

  reference_fe => this%get_reference_fe(field_id)
  
  assert ( size(components_code) == reference_fe%get_number_field_components() )
  
  ! Iterate over all DoFs associated to field "field_id" on top of the vef
  ! with local identifier ivef within current fe
  own_dofs_on_vef_iterator = reference_fe%create_own_dofs_on_n_face_iterator(ivef)
  do while ( .not. own_dofs_on_vef_iterator%is_upper_bound() )
    local_within_ref_fe_dof_id  = own_dofs_on_vef_iterator%get_current()
    if ( this%fe_space%lst_dofs_lids(base_pos_lst_dofs_lids+local_within_ref_fe_dof_id) == 0 ) then
      component_id = reference_fe%get_component_node(local_within_ref_fe_dof_id)
      if ( components_code(component_id) ) then 
       current_dof = current_dof +1 
       if ( free_dofs_loop ) then ! A free dofs loop is surrounding this call 
         this%fe_space%lst_dofs_lids(base_pos_lst_dofs_lids+local_within_ref_fe_dof_id) = current_dof
       else
         this%fe_space%lst_dofs_lids(base_pos_lst_dofs_lids+local_within_ref_fe_dof_id) = -current_dof
       end if
      end if  
    end if
    call own_dofs_on_vef_iterator%next()
  end do
end subroutine fe_iterator_fill_own_dofs_on_vef_component_wise

subroutine fe_iterator_fill_own_dofs_on_vef_from_source_fe ( this, target_ivef, source_fe, source_ivef, field_id )
  implicit none
  class(fe_iterator_t) , intent(inout) :: this
  integer(ip)          , intent(in)    :: target_ivef
  class(fe_iterator_t) , intent(in)    :: source_fe
  integer(ip)          , intent(in)    :: source_ivef
  integer(ip)          , intent(in)    :: field_id

  class(reference_fe_t), pointer :: reference_fe_source 
  integer(ip)                    :: base_pos_lst_dofs_lids_source
  type(list_iterator_t)          :: own_dofs_on_vef_iterator_target
  
  class(reference_fe_t), pointer :: reference_fe_target 
  integer(ip)                    :: base_pos_lst_dofs_lids_target
  
  integer(ip)                    :: idof_source, idof_target
  integer(ip)                    :: own_dof_within_vef_target_fe
  integer(ip)                    :: permutation_index

  assert ( target_ivef <= this%get_num_vefs() )
  assert ( source_ivef <= source_fe%get_num_vefs() )
  assert ( field_id <= this%get_number_fields() )
  
  reference_fe_target => this%get_reference_fe(field_id)
  base_pos_lst_dofs_lids_target = this%fe_space%ptr_dofs_per_fe(field_id, this%get_lid())-1
  
  reference_fe_source => source_fe%get_reference_fe(field_id)
  base_pos_lst_dofs_lids_source = this%fe_space%ptr_dofs_per_fe(field_id, source_fe%get_lid())-1
  ! Iterate over all DoFs associated to field "field_id" on top of the vef
  ! with local identifier ivef within target fe
  own_dofs_on_vef_iterator_target = reference_fe_target%create_own_dofs_on_n_face_iterator(target_ivef)
  if ( own_dofs_on_vef_iterator_target%get_size() > 0 ) then
       assert (reference_fe_source%check_compatibility_of_n_faces(reference_fe_target,target_ivef,source_ivef))
       permutation_index = reference_fe_target%compute_permutation_index( reference_fe_source, &
                                                                          target_ivef, &
                                                                          source_ivef, &
                                                                          this%get_vef_lids(), &
                                                                          source_fe%get_vef_lids())
      own_dof_within_vef_target_fe = 1
      do while (.not. own_dofs_on_vef_iterator_target%is_upper_bound())
        idof_target = own_dofs_on_vef_iterator_target%get_current()
        idof_source = reference_fe_source%get_permuted_own_dof_n_face(own_dof_within_vef_target_fe, &
                                                                    source_ivef, & 
                                                                    permutation_index)
        
        this%fe_space%lst_dofs_lids(base_pos_lst_dofs_lids_target+idof_target) = &                                                      
            this%fe_space%lst_dofs_lids(base_pos_lst_dofs_lids_source+idof_source)
        call own_dofs_on_vef_iterator_target%next()
        own_dof_within_vef_target_fe = own_dof_within_vef_target_fe + 1
      end do
  end if
end subroutine fe_iterator_fill_own_dofs_on_vef_from_source_fe

subroutine fe_iterator_fill_dofs_face_integration_coupling ( this, ivef, field_id, dof_count )
  implicit none
  ! Parameters
  class(fe_iterator_t), intent(inout)   :: this
  integer(ip)         , intent(in)      :: ivef
  integer(ip)         , intent(in)      :: field_id
  integer(ip)         , intent(inout)   :: dof_count

  ! Local variables
  integer(ip)                    :: idof, base_pos_lst_dofs_lids
  class(reference_fe_t), pointer :: reference_fe
  type(list_iterator_t)          :: face_integration_coupling_dofs 
  
  reference_fe => this%get_reference_fe(field_id)
  assert ( reference_fe%get_n_face_dimension(ivef) == reference_fe%get_number_dimensions()-1 )
  
  if ( reference_fe%get_continuity() ) then
    face_integration_coupling_dofs = reference_fe%create_dofs_on_n_face_iterator(ivef)
    base_pos_lst_dofs_lids = this%fe_space%ptr_dofs_per_fe(field_id, this%get_lid())-1
    do while (.not. face_integration_coupling_dofs%is_upper_bound())
      idof = face_integration_coupling_dofs%get_current()
      dof_count = dof_count +1 
      this%fe_space%lst_dofs_lids(base_pos_lst_dofs_lids+idof) = dof_count
      call face_integration_coupling_dofs%next()
    end do
  end if
end subroutine fe_iterator_fill_dofs_face_integration_coupling

! Renumbers the DoF identifiers corresponding to all fields mapped to block with identifier block_id
subroutine fe_iterator_renumber_dofs_block ( this, block_id, perm_old2new )
  implicit none
  ! Parameters
  class(fe_iterator_t)      , intent(inout) :: this
  integer(ip)               , intent(in)    :: block_id
  ! No way to know the size of perm_old2new() within the local scope
  ! of class(fe_iterator_t). This is why I used an assumed-size dummy
  ! argument in this context. This will be solved in the future whenever
  ! class(fe_iterator_t) also has access to the whole mesh-like container.
  integer(ip)               , intent(in)    :: perm_old2new(*)
  integer(ip)                            :: field_id
  assert ( block_id >= 1 .and. block_id <= this%fe_space%get_number_blocks() )
  do field_id=1, this%fe_space%get_number_fields()
    if ( this%fe_space%block_layout%get_block_id(field_id) == block_id ) then
      call this%renumber_dofs_field( field_id, perm_old2new )
    end if
  end do 
end subroutine fe_iterator_renumber_dofs_block

! Renumbers the DoF identifiers corresponding to field with identifier field_id
subroutine fe_iterator_renumber_dofs_field ( this, field_id, perm_old2new )
  implicit none
  ! Parameters
  class(fe_iterator_t), intent(inout) :: this
  integer(ip)         , intent(in) :: field_id
  ! No way to know the size of perm_old2new() within the local scope
  ! of class(fe_iterator_t). This is why I used an assumed-size dummy
  ! argument in this context. This will be solved in the future whenever
  ! class(fe_iterator_t) also has access to the whole mesh-like container.
  integer(ip)         , intent(in) :: perm_old2new(*)
  integer(ip) :: i, start_pos_lst_dofs_lids, end_pos_lst_dofs_lids
  
  start_pos_lst_dofs_lids = this%fe_space%ptr_dofs_per_fe(field_id,this%get_lid())
  
  if ( field_id == this%fe_space%get_number_fields() ) then
    end_pos_lst_dofs_lids = this%fe_space%ptr_dofs_per_fe(1, this%get_lid()+1)-1
  else  
    end_pos_lst_dofs_lids   = this%fe_space%ptr_dofs_per_fe(field_id+1, this%get_lid())-1
  end if  
  
  assert ( field_id >= 1 .and. field_id <= this%fe_space%get_number_fields() )
  do i = start_pos_lst_dofs_lids, end_pos_lst_dofs_lids
    if ( this%fe_space%lst_dofs_lids(i) > 0 ) then
      this%fe_space%lst_dofs_lids(i) = perm_old2new(this%fe_space%lst_dofs_lids(i))
    end if
  end do
end subroutine fe_iterator_renumber_dofs_field

subroutine fe_iterator_update_integration( this )
  implicit none
  class(fe_iterator_t), intent(inout) :: this 
  integer(ip) :: field_id
  type(point_t)            , pointer :: coordinates(:)
  type(fe_map_t)           , pointer :: fe_map
  type(quadrature_t)       , pointer :: quadrature
  type(cell_integrator_t), pointer :: cell_integrator

  fe_map      => this%get_fe_map()
  coordinates => fe_map%get_coordinates()
  call this%get_coordinates(coordinates)

  quadrature => this%get_quadrature()
  call fe_map%update(quadrature)
  do field_id = 1, this%get_number_fields()
     cell_integrator => this%get_cell_integrator(field_id) 
     call cell_integrator%update(fe_map)
  end do
  
end subroutine fe_iterator_update_integration

subroutine fe_iterator_assemble(this,elmat,elvec,matrix_array_assembler)
  implicit none
  class(fe_iterator_t)            , intent(in)    :: this
  real(rp)                        , intent(in)    :: elmat(:,:)
  real(rp)                        , intent(in)    :: elvec(:)
  class(matrix_array_assembler_t) , intent(inout) :: matrix_array_assembler
  check(.false.)
end subroutine fe_iterator_assemble 

function fe_iterator_get_fe_space(this)
  implicit none
  class(fe_iterator_t), target, intent(in) :: this
  class(serial_fe_space_t), pointer :: fe_iterator_get_fe_space
  fe_iterator_get_fe_space => this%fe_space
end function fe_iterator_get_fe_space

function fe_iterator_get_number_fields(this)
  implicit none
  class(fe_iterator_t), intent(in) :: this
  integer(ip) :: fe_iterator_get_number_fields
  fe_iterator_get_number_fields = this%fe_space%number_fields
end function fe_iterator_get_number_fields

function fe_iterator_get_fe_space_type(this, field_id)
  implicit none
  class(fe_iterator_t), intent(in) :: this
  integer(ip)         , intent(in) :: field_id
  integer(ip)                      :: fe_iterator_get_fe_space_type
  class(reference_fe_t), pointer   :: reference_fe
  reference_fe => this%get_reference_fe(field_id)
  if ( reference_fe%get_conformity() .and. reference_fe%get_continuity()  ) then
    fe_iterator_get_fe_space_type = fe_space_type_cg
  else if ( reference_fe%get_conformity() .and. .not. reference_fe%get_continuity() ) then
    fe_iterator_get_fe_space_type = fe_space_type_dg_conforming
  else 
    fe_iterator_get_fe_space_type = fe_space_type_dg
  end if
end function fe_iterator_get_fe_space_type

function fe_iterator_get_field_type(this, field_id)
  implicit none
  class(fe_iterator_t), intent(in) :: this
  integer(ip)         , intent(in) :: field_id
  character(:), pointer :: fe_iterator_get_field_type
  class(reference_fe_t), pointer :: reference_fe
  reference_fe => this%get_reference_fe(field_id)
  fe_iterator_get_field_type => reference_fe%get_field_type()
end function fe_iterator_get_field_type

function fe_iterator_get_field_blocks(this)
  implicit none
  class(fe_iterator_t), intent(in) :: this
  integer(ip), pointer             :: fe_iterator_get_field_blocks(:)
  fe_iterator_get_field_blocks => this%fe_space%block_layout%get_field_id_to_block_id()
end function fe_iterator_get_field_blocks


function fe_iterator_get_number_dofs(this)
  implicit none
  class(fe_iterator_t), intent(in) :: this
  integer(ip) :: fe_iterator_get_number_dofs
  fe_iterator_get_number_dofs = this%fe_space%ptr_dofs_per_fe(1, this%get_lid()+1)- &
                                this%fe_space%ptr_dofs_per_fe(1, this%get_lid())
end function fe_iterator_get_number_dofs

subroutine fe_iterator_get_number_dofs_per_field (this, number_dofs_per_field )
  implicit none
  class(fe_iterator_t), intent(in)    :: this
  integer(ip)         , intent(inout) :: number_dofs_per_field(this%fe_space%number_fields)
  
  integer(ip) :: field_id
  do field_id = 1, this%get_number_fields()-1
    number_dofs_per_field(field_id) = this%fe_space%ptr_dofs_per_fe(field_id+1, this%get_lid())- &
                                      this%fe_space%ptr_dofs_per_fe(field_id  , this%get_lid())
  end do
  number_dofs_per_field(field_id) = this%fe_space%ptr_dofs_per_fe(1, this%get_lid()+1)- &
                                    this%fe_space%ptr_dofs_per_fe(field_id, this%get_lid())
end subroutine fe_iterator_get_number_dofs_per_field

subroutine fe_iterator_get_field_elem2dof( this, field_id, elem2dof )
  implicit none
  class(fe_iterator_t), intent(in)    :: this
  integer(ip)         , intent(in)    :: field_id
  integer(ip), pointer, intent(inout) :: elem2dof(:)
  integer(ip) :: spos, epos
 
  spos = this%fe_space%ptr_dofs_per_fe(field_id  ,this%get_lid())
  if ( field_id == this%fe_space%get_number_fields() ) then
     epos = this%fe_space%ptr_dofs_per_fe(1,this%get_lid()+1)-1
  else
     epos = this%fe_space%ptr_dofs_per_fe(field_id+1,this%get_lid())-1
  end if
  elem2dof => this%fe_space%lst_dofs_lids(spos:epos)
end subroutine fe_iterator_get_field_elem2dof

subroutine fe_iterator_get_elem2dof( this, elem2dof )
  implicit none
  class(fe_iterator_t), intent(in)    :: this
  type(i1p_t)         , intent(inout) :: elem2dof(:)
  integer(ip) :: field_id, j, spos, epos
  
  assert ( size(elem2dof) == this%fe_space%number_fields )
  do field_id = 1, this%get_number_fields()-1
    spos = this%fe_space%ptr_dofs_per_fe(field_id  ,this%get_lid())
    epos = this%fe_space%ptr_dofs_per_fe(field_id+1,this%get_lid())-1
    elem2dof(field_id)%p => this%fe_space%lst_dofs_lids(spos:epos)
  end do
  spos = this%fe_space%ptr_dofs_per_fe(field_id  , this%get_lid())
  epos = this%fe_space%ptr_dofs_per_fe(1         , this%get_lid()+1)-1
  elem2dof(field_id)%p => this%fe_space%lst_dofs_lids(spos:epos)
end subroutine fe_iterator_get_elem2dof

function fe_iterator_get_order( this, field_id )
  implicit none
  class(fe_iterator_t), intent(in) :: this
  integer(ip)         , intent(in) :: field_id
  integer(ip) :: fe_iterator_get_order
  class(reference_fe_t), pointer :: reference_fe
  reference_fe => this%get_reference_fe(field_id)
  fe_iterator_get_order = reference_fe%get_order()
end function fe_iterator_get_order

function fe_iterator_get_max_order_single_field( this, field_id )
  implicit none
  class(fe_iterator_t), intent(in) :: this
  integer(ip)         , intent(in) :: field_id
  integer(ip) :: fe_iterator_get_max_order_single_field
  class(reference_fe_t), pointer :: reference_fe
  reference_fe => this%get_reference_fe(field_id)
  fe_iterator_get_max_order_single_field = reference_fe%get_max_order()
end function fe_iterator_get_max_order_single_field

function fe_iterator_get_max_order_all_fields( this )
  implicit none
  class(fe_iterator_t), intent(in) :: this
  integer(ip) :: fe_iterator_get_max_order_all_fields
  integer(ip) :: field_id
  fe_iterator_get_max_order_all_fields = -1
  do field_id=1, this%get_number_fields()
   if ( fe_iterator_get_max_order_all_fields < this%get_max_order_single_field(field_id) ) then
        fe_iterator_get_max_order_all_fields = this%get_max_order_single_field(field_id)
   end if
  end do
end function fe_iterator_get_max_order_all_fields

function fe_iterator_at_strong_dirichlet_boundary ( this, field_id )
  implicit none
  class(fe_iterator_t)   , intent(in) :: this
  integer(ip)            , intent(in) :: field_id
  logical                             :: fe_iterator_at_strong_dirichlet_boundary
  fe_iterator_at_strong_dirichlet_boundary = this%fe_space%at_strong_dirichlet_boundary_per_fe(field_id,this%get_lid())
end function fe_iterator_at_strong_dirichlet_boundary

function fe_iterator_has_fixed_dofs ( this, field_id )
  implicit none
  class(fe_iterator_t)   , intent(in) :: this
  integer(ip)            , intent(in) :: field_id
  logical                             :: fe_iterator_has_fixed_dofs
  fe_iterator_has_fixed_dofs = this%fe_space%has_fixed_dofs_per_fe(field_id,this%get_lid())
end function fe_iterator_has_fixed_dofs


subroutine fe_iterator_determine_at_strong_dirichlet_boundary_single_field ( this, field_id )
  implicit none
  class(fe_iterator_t)   , intent(inout) :: this
  integer(ip)            , intent(in)    :: field_id
  integer(ip), pointer :: elem2dof(:)
  integer(ip) :: i 
  call this%get_field_elem2dof(field_id, elem2dof)
  this%fe_space%at_strong_dirichlet_boundary_per_fe(field_id,this%get_lid()) = .false.
  do i=1, size(elem2dof)
    if ( this%is_strong_dirichlet_dof(elem2dof(i)) ) then
      this%fe_space%at_strong_dirichlet_boundary_per_fe(field_id,this%get_lid()) = .true.
      exit
    end if  
  end do  
end subroutine fe_iterator_determine_at_strong_dirichlet_boundary_single_field

subroutine fe_iterator_determine_has_fixed_dofs ( this, field_id )
  implicit none
  class(fe_iterator_t)   , intent(inout) :: this
  integer(ip)            , intent(in)    :: field_id
  integer(ip), pointer :: elem2dof(:)
  integer(ip) :: i 
  call this%get_field_elem2dof(field_id, elem2dof)
  this%fe_space%has_fixed_dofs_per_fe(field_id,this%get_lid()) = .false.
  do i=1, size(elem2dof)
    if ( this%is_fixed_dof(elem2dof(i)) ) then
      this%fe_space%has_fixed_dofs_per_fe(field_id,this%get_lid()) = .true.
      exit
    end if  
  end do  
end subroutine fe_iterator_determine_has_fixed_dofs


subroutine fe_iterator_determine_at_strong_dirichlet_boundary_all_fields ( this )
  implicit none
  class(fe_iterator_t)   , intent(inout) :: this
  integer(ip) :: field_id
  do field_id=1, this%get_number_fields()
    call this%determine_at_strong_dirichlet_boundary(field_id)
  end do
end subroutine fe_iterator_determine_at_strong_dirichlet_boundary_all_fields




function fe_iterator_is_strong_dirichlet_dof ( this, dof_lid )
  implicit none
  class(fe_iterator_t)   , intent(in) :: this
  integer(ip)            , intent(in) :: dof_lid
  logical :: fe_iterator_is_strong_dirichlet_dof
  fe_iterator_is_strong_dirichlet_dof = (dof_lid < 0 .and. dof_lid >= -this%fe_space%get_number_strong_dirichlet_dofs() )
end function fe_iterator_is_strong_dirichlet_dof

function fe_iterator_is_fixed_dof ( this, dof_lid )
  implicit none
  class(fe_iterator_t)   , intent(in) :: this
  integer(ip)            , intent(in) :: dof_lid
  logical :: fe_iterator_is_fixed_dof
  fe_iterator_is_fixed_dof = dof_lid < 0
end function fe_iterator_is_fixed_dof

function fe_iterator_compute_volume ( this )
  implicit none
  class(fe_iterator_t), intent(in) :: this
  real(rp) :: fe_iterator_compute_volume
  integer(ip)                 :: q_poin, number_quadrature_points
  
  type(quadrature_t), pointer :: quadrature
  type(fe_map_t)    , pointer :: fe_map
  
  quadrature => this%get_quadrature()
  fe_map     => this%get_fe_map()
  
  number_quadrature_points = quadrature%get_number_quadrature_points()
  fe_iterator_compute_volume = 0.0_rp
  do q_poin = 1, number_quadrature_points
     fe_iterator_compute_volume = fe_iterator_compute_volume + &
          &                          fe_map%get_det_jacobian(q_poin) * &
          &                          quadrature%get_weight(q_poin)
  end do
end function fe_iterator_compute_volume

function fe_iterator_get_quadrature_degree ( this )
  implicit none
  class(fe_iterator_t), intent(in) :: this
  integer(ip) :: fe_iterator_get_quadrature_degree
  fe_iterator_get_quadrature_degree = this%fe_space%fe_quadratures_degree(this%get_lid())
  if ( fe_iterator_get_quadrature_degree == fe_space_default_quadrature_degree_flag ) then
     fe_iterator_get_quadrature_degree = this%get_default_quadrature_degree()
  end if
end function fe_iterator_get_quadrature_degree

function fe_iterator_get_default_quadrature_degree ( this )
  implicit none
  class(fe_iterator_t), intent(in) :: this
  integer(ip) :: fe_iterator_get_default_quadrature_degree
  integer(ip) :: reference_fe_id
 
  reference_fe_id = this%fe_space%max_order_reference_fe_id_per_fe(this%get_lid())
  fe_iterator_get_default_quadrature_degree = & 
    this%fe_space%reference_fes(reference_fe_id)%p%get_default_quadrature_degree()
end function fe_iterator_get_default_quadrature_degree

subroutine fe_iterator_set_quadrature_degree ( this, quadrature_degree )
  implicit none
  class(fe_iterator_t), intent(inout) :: this
  integer(ip)         , intent(in)    :: quadrature_degree
  assert ( quadrature_degree >= 0 )
  assert ( allocated ( this%fe_space%fe_quadratures_degree) )
  this%fe_space%fe_quadratures_degree(this%get_lid()) = quadrature_degree
end subroutine fe_iterator_set_quadrature_degree

function fe_iterator_get_quadrature ( this )
  implicit none
  class(fe_iterator_t), intent(in) :: this
  type(quadrature_t), pointer      :: fe_iterator_get_quadrature
  integer(ip) :: fe_quadratures_key, fe_quadratures_position
  integer(ip) :: istat
  
  fe_quadratures_key = this%fe_space%generate_fe_quadratures_position_key(this%get_reference_fe_geo_id(),&
                                                                          this%get_quadrature_degree())
  
  call this%fe_space%fe_quadratures_and_maps_position%get(key=fe_quadratures_key, &
                                                          val=fe_quadratures_position, &
                                                          stat=istat)
  assert ( .not. istat == key_not_found )
  fe_iterator_get_quadrature => this%fe_space%fe_quadratures(fe_quadratures_position)
end function fe_iterator_get_quadrature

function fe_iterator_get_fe_map ( this )
  implicit none
  class(fe_iterator_t), intent(in) :: this
  type(fe_map_t), pointer :: fe_iterator_get_fe_map
  integer(ip) :: fe_maps_position, fe_maps_key
  integer(ip) :: istat
  
  fe_maps_key = this%fe_space%generate_fe_quadratures_position_key(this%get_reference_fe_geo_id(),&
                                                                   this%get_quadrature_degree())
  
  call this%fe_space%fe_quadratures_and_maps_position%get(key=fe_maps_key, &
                                                          val=fe_maps_position, &
                                                          stat=istat)
  
  assert ( .not. istat == key_not_found )
  fe_iterator_get_fe_map => this%fe_space%fe_maps(fe_maps_position)
end function fe_iterator_get_fe_map

function fe_iterator_get_cell_integrator ( this, field_id )
  implicit none
  class(fe_iterator_t), intent(in)   :: this
  integer(ip)         , intent(in)   :: field_id
  type(cell_integrator_t), pointer :: fe_iterator_get_cell_integrator
  integer(ip) :: fe_cell_integrators_position_key
  integer(ip) :: fe_cell_integrators_position
  integer(ip) :: istat
  
  fe_cell_integrators_position_key = &
        this%fe_space%generate_fe_cell_integrators_position_key(this%get_reference_fe_geo_id(), &
                                                                  this%get_quadrature_degree(), &
                                                                  this%fe_space%reference_fe_id_per_fe(field_id, this%get_lid()))
  
  call this%fe_space%fe_cell_integrators_position%get(key=fe_cell_integrators_position_key, &
                                                        val=fe_cell_integrators_position, &
                                                        stat=istat)
  assert ( .not. istat == key_not_found )
  fe_iterator_get_cell_integrator => this%fe_space%fe_cell_integrators(fe_cell_integrators_position)
end function fe_iterator_get_cell_integrator

subroutine fe_iterator_get_fe_vef(this, ivef, fe_vef)
  implicit none
  class(fe_iterator_t)   , intent(in)    :: this
  integer(ip)            , intent(in)    :: ivef
  type(fe_vef_iterator_t), intent(inout) :: fe_vef
  call fe_vef%set_lid(this%cell%get_vef_lid(ivef))
end subroutine fe_iterator_get_fe_vef

function fe_iterator_get_reference_fe(this, field_id)
  implicit none
  class(fe_iterator_t), target   , intent(in)    :: this
  integer(ip)                    , intent(in)    :: field_id
  class(reference_fe_t), pointer :: fe_iterator_get_reference_fe
  fe_iterator_get_reference_fe => this%fe_space%reference_fes(this%fe_space%reference_fe_id_per_fe(field_id,this%get_lid()))%p
end function fe_iterator_get_reference_fe

function fe_iterator_get_max_order_reference_fe(this)
  implicit none
  class(fe_iterator_t),  intent(in)      :: this
  class(reference_fe_t), pointer         :: fe_iterator_get_max_order_reference_fe
  class(reference_fe_t), pointer         :: reference_fe
  integer(ip)                            :: max_order
  integer(ip)                            :: field_id
  max_order = -1
  do field_id=1, this%get_number_fields()
    reference_fe => this%get_reference_fe(field_id)
    if ( reference_fe%get_max_order() > max_order ) then
      max_order = reference_fe%get_max_order()
      fe_iterator_get_max_order_reference_fe => reference_fe
    end if
  end do
end function fe_iterator_get_max_order_reference_fe

function fe_iterator_get_max_order_reference_fe_id(this)
  implicit none
  class(fe_iterator_t),  intent(in)      :: this
  integer(ip)                            :: fe_iterator_get_max_order_reference_fe_id
  class(reference_fe_t), pointer         :: reference_fe
  integer(ip)                            :: max_order
  integer(ip)                            :: field_id
  max_order = -1
  fe_iterator_get_max_order_reference_fe_id = -1
  do field_id=1, this%get_number_fields()
    reference_fe => this%get_reference_fe(field_id)
    if ( reference_fe%get_max_order() >= max_order ) then
      max_order = reference_fe%get_max_order()
      fe_iterator_get_max_order_reference_fe_id = this%get_reference_fe_id(field_id)
    end if
  end do
end function fe_iterator_get_max_order_reference_fe_id

function fe_iterator_get_reference_fe_id(this, field_id)
  implicit none
  class(fe_iterator_t), target   , intent(in)    :: this
  integer(ip)                    , intent(in)    :: field_id
  integer(ip) :: fe_iterator_get_reference_fe_id
  fe_iterator_get_reference_fe_id = this%fe_space%reference_fe_id_per_fe(field_id,this%get_lid())
end function fe_iterator_get_reference_fe_id

function fe_iterator_create_own_dofs_on_vef_iterator ( this, ivef, field_id ) result(list_iterator)
  implicit none
  class(fe_iterator_t), intent(in) :: this
  integer(ip)                , intent(in) :: ivef
  integer(ip)                , intent(in) :: field_id
  type(list_iterator_t)                   :: list_iterator
  class(reference_fe_t), pointer          :: reference_fe
  
  reference_fe => this%get_reference_fe(field_id)
  list_iterator = reference_fe%create_own_dofs_on_n_face_iterator(ivef)
end function fe_iterator_create_own_dofs_on_vef_iterator

subroutine fe_iterator_impose_strong_dirichlet_bcs ( this, elmat, elvec )
 implicit none
 class(fe_iterator_t), intent(in)    :: this
 real(rp)            , intent(in)    :: elmat(:,:)
 real(rp)            , intent(inout) :: elvec(:)  
 integer(ip)                         :: idof, dof_lid, elmat_col, field_id, spos
 
 class(reference_fe_t), pointer :: reference_fe
 real(rp)             , pointer :: strong_dirichlet_values_entries(:)
 
 strong_dirichlet_values_entries => this%fe_space%strong_dirichlet_values%get_entries()
 elmat_col = 0
 do field_id = 1, this%fe_space%get_number_fields()
   reference_fe => this%get_reference_fe(field_id)
   spos = this%fe_space%ptr_dofs_per_fe(field_id, this%get_lid())
   do idof = 1, reference_fe%get_number_shape_functions()
       elmat_col = elmat_col + 1
       dof_lid = this%fe_space%lst_dofs_lids(spos+idof-1)  
       if ( this%is_strong_dirichlet_dof(dof_lid) ) then
          elvec = elvec - elmat(:,elmat_col)*strong_dirichlet_values_entries(-this%fe_space%lst_dofs_lids(spos+idof-1))
       end if
   end do
 end do
 
end subroutine fe_iterator_impose_strong_dirichlet_bcs

subroutine fe_iterator_first_local_non_void(this,field_id)
 implicit none
 class(fe_iterator_t), intent(inout) :: this
 integer(ip),          intent(in)    :: field_id
 class(base_static_triangulation_t), pointer :: base_static_triangulation
 ! Find the first local elem non void in this field
 ! If all the elems are void, then return the last local elem
 call this%first()
 do while( .not. this%has_finished())
   if (this%is_local()) then
     if (this%get_order(field_id) > 0) exit
   end if
   call this%next()
 end do
 base_static_triangulation => this%fe_space%get_triangulation()
 if (this%has_finished()) call this%set_lid(base_static_triangulation%get_num_local_cells())
end subroutine fe_iterator_first_local_non_void

