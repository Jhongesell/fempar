! Copyright (C) 2014 Santiago Badia, Alberto F. Mart√≠n and Javier Principe
!
! This file is part of FEMPAR (Finite Element Multiphysics PARallel library)
!
! FEMPAR is free software: you can redistribute it and/or modify
! it under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version.
!
! FEMPAR is distributed in the hope that it will be useful,
! but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
! GNU General Public License for more details.
!
! You should have received a copy of the GNU General Public License
! along with FEMPAR. If not, see <http://www.gnu.org/licenses/>.
!
! Additional permission under GNU GPL version 3 section 7
!
! If you modify this Program, or any covered work, by linking or combining it 
! with the Intel Math Kernel Library and/or the Watson Sparse Matrix Package 
! and/or the HSL Mathematical Software Library (or a modified version of them), 
! containing parts covered by the terms of their respective licenses, the
! licensors of this Program grant you additional permission to convey the 
! resulting work. 
!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
subroutine dirk_solver_create( this, ts_op, nl_solver)
  implicit none
  class(dirk_solver_t),                    intent(inout) :: this
  type(time_stepping_operator_t) , target, intent(in)    :: ts_op
  type(nonlinear_solver_t)      ,  target, intent(in)    :: nl_solver
  this%ts_op     => ts_op
  this%nl_solver => nl_solver
end subroutine dirk_solver_create

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
subroutine dirk_solver_apply( this, x, y )
  implicit none
  class(dirk_solver_t),   intent(inout) :: this
  class(vector_t)     ,   intent(in)    :: x
  class(vector_t)     ,   intent(inout) :: y
  integer(ip) :: i
  type(time_stepping_stage_fe_operator_t), pointer :: stage_op

  assert ( associated(this%ts_op) )
  assert ( associated(this%nl_solver) )
  
  call this%ts_op%set_initial_data(x) 
  call y%copy(x)
  do i = 1, this%ts_op%scheme%num_stages
    stage_op => this%ts_op%get_stage_operator(i,i)
    call stage_op%set_evaluation_time( this%ts_op%get_current_time() + this%ts_op%get_time_step() * this%ts_op%scheme%c(i) )
    call this%nl_solver%apply ( this%ts_op%dofs_stages(i) , this%ts_op%dofs_stages(i) ) !pmartorell: Not initial value required
    call y%axpby(this%ts_op%dt*this%ts_op%scheme%b(i),this%ts_op%dofs_stages(i),1.0_rp)
  end do
end subroutine dirk_solver_apply

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
subroutine dirk_solver_advance_fe_function( this, fe_function)
  implicit none
  class(dirk_solver_t),    intent(inout) :: this
  type(fe_function_t),     intent(inout) :: fe_function
  
  class(discrete_integration_t), pointer :: di_fe_op
  class(vector_t),           allocatable :: free_dof_values_previous
  class(vector_t),               pointer :: free_dof_values_current, fixed_dof_values
  
  free_dof_values_current  =>  fe_function%get_free_dof_values()
  fixed_dof_values         =>  fe_function%get_fixed_dof_values()
  di_fe_op                 =>  this%ts_op%fe_op%fe_op%get_discrete_integration()
  
  call free_dof_values_current%mold(free_dof_values_previous)
  call free_dof_values_previous%clone(free_dof_values_current)
  call free_dof_values_previous%copy(free_dof_values_current)
  
  call this%apply(free_dof_values_previous,free_dof_values_current)
  
  call this%ts_op%update_current_time() 
  call di_fe_op%set_current_time( this%ts_op%get_current_time() ) 
  call fixed_dof_values%copy( di_fe_op%get_boundary_data() )

end subroutine
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
subroutine dirk_solver_free( this )
  implicit none
  class(dirk_solver_t), intent(inout) :: this
  nullify(this%ts_op)
  nullify(this%nl_solver)
end subroutine dirk_solver_free

