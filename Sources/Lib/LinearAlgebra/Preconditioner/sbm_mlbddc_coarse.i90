! Copyright (C) 2014 Santiago Badia, Alberto F. Mart√≠n and Javier Principe
!
! This file is part of FEMPAR (Finite Element Multiphysics PARallel library)
!
! FEMPAR is free software: you can redistribute it and/or modify
! it under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version.
!
! FEMPAR is distributed in the hope that it will be useful,
! but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
! GNU General Public License for more details.
!
! You should have received a copy of the GNU General Public License
! along with FEMPAR. If not, see <http://www.gnu.org/licenses/>.
!
! Additional permission under GNU GPL version 3 section 7
!
! If you modify this Program, or any covered work, by linking or combining it 
! with the Intel Math Kernel Library and/or the Watson Sparse Matrix Package 
! and/or the HSL Mathematical Software Library (or a modified version of them), 
! containing parts covered by the terms of their respective licenses, the
! licensors of this Program grant you additional permission to convey the 
! resulting work. 
!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
subroutine mlbddc_coarse_create ( this, fe_space, par_sparse_matrix, mlbddc_params )
  implicit none
  class(mlbddc_coarse_t)           , intent(inout) :: this
  type(coarse_fe_space_t)  , target, intent(in)    :: fe_space
  type(par_sparse_matrix_t), target, intent(in)    :: par_sparse_matrix
  type(parameterlist_t)    , target, intent(in)    :: mlbddc_params 
  call this%free()
  this%fe_space => fe_space
  this%par_sparse_matrix => par_sparse_matrix
  this%mlbddc_params => mlbddc_params
  call this%create_vector_spaces()
end subroutine mlbddc_coarse_create

subroutine mlbddc_coarse_create_vector_spaces (this)
  implicit none
  class(mlbddc_coarse_t), intent(inout)  :: this
  type(vector_space_t), pointer :: par_sparse_matrix_domain_vector_space
  type(vector_space_t), pointer :: par_sparse_matrix_range_vector_space
  type(vector_space_t), pointer :: mlbddc_coarse_domain_vector_space
  type(vector_space_t), pointer :: mlbddc_coarse_range_vector_space
  par_sparse_matrix_domain_vector_space => this%par_sparse_matrix%get_domain_vector_space()
  par_sparse_matrix_range_vector_space => this%par_sparse_matrix%get_range_vector_space()
  mlbddc_coarse_domain_vector_space => this%get_domain_vector_space()
  mlbddc_coarse_range_vector_space => this%get_range_vector_space()
  call par_sparse_matrix_domain_vector_space%clone(mlbddc_coarse_domain_vector_space)
  call par_sparse_matrix_range_vector_space%clone(mlbddc_coarse_range_vector_space)
end subroutine mlbddc_coarse_create_vector_spaces

subroutine mlbddc_coarse_setup_constraint_matrix (this)
  implicit none
  class(mlbddc_coarse_t), intent(inout) :: this
  type(coarse_fe_space_t)   , pointer :: fe_space
  assert ( this%am_i_l1_task() )
  fe_space => this%get_coarse_fe_space()
  call fe_space%setup_constraint_matrix(block_id=1, constraint_matrix=this%constraint_matrix)
end subroutine mlbddc_coarse_setup_constraint_matrix

subroutine mlbddc_coarse_setup_weighting_operator(this)
  implicit none
  class(mlbddc_coarse_t), intent(inout) :: this
  integer(ip) :: field_id
  type(coarse_fe_object_iterator_t) :: object
  type(coarse_fe_vef_iterator_t) :: vef
  type(coarse_fe_cell_iterator_t) :: fe
  type(list_iterator_t) :: own_dofs_on_vef_iterator 
  integer(ip), pointer :: fe_dofs(:)
  integer(ip) :: ivef, ivef_within_object, idof, dof_lid
  type(coarse_fe_space_t), pointer :: fe_space
  
  fe_space => this%get_coarse_fe_space()  

  ! To-think where field_id should come from? Temporarily let us assume that we have
  ! a single-field PDE problem
  field_id = 1
  
  call memalloc(fe_space%get_block_num_dofs(1), this%W, __FILE__, __LINE__ )
  this%W = 1.0_rp
  
  call fe_space%create_coarse_fe_cell_iterator(fe)
  call fe_space%create_coarse_fe_object_iterator(object)
  call fe_space%create_coarse_fe_vef_iterator(vef)
  do while ( .not. object%has_finished() )
     do ivef_within_object=1, object%get_num_vefs()
        call object%get_vef(ivef_within_object,vef)
        call vef%get_cell_around(1,fe)
        call fe%get_field_fe_dofs(field_id, fe_dofs)
        ivef = fe%get_vef_lid_from_gid(vef%get_gid())
        own_dofs_on_vef_iterator = fe%create_own_dofs_on_vef_iterator(ivef, field_id)
        do while ( .not. own_dofs_on_vef_iterator%is_upper_bound() )
           idof    = own_dofs_on_vef_iterator%get_current()
           dof_lid = fe_dofs(idof)
           this%W(dof_lid) = this%W(dof_lid)/real(object%get_num_parts_around(),rp)
           call own_dofs_on_vef_iterator%next()
        end do
     end do
     call object%next()
  end do
  call fe_space%free_coarse_fe_vef_iterator(vef)
  call fe_space%free_coarse_fe_cell_iterator(fe)
  call fe_space%free_coarse_fe_object_iterator(object)
end subroutine mlbddc_coarse_setup_weighting_operator

! Helper function that extracts a run-time polymorphic class(matrix_t)
! from XXX, and dynamically casts it into  
! type(par_sparse_matrix_t). If the dynamic cast cannot be performed 
! [because class(matrix_t) is NOT of type(par_sparse_matrix_t)], then it 
! aborts the execution of the program.
function mlbddc_coarse_get_par_sparse_matrix(this)
  implicit none
  class(mlbddc_coarse_t)   , intent(in) :: this
  type(par_sparse_matrix_t), pointer    :: mlbddc_coarse_get_par_sparse_matrix
  mlbddc_coarse_get_par_sparse_matrix => this%par_sparse_matrix
end function mlbddc_coarse_get_par_sparse_matrix

function mlbddc_coarse_get_fe_space(this)
  implicit none
  class(mlbddc_coarse_t)   , intent(in) :: this
  class(base_fe_space_t)  , pointer     :: mlbddc_coarse_get_fe_space
  mlbddc_coarse_get_fe_space => this%fe_space
end function mlbddc_coarse_get_fe_space

! Helper function that extracts type(coarse_fe_space_t) from XXX
function mlbddc_coarse_get_coarse_fe_space(this)
  implicit none
  class(mlbddc_coarse_t)   , intent(in) :: this
  type(coarse_fe_space_t)  , pointer    :: mlbddc_coarse_get_coarse_fe_space
  mlbddc_coarse_get_coarse_fe_space => this%fe_space
end function mlbddc_coarse_get_coarse_fe_space

function mlbddc_coarse_get_par_environment(this)
  implicit none
  class(mlbddc_coarse_t)   , intent(in) :: this
  type(environment_t)  , pointer    :: mlbddc_coarse_get_par_environment
  type(coarse_fe_space_t)  , pointer    :: fe_space
  fe_space => this%get_coarse_fe_space()
  mlbddc_coarse_get_par_environment => fe_space%get_environment()
end function mlbddc_coarse_get_par_environment

function mlbddc_coarse_am_i_l1_task(this)
  implicit none
  class(mlbddc_coarse_t)   , intent(in) :: this
  logical                               :: mlbddc_coarse_am_i_l1_task
  type(environment_t)   , pointer   :: par_environment
  par_environment => this%get_par_environment()
  mlbddc_coarse_am_i_l1_task = par_environment%am_i_l1_task()
end function mlbddc_coarse_am_i_l1_task

function mlbddc_coarse_is_operator_associated( this )
  implicit none
  class(mlbddc_coarse_t)          , intent(in) :: this
  logical :: mlbddc_coarse_is_operator_associated
  mlbddc_coarse_is_operator_associated = associated(this%fe_space) .and. associated(this%par_sparse_matrix) 
end function mlbddc_coarse_is_operator_associated

subroutine mlbddc_coarse_nullify_operator ( this )
  implicit none
  class(mlbddc_coarse_t), intent(inout) :: this
  nullify(this%par_sparse_matrix)
  nullify(this%fe_space)
end subroutine mlbddc_coarse_nullify_operator 
