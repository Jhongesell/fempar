! Copyright (C) 2014 Santiago Badia, Alberto F. Mart√≠n and Javier Principe
!
! This file is part of FEMPAR (Finite Element Multiphysics PARallel library)
!
! FEMPAR is free software: you can redistribute it and/or modify
! it under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version.
!
! FEMPAR is distributed in the hope that it will be useful,
! but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
! GNU General Public License for more details.
!
! You should have received a copy of the GNU General Public License
! along with FEMPAR. If not, see <http://www.gnu.org/licenses/>.
!
! Additional permission under GNU GPL version 3 section 7
!
! If you modify this Program, or any covered work, by linking or combining it 
! with the Intel Math Kernel Library and/or the Watson Sparse Matrix Package 
! and/or the HSL Mathematical Software Library (or a modified version of them), 
! containing parts covered by the terms of their respective licenses, the
! licensors of this Program grant you additional permission to convey the 
! resulting work. 
!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
subroutine object_accessor_create ( this, lid, base_static_triangulation )
  implicit none
  class(object_accessor_t)                      , intent(inout) :: this
  integer(ip)                                   , intent(in)    :: lid
  class(base_static_triangulation_t), target, intent(in)    :: base_static_triangulation
  call this%free()
  this%lid = lid
  this%base_static_triangulation => base_static_triangulation
end subroutine object_accessor_create

subroutine object_accessor_free ( this)
  implicit none
  class(object_accessor_t), intent(inout) :: this
  this%lid = -1
  nullify ( this%base_static_triangulation )
end subroutine object_accessor_free

subroutine object_accessor_next(this)
  implicit none
  class(object_accessor_t), intent(inout) :: this
  this%lid = this%lid + 1
end subroutine object_accessor_next

subroutine object_accessor_set_lid(this, lid)
  implicit none
  class(object_accessor_t), intent(inout) :: this
  integer(ip)             , intent(in)    :: lid
  this%lid = lid
end subroutine object_accessor_set_lid

function object_accessor_past_the_end(this)
  implicit none
  class(object_accessor_t), intent(in) :: this
  logical :: object_accessor_past_the_end
  object_accessor_past_the_end = (this%lid > this%base_static_triangulation%number_objects)
end function object_accessor_past_the_end

function object_accessor_get_lid (this)
  implicit none
  class(object_accessor_t), intent(in) :: this
  integer(ip) :: object_accessor_get_lid
  object_accessor_get_lid = this%lid
end function object_accessor_get_lid

function object_accessor_get_gid (this)
  implicit none
  class(object_accessor_t), intent(in) :: this
  integer(igp) :: object_accessor_get_gid
  assert ( this%lid >= 1 .and. .not. this%past_the_end() )
  object_accessor_get_gid = this%base_static_triangulation%objects_gids(this%lid)
end function object_accessor_get_gid

function object_accessor_get_dimension (this)
  implicit none
  class(object_accessor_t), intent(in) :: this
  integer(igp) :: object_accessor_get_dimension
  assert ( this%lid >= 1 .and. .not. this%past_the_end() )
  object_accessor_get_dimension = this%base_static_triangulation%objects_dimension(this%lid)
end function object_accessor_get_dimension

function object_accessor_get_number_parts_around(this)
  implicit none
  class(object_accessor_t), intent(in) :: this
  integer(ip) :: object_accessor_get_number_parts_around
  assert ( this%lid >= 1 .and. .not. this%past_the_end() )
  object_accessor_get_number_parts_around = this%base_static_triangulation%parts_object%get_sublist_size(this%lid)
end function object_accessor_get_number_parts_around

function object_accessor_create_parts_around_iterator(this)
  implicit none
  class(object_accessor_t), intent(in) :: this
  type(list_iterator_t) :: object_accessor_create_parts_around_iterator
  assert ( this%lid >= 1 .and. .not. this%past_the_end() )
  object_accessor_create_parts_around_iterator = this%base_static_triangulation%parts_object%create_iterator(this%lid)
end function object_accessor_create_parts_around_iterator

function object_accessor_get_number_vefs_on_object(this)
  implicit none
  class(object_accessor_t), intent(in) :: this
  integer(ip) :: object_accessor_get_number_vefs_on_object
  assert ( this%lid >= 1 .and. .not. this%past_the_end() )
  object_accessor_get_number_vefs_on_object = this%base_static_triangulation%vefs_object%get_sublist_size(this%lid)
end function object_accessor_get_number_vefs_on_object

function object_accessor_get_vefs_on_object_iterator(this)
  implicit none
  class(object_accessor_t), intent(in) :: this
  type(vefs_on_object_iterator_t) :: object_accessor_get_vefs_on_object_iterator
  call object_accessor_get_vefs_on_object_iterator%create(this%lid, this%base_static_triangulation)
end function object_accessor_get_vefs_on_object_iterator
