! Copyright (C) 2014 Santiago Badia, Alberto F. Mart√≠n and Javier Principe
!
! This file is part of FEMPAR (Finite Element Multiphysics PARallel library)
!
! FEMPAR is free software: you can redistribute it and/or modify
! it under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version.
!
! FEMPAR is distributed in the hope that it will be useful,
! but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
! GNU General Public License for more details.
!
! You should have received a copy of the GNU General Public License
! along with FEMPAR. If not, see <http://www.gnu.org/licenses/>.
!
! Additional permission under GNU GPL version 3 section 7
!
! If you modify this Program, or any covered work, by linking or combining it 
! with the Intel Math Kernel Library and/or the Watson Sparse Matrix Package 
! and/or the HSL Mathematical Software Library (or a modified version of them), 
! containing parts covered by the terms of their respective licenses, the
! licensors of this Program grant you additional permission to convey the 
! resulting work. 
!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

!========================================================================================
subroutine unfitted_cell_iterator_create ( this, base_static_triangulation )
  
  implicit none
  class(unfitted_cell_iterator_t)           , intent(inout) :: this
  class(base_static_triangulation_t), target, intent(in)    :: base_static_triangulation

  class(marching_cubes_t), pointer :: marching_cubes

  select type (base_static_triangulation)
  class is (serial_unfitted_triangulation_t)
    marching_cubes => base_static_triangulation%get_marching_cubes()
  class is (unfitted_p4est_serial_triangulation_t)
    marching_cubes => base_static_triangulation%get_marching_cubes()
  class is (par_unfitted_triangulation_t)
    marching_cubes => base_static_triangulation%get_marching_cubes()
  class default
    check(.false.)
  end select

  assert(associated(marching_cubes%triangulation))
  call this%free()
  call this%cell_iterator_t%create(base_static_triangulation)
  this%marching_cubes => marching_cubes

end subroutine unfitted_cell_iterator_create

!========================================================================================
subroutine unfitted_cell_iterator_free ( this )
  implicit none
  class(unfitted_cell_iterator_t), intent(inout) :: this
  call this%cell_iterator_t%free()
  this%marching_cubes => null()
end subroutine unfitted_cell_iterator_free

!========================================================================================
subroutine unfitted_cell_iterator_update_sub_triangulation( this )

  implicit none
  class(unfitted_cell_iterator_t), intent(inout) :: this

  integer(ip) :: num_subnodes, num_nodes, num_intersections, i1, i2, mc_case, num_dim
  integer(ip) :: ipo, idi, subcell, subface
  type(point_t), pointer, dimension(:) ::  node_coords, intersec_coords, coordinates
  type(quadrature_t), pointer :: quadrature
  real(rp), pointer :: points_in_quadrature(:,:)
  type(point_t),  pointer :: phys_cell_coords(:)
  type(fe_map_t), pointer :: fe_map
  class(lagrangian_reference_fe_t), pointer :: reference_fe_geo
  integer(ip) :: subcell_nodes(4)
  integer(ip) :: subface_nodes(3)

  assert(this%is_local())

  if ( .not. this%is_cut() ) return

  assert(this%marching_cubes%mc_runtime_init)
  num_subnodes = this%get_number_of_subnodes()
  num_nodes = this%get_num_nodes()
  num_dim = this%marching_cubes%triangulation%get_num_dimensions()
  num_intersections = num_subnodes - num_nodes
  assert(num_subnodes > num_nodes)

  ! Recover the sub-points coordinates on the ref space
  coordinates => this%marching_cubes%subnodes_ref_coords
  assert( num_subnodes <= size(coordinates) )
  node_coords => coordinates(1:num_nodes)
  call this%get_coordinates_ref_space( node_coords )
  intersec_coords => coordinates(num_nodes+1:num_subnodes)
  i1 = this%marching_cubes%mc_ptr_to_intersections(this%get_lid())
  i2 = this%marching_cubes%mc_ptr_to_intersections(this%get_lid()+1)-1
  intersec_coords(:) = this%marching_cubes%mc_intersection_points(i1:i2)

  ! Recover connectivities of the sub-cells
  mc_case = this%marching_cubes%mc_case_per_cell(this%get_lid())
  this%marching_cubes%subcells_nodal_connectivities(:,:) = 0
  do subcell=1,this%get_number_of_subcells()
    this%marching_cubes%subcells_nodal_connectivities(:,subcell) = &
    this%marching_cubes%mc_table_subcell_node_ids_per_case(:,subcell,mc_case)
  end do

  ! Recover connectivities of the sub-faces
  this%marching_cubes%subfaces_nodal_connectivities(:,:) = 0
  do subface=1,this%get_number_of_subfaces()
    this%marching_cubes%subfaces_nodal_connectivities(:,subface) = &
    this%marching_cubes%mc_table_subface_node_ids_per_case(:,subface,mc_case)
  end do

  !Fix inverted sub-cells and sub-faces (only in 3D)
  if (this%marching_cubes%triangulation%get_num_dimensions()==3) then

    ! Search for inverted sub-cells and reorient them
    this%marching_cubes%subcell_has_been_reoriented(:)=.false.
    do subcell=1,this%get_number_of_subcells()
      if (this%subcell_has_been_reoriented(subcell)) then
        this%marching_cubes%subcell_has_been_reoriented(subcell)=.true.
        subcell_nodes(:) = this%marching_cubes%subcells_nodal_connectivities(:,subcell)
        this%marching_cubes%subcells_nodal_connectivities(:,subcell) = subcell_nodes([1,3,2,4])
      end if
    end do

    ! Find sub-faces belonging to inverted sub-cells and reorient them
    do subface=1,this%get_number_of_subfaces()
      if (this%subface_touches_interior_reoriented_subcell(subface)) then
        subface_nodes(:) = this%marching_cubes%subfaces_nodal_connectivities(:,subface)
        this%marching_cubes%subfaces_nodal_connectivities(:,subface) = subface_nodes([1,3,2])
      end if
    end do

  end if

  ! Update the quadrature
  quadrature => this%marching_cubes%subnodes_nodal_quadratures(mc_case)
  assert(num_dim == quadrature%get_number_dimensions())
  assert(num_subnodes == quadrature%get_number_quadrature_points())
  points_in_quadrature => quadrature%get_coordinates()
  do ipo = 1, num_subnodes
    do idi = 1, num_dim
      points_in_quadrature(idi,ipo) = coordinates(ipo)%get(idi)
    end do
  end do

  ! Prepare the fe map
  fe_map => this%marching_cubes%subnodes_fe_maps(mc_case)
  reference_fe_geo => this%get_reference_fe_geo()
  call fe_map%update_interpolation( quadrature, reference_fe_geo )
  phys_cell_coords => fe_map%get_coordinates()
  call this%get_coordinates(phys_cell_coords)
  call fe_map%compute_quadrature_coordinates()

  ! At this point we can extract the physical coordinates of the sub-nodes from the fe_maps

end subroutine unfitted_cell_iterator_update_sub_triangulation

!========================================================================================
function unfitted_cell_iterator_get_number_of_subcells( this ) result ( num_subcells )
  implicit none
  class(unfitted_cell_iterator_t), intent(in) :: this
  integer(ip) :: num_subcells
  integer(ip) :: mc_case
  assert(this%is_local())
  assert(this%marching_cubes%mc_tables_init)
  mc_case           = this%marching_cubes%mc_case_per_cell(this%get_lid())
  num_subcells      = this%marching_cubes%mc_table_num_subcells_per_case(mc_case)
end function unfitted_cell_iterator_get_number_of_subcells

!========================================================================================
function unfitted_cell_iterator_get_number_of_subcell_nodes( this ) result ( num_nodes_subcell )
  implicit none
  class(unfitted_cell_iterator_t), intent(in) :: this
  integer(ip) :: num_nodes_subcell
  assert(this%is_local())
  assert(this%marching_cubes%mc_tables_init)
  num_nodes_subcell = 0
  if (this%get_number_of_subcells() .ne. 0) num_nodes_subcell = this%marching_cubes%mc_table_num_nodes_subcell
end function unfitted_cell_iterator_get_number_of_subcell_nodes

!========================================================================================
subroutine unfitted_cell_iterator_get_phys_coords_of_subcell( this, subcell, points)

  implicit none
  class(unfitted_cell_iterator_t), intent(in)    :: this
  integer(ip),                     intent(in)    :: subcell
  type(point_t),                   intent(inout) :: points(:)

  integer(ip) :: num_nodes_subcell, mc_case
  type(point_t), pointer :: subnodes_phys_coords(:)
  type(fe_map_t), pointer :: fe_map
  integer(ip), pointer ::  subcell_node_ids(:)

  assert(this%is_local())
  assert(this%marching_cubes%mc_runtime_init)
  num_nodes_subcell = this%get_number_of_subcell_nodes()
  assert( size(points) == num_nodes_subcell)
  assert(this%is_cut())
  assert( (1_ip <= subcell) .and. (subcell <= this%get_number_of_subcells())  )

  mc_case = this%marching_cubes%mc_case_per_cell(this%get_lid())
  fe_map => this%marching_cubes%subnodes_fe_maps(mc_case)
  subnodes_phys_coords => fe_map%get_quadrature_points_coordinates()
  subcell_node_ids =>  this%marching_cubes%subcells_nodal_connectivities(:,subcell)
  points(:) = subnodes_phys_coords(subcell_node_ids(:))

end subroutine unfitted_cell_iterator_get_phys_coords_of_subcell

!========================================================================================
subroutine unfitted_cell_iterator_get_ref_coords_of_subcell( this, subcell, points)

  implicit none
  class(unfitted_cell_iterator_t), intent(in)    :: this
  integer(ip),                     intent(in)    :: subcell
  type(point_t),                   intent(inout) :: points(:)

  integer(ip) :: num_nodes_subcell
  type(point_t), pointer :: subnodes_ref_coords(:)
  integer(ip), pointer ::  subcell_node_ids(:)

  assert(this%is_local())
  assert(this%marching_cubes%mc_runtime_init)
  num_nodes_subcell = this%get_number_of_subcell_nodes()
  assert( size(points) == num_nodes_subcell)
  assert(this%is_cut())
  assert( (1_ip <= subcell) .and. (subcell <= this%get_number_of_subcells())  )

  subnodes_ref_coords => this%marching_cubes%subnodes_ref_coords
  subcell_node_ids =>  this%marching_cubes%subcells_nodal_connectivities(:,subcell)
  points(:) = subnodes_ref_coords(subcell_node_ids(:))

end subroutine unfitted_cell_iterator_get_ref_coords_of_subcell

!========================================================================================
function unfitted_cell_iterator_get_number_of_subfaces( this ) result ( num_subfaces )
  implicit none
  class(unfitted_cell_iterator_t), intent(in)    :: this
  integer(ip) :: num_subfaces
  integer(ip) :: mc_case
  assert(this%is_local())
  assert(this%marching_cubes%mc_tables_init)
  mc_case           = this%marching_cubes%mc_case_per_cell(this%get_lid())
  num_subfaces      = this%marching_cubes%mc_table_num_subfaces_per_case(mc_case)
end function unfitted_cell_iterator_get_number_of_subfaces

!========================================================================================
function unfitted_cell_iterator_get_number_of_subface_nodes( this ) result ( num_nodes_subface )
  implicit none
  class(unfitted_cell_iterator_t), intent(in)    :: this
  integer(ip) :: num_nodes_subface
  assert(this%is_local())
  assert(this%marching_cubes%mc_tables_init)
  num_nodes_subface = 0
  if (this%get_number_of_subfaces() .ne. 0) num_nodes_subface = this%marching_cubes%mc_table_num_nodes_subface
end function unfitted_cell_iterator_get_number_of_subface_nodes

!========================================================================================
subroutine unfitted_cell_iterator_get_phys_coords_of_subface( this, subface, points )

  implicit none
  class(unfitted_cell_iterator_t), intent(in)    :: this
  integer(ip),                     intent(in)    :: subface
  type(point_t),                   intent(inout) :: points(:)

  integer(ip) :: num_nodes_subface, mc_case
  type(point_t), pointer :: subnodes_phys_coords(:)
  type(fe_map_t), pointer :: fe_map
  integer(ip), pointer ::  subface_node_ids(:)

  assert(this%is_local())
  assert(this%marching_cubes%mc_runtime_init)
  num_nodes_subface = this%get_number_of_subface_nodes()
  assert( size(points) == num_nodes_subface)
  assert(this%is_cut())
  assert( (1_ip <= subface) .and. (subface <= this%get_number_of_subfaces())  )

  mc_case = this%marching_cubes%mc_case_per_cell(this%get_lid())
  fe_map => this%marching_cubes%subnodes_fe_maps(mc_case)
  subnodes_phys_coords => fe_map%get_quadrature_points_coordinates()
  subface_node_ids =>  this%marching_cubes%subfaces_nodal_connectivities(:,subface)
  points(:) = subnodes_phys_coords(subface_node_ids(:))

end subroutine unfitted_cell_iterator_get_phys_coords_of_subface

!========================================================================================
subroutine unfitted_cell_iterator_get_ref_coords_of_subface( this, subface, points )

  implicit none
  class(unfitted_cell_iterator_t), intent(in)    :: this
  integer(ip),                     intent(in)    :: subface
  type(point_t),                   intent(inout) :: points(:)

  integer(ip) :: num_nodes_subface, mc_case
  type(point_t), pointer :: subnodes_ref_coords(:)
  integer(ip), pointer ::  subface_node_ids(:)

  assert(this%is_local())
  assert(this%marching_cubes%mc_runtime_init)
  num_nodes_subface = this%get_number_of_subface_nodes()
  assert( size(points) == num_nodes_subface)
  assert(this%is_cut())
  assert( (1_ip <= subface) .and. (subface <= this%get_number_of_subfaces())  )

  subnodes_ref_coords => this%marching_cubes%subnodes_ref_coords
  mc_case = this%marching_cubes%mc_case_per_cell(this%get_lid())
  subface_node_ids =>  this%marching_cubes%subfaces_nodal_connectivities(:,subface)
  points(:) = subnodes_ref_coords(subface_node_ids(:))

end subroutine unfitted_cell_iterator_get_ref_coords_of_subface

!========================================================================================
function unfitted_cell_iterator_get_mc_case( this ) result ( mc_case )
  implicit none
  class(unfitted_cell_iterator_t), intent(in)    :: this
  integer(ip) :: mc_case
  assert(this%is_local())
  assert(this%marching_cubes%mc_runtime_init)
  mc_case = this%marching_cubes%mc_case_per_cell(this%get_lid())
end function unfitted_cell_iterator_get_mc_case

!========================================================================================
function unfitted_cell_iterator_is_cut( this ) result ( is_cut )
  implicit none
  class(unfitted_cell_iterator_t), intent(in)    :: this
  logical :: is_cut
  integer(ip) :: mc_case, num_mc_cases
  assert(this%is_local())
  assert(this%marching_cubes%mc_runtime_init)
  mc_case = this%marching_cubes%mc_case_per_cell(this%get_lid())
  num_mc_cases = this%marching_cubes%mc_table_num_cases
  is_cut = (1_ip < mc_case) .and. ( mc_case < num_mc_cases)
end function unfitted_cell_iterator_is_cut

!========================================================================================
function unfitted_cell_iterator_is_interior( this ) result ( is_interior )
  implicit none
  class(unfitted_cell_iterator_t), intent(in)  :: this
  logical :: is_interior
  integer(ip) :: mc_case, num_mc_cases
  assert(this%is_local())
  assert(this%marching_cubes%mc_runtime_init)
  mc_case = this%marching_cubes%mc_case_per_cell(this%get_lid())
  num_mc_cases = this%marching_cubes%mc_table_num_cases
  is_interior = ( mc_case == num_mc_cases )
end function unfitted_cell_iterator_is_interior

!========================================================================================
function unfitted_cell_iterator_is_exterior( this ) result ( is_exterior )
  implicit none
  class(unfitted_cell_iterator_t), intent(in)  :: this
  logical :: is_exterior
  integer(ip) :: mc_case
  assert(this%is_local())
  assert(this%marching_cubes%mc_runtime_init)
  mc_case = this%marching_cubes%mc_case_per_cell(this%get_lid())
  is_exterior = ( mc_case == 1_ip )
end function unfitted_cell_iterator_is_exterior

!========================================================================================
function unfitted_cell_iterator_is_interior_subcell( this, subcell ) result ( is_in )
  implicit none
  class(unfitted_cell_iterator_t), intent(in)  :: this
  integer(ip), intent(in) :: subcell
  logical :: is_in
  integer(ip) :: mc_case, num_subcells, pos
  assert(this%is_local())
  assert(this%marching_cubes%mc_runtime_init)
  assert(this%is_cut())
  num_subcells = this%get_number_of_subcells()
  assert( ( 1_ip <= subcell) .and. subcell <= num_subcells )
  mc_case = this%marching_cubes%mc_case_per_cell(this%get_lid())
  pos = this%marching_cubes%mc_table_inout_subcells_per_case(subcell, mc_case)
  is_in = ( pos == -1_ip )
end function unfitted_cell_iterator_is_interior_subcell

!========================================================================================
function unfitted_cell_iterator_is_exterior_subcell( this, subcell ) result ( is_out )
  implicit none
  class(unfitted_cell_iterator_t), intent(in)  :: this
  integer(ip), intent(in) :: subcell
  logical :: is_out
  integer(ip) :: mc_case, num_subcells, pos
  assert(this%is_local())
  assert(this%marching_cubes%mc_runtime_init)
  assert(this%is_cut())
  num_subcells = this%get_number_of_subcells()
  assert( ( 1_ip <= subcell) .and. subcell <= num_subcells )
  mc_case = this%marching_cubes%mc_case_per_cell(this%get_lid())
  pos = this%marching_cubes%mc_table_inout_subcells_per_case(subcell, mc_case)
  is_out = ( pos == 1_ip )
end function unfitted_cell_iterator_is_exterior_subcell

!========================================================================================
function unfitted_cell_iterator_get_number_of_subnodes( this ) result (num_subnodes)
  implicit none
  class(unfitted_cell_iterator_t), intent(in) :: this
  integer(ip) :: num_subnodes
  integer(ip) :: mc_case
  assert(this%is_local())
  assert(this%marching_cubes%mc_tables_init)
  mc_case      = this%marching_cubes%mc_case_per_cell(this%get_lid())
  num_subnodes = this%get_num_nodes() + this%marching_cubes%mc_table_num_cut_edges_per_case(mc_case)
end function unfitted_cell_iterator_get_number_of_subnodes

!========================================================================================
function unfitted_cell_iterator_subcell_has_been_reoriented(this,subcell) result (is_inverted)

  implicit none
  class(unfitted_cell_iterator_t), intent(in) :: this
  integer(ip), intent(in) :: subcell
  logical :: is_inverted

  type(point_t) :: Xe(4)
  type(vector_field_t) :: u,v,w
  integer(ip), pointer :: Te(:)
  real(rp) :: vol

  ! This routine is only needed (and implemented) when the sub-cell is a tetrahedron
  ! Check it
  assert(1<=subcell .and. subcell <= this%get_number_of_subcells())
  assert(this%get_number_of_subcell_nodes()==4)
  assert(this%marching_cubes%triangulation%get_num_dimensions() == 3)

  ! Get connectivities and coordinates
  ! We assume that update_triangulation was called for this cell
  Te => this%marching_cubes%subcells_nodal_connectivities(:,subcell)
  Xe(:) = this%marching_cubes%subnodes_ref_coords(Te)

  ! Compute (signed) volume
  u = Xe(2) - Xe(1)
  v = Xe(3) - Xe(1)
  w = Xe(4) - Xe(1)
  vol = w*cross_product(u,v)

  ! Check if inverted
  is_inverted = vol < 0.0_rp

end function unfitted_cell_iterator_subcell_has_been_reoriented

!========================================================================================
function unfitted_cell_iterator_subface_touches_reoriented_subcell(this,subface) result (is_neighbour)

  implicit none
  class(unfitted_cell_iterator_t), intent(in) :: this
  integer(ip), intent(in) :: subface
  logical :: is_neighbour

  integer(ip), pointer :: Tf(:), Te(:)
  integer(ip) :: subcell
  logical :: has_the_subface, has_been_reoriented, is_interior

  ! This routine is only needed (and implemented) when the sub-cell is a tetrahedron
  ! Check it
  assert(1<=subface .and. subface <= this%get_number_of_subfaces())
  assert(this%get_number_of_subcell_nodes()==4)
  assert(this%marching_cubes%triangulation%get_num_dimensions() == 3)

  ! Initialize result
  is_neighbour= .false.

  ! Get connectivities of the sub-face
  Tf => this%marching_cubes%subfaces_nodal_connectivities(:,subface)

  ! Do a loop in sub-cell
  do subcell=1,this%get_number_of_subcells()

    ! Find the sub-cells that have the three nodes of the face
    Te => this%marching_cubes%subcells_nodal_connectivities(:,subcell)
    has_the_subface = (any(Te == Tf(1)) .and. any(Te == Tf(2)) .and. any(Te == Tf(3)))

    ! Check if it has been reoriented
    has_been_reoriented = this%marching_cubes%subcell_has_been_reoriented(subcell)

    !Check if it is interior
    is_interior = this%is_interior_subcell(subcell)

    ! If the three conditions are met, return true, so that the sub-face will be reoriented
    is_neighbour = has_the_subface .and. has_been_reoriented .and. is_interior

  end do

end function unfitted_cell_iterator_subface_touches_reoriented_subcell




