! Copyright (C) 2014 Santiago Badia, Alberto F. Mart√≠n and Javier Principe
!
! This file is part of FEMPAR (Finite Element Multiphysics PARallel library)
!
! FEMPAR is free software: you can redistribute it and/or modify
! it under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version.
!
! FEMPAR is distributed in the hope that it will be useful,
! but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
! GNU General Public License for more details.
!
! You should have received a copy of the GNU General Public License
! along with FEMPAR. If not, see <http://www.gnu.org/licenses/>.
!
! Additional permission under GNU GPL version 3 section 7
!
! If you modify this Program, or any covered work, by linking or combining it 
! with the Intel Math Kernel Library and/or the Watson Sparse Matrix Package 
! and/or the HSL Mathematical Software Library (or a modified version of them), 
! containing parts covered by the terms of their respective licenses, the
! licensors of this Program grant you additional permission to convey the 
! resulting work. 
!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

!========================================================================================
subroutine unfitted_vef_iterator_create ( this, triangulation )
  
  implicit none
  class(unfitted_vef_iterator_t)           , intent(inout) :: this
  class(triangulation_t)            , target, intent(in)    :: triangulation

  class(unfitted_boundary_cutter_t), pointer :: boundary_cutter

  call this%free()
  
  select type (triangulation)
  class is (serial_unfitted_triangulation_t)
    boundary_cutter => triangulation%get_unfitted_boundary_cutter()
    call this%bst_vef_iterator_t%create(triangulation)
  class is (unfitted_p4est_serial_triangulation_t)
    boundary_cutter => triangulation%get_unfitted_boundary_cutter()
    call vef_iterator_create(this,triangulation)
  class is (par_unfitted_triangulation_t)
    boundary_cutter => triangulation%get_unfitted_boundary_cutter()
    call this%bst_vef_iterator_t%create(triangulation)
  class default
    check(.false.)
  end select
  
  this%boundary_cutter => boundary_cutter
  call this%boundary_cutter%create_cell_iterator(this%unfitted_cell)  
  call this%update_unfitted_cell()

end subroutine unfitted_vef_iterator_create

!========================================================================================
subroutine unfitted_vef_iterator_free ( this )
  implicit none
  class(unfitted_vef_iterator_t), intent(inout) :: this

  if (associated(this%boundary_cutter)) then
    call this%boundary_cutter%free_cell_iterator(this%unfitted_cell)
  end if
  this%boundary_cutter => null()
  call this%bst_vef_iterator_t%free()

end subroutine unfitted_vef_iterator_free

!========================================================================================
subroutine unfitted_vef_iterator_first(this)
  implicit none
  class(unfitted_vef_iterator_t), intent(inout) :: this
  call this%bst_vef_iterator_t%first()
  call this%update_unfitted_cell()
end subroutine unfitted_vef_iterator_first

!========================================================================================
subroutine unfitted_vef_iterator_next(this)
  implicit none
  class(unfitted_vef_iterator_t), intent(inout) :: this
  call this%bst_vef_iterator_t%next()
  call this%update_unfitted_cell()
end subroutine unfitted_vef_iterator_next

!========================================================================================
subroutine unfitted_vef_iterator_set_gid(this,gid)
  implicit none
  class(unfitted_vef_iterator_t), intent(inout) :: this
  integer(ip)           , intent(in)    :: gid
  call this%bst_vef_iterator_t%set_gid(gid)
  call this%update_unfitted_cell()
end subroutine unfitted_vef_iterator_set_gid

!========================================================================================
subroutine unfitted_vef_iterator_update_sub_triangulation( this )
  implicit none
  class(unfitted_vef_iterator_t), intent(inout) :: this
  call this%unfitted_cell%update_sub_triangulation()
end subroutine unfitted_vef_iterator_update_sub_triangulation

!========================================================================================
function unfitted_vef_iterator_get_num_subvefs( this ) result ( num_subvefs )
  implicit none

  class(unfitted_vef_iterator_t), intent(in)    :: this
  integer(ip) :: num_subvefs

  integer(ip) :: mc_case

  massert(this%is_facet(),'This procedure is only implemeted for facets')
  assert(associated(this%boundary_cutter))
  assert(allocated(this%unfitted_cell))
  
  num_subvefs = this%boundary_cutter%get_num_fitted_subfacets( this%unfitted_cell%get_gid(), this%facet_lid )
end function unfitted_vef_iterator_get_num_subvefs

!========================================================================================
function unfitted_vef_iterator_get_num_subvef_nodes( this ) result ( num_nodes_subvef )

  implicit none
  class(unfitted_vef_iterator_t), intent(in)    :: this
  integer(ip) :: num_nodes_subvef

  massert(this%is_facet(),'This procedure is only implemeted for facets')
  assert(associated(this%boundary_cutter))
  assert(allocated(this%unfitted_cell))

  num_nodes_subvef = this%unfitted_cell%get_num_subfacet_nodes()

end function unfitted_vef_iterator_get_num_subvef_nodes

!========================================================================================
subroutine unfitted_vef_iterator_get_phys_coords_of_subvef( this, subvef, points )

  implicit none
  class(unfitted_vef_iterator_t), intent(in)    :: this
  integer(ip),                    intent(in)    :: subvef
  type(point_t),                  intent(inout) :: points(:)

  massert(this%is_facet(),'This procedure is only implemeted for facets')
  assert(associated(this%boundary_cutter))
  assert(allocated(this%unfitted_cell))

  assert( size(points) == this%unfitted_cell%get_num_subfacet_nodes())
  assert(this%is_cut())
  assert( (1_ip <= subvef) .and. (subvef <= this%get_num_subvefs()) )
  
  call this%boundary_cutter%get_phys_coords_of_subvef ( this%unfitted_cell%get_gid(), this%facet_lid, subvef, points )
  
end subroutine unfitted_vef_iterator_get_phys_coords_of_subvef

!========================================================================================
subroutine unfitted_vef_iterator_get_ref_coords_of_subvef( this, subvef, points )

  implicit none
  class(unfitted_vef_iterator_t), intent(in)    :: this
  integer(ip),                     intent(in)    :: subvef
  type(point_t),                   intent(inout) :: points(:)

 
  massert(this%is_facet(),'This procedure is only implemeted for facets')
  assert(associated(this%boundary_cutter))
  assert(allocated(this%unfitted_cell))
  
  assert( size(points) == this%unfitted_cell%get_num_subfacet_nodes())
  assert(this%is_cut())
  assert((1_ip <= subvef) .and. (subvef <= this%get_num_subvefs()))
  
  call this%boundary_cutter%get_ref_coords_of_subvef ( this%unfitted_cell%get_reference_fe(), this%unfitted_cell%get_gid(), this%facet_lid, subvef, points )

end subroutine unfitted_vef_iterator_get_ref_coords_of_subvef

!========================================================================================
function unfitted_vef_iterator_is_cut( this ) result ( is_cut )

  implicit none
  class(unfitted_vef_iterator_t), intent(in)    :: this
  logical :: is_cut


  massert(this%is_facet(),'This procedure is only implemeted for facets')
  assert(associated(this%boundary_cutter))
  assert(allocated(this%unfitted_cell))

  is_cut = this%boundary_cutter%is_cut_facet( this%unfitted_cell%get_gid(), this%facet_lid )

end function unfitted_vef_iterator_is_cut

!========================================================================================
function unfitted_vef_iterator_is_interior( this ) result ( is_interior )
  implicit none
  class(unfitted_vef_iterator_t), intent(in)  :: this
  logical :: is_interior

  integer(ip) :: mc_case
  integer(ip) :: istat

  massert(this%is_facet(),'This procedure is only implemeted for facets')
  assert(associated(this%boundary_cutter))
  assert(allocated(this%unfitted_cell))

  is_interior = this%boundary_cutter%is_interior_facet( this%unfitted_cell%get_gid(), this%facet_lid )

end function unfitted_vef_iterator_is_interior

!========================================================================================
function unfitted_vef_iterator_is_exterior( this ) result ( is_exterior )
  implicit none
  class(unfitted_vef_iterator_t), intent(in)  :: this
  logical :: is_exterior

  integer(ip) :: mc_case
  integer(ip) :: istat

  massert(this%is_facet(),'This procedure is only implemeted for facets')
  assert(associated(this%boundary_cutter))
  assert(allocated(this%unfitted_cell))

  is_exterior = this%boundary_cutter%is_exterior_facet( this%unfitted_cell%get_gid(), this%facet_lid )

end function unfitted_vef_iterator_is_exterior

!========================================================================================
function unfitted_vef_iterator_is_interior_subvef( this, subvef ) result ( is_in )
  implicit none
  class(unfitted_vef_iterator_t), intent(in)  :: this
  integer(ip), intent(in) :: subvef
  logical :: is_in

  integer(ip) :: mc_case
  integer(ip) :: istat

  massert(this%is_facet(),'This procedure is only implemeted for facets')
  assert(associated(this%boundary_cutter))
  assert(allocated(this%unfitted_cell))

  is_in = this%boundary_cutter%is_interior_subfacet( this%unfitted_cell%get_gid(), this%facet_lid, subvef )

end function unfitted_vef_iterator_is_interior_subvef

!========================================================================================
function unfitted_vef_iterator_is_exterior_subvef( this, subvef ) result ( is_out )
  implicit none
  class(unfitted_vef_iterator_t), intent(in)  :: this
  integer(ip), intent(in) :: subvef
  logical :: is_out

  integer(ip) :: mc_case
  integer(ip) :: istat

  massert(this%is_facet(),'This procedure is only implemeted for facets')
  assert(associated(this%boundary_cutter))
  assert(allocated(this%unfitted_cell))

  is_out = this%boundary_cutter%is_exterior_subfacet( this%unfitted_cell%get_gid(), this%facet_lid, subvef )

end function unfitted_vef_iterator_is_exterior_subvef

!========================================================================================
subroutine unfitted_vef_iterator_update_unfitted_cell(this)

  implicit none
  class(unfitted_vef_iterator_t), target, intent(inout)  :: this

  integer(ip) :: vef_lid
  class(reference_fe_t), pointer :: reference_fe

  class(triangulation_t), pointer  :: triangulation

  if (.not. allocated(this%unfitted_cell)) return
  triangulation => this%unfitted_cell%get_triangulation()
  assert(associated(triangulation))
  select type(this)
    type is (unfitted_vef_iterator_t)
      select type (triangulation)
        type is (unfitted_p4est_serial_triangulation_t)
          return
      end select
  end select

  if (.not. this%has_finished()) then
    call this%get_cell_around(1,this%unfitted_cell)
    assert(this%unfitted_cell%is_local())
    vef_lid = this%unfitted_cell%get_vef_lid_from_gid(this%get_gid())
    reference_fe =>  this%unfitted_cell%get_reference_fe()
    this%facet_lid = vef_lid - reference_fe%get_first_facet_id() + 1
  end if

end subroutine unfitted_vef_iterator_update_unfitted_cell


