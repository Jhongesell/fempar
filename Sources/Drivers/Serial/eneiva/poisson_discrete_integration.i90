! Copyright (C) 2014 Santiago Badia, Alberto F. Mart√≠n and Javier Principe
!
! This file is part of FEMPAR (Finite Element Multiphysics PARallel library)
!
! FEMPAR is free software: you can redistribute it and/or modify
! it under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version.
!
! FEMPAR is distributed in the hope that it will be useful,
! but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
! GNU General Public License for more details.
!
! You should have received a copy of the GNU General Public License
! along with FEMPAR. If not, see <http://www.gnu.org/licenses/>.
!
! Additional permission under GNU GPL version 3 section 7
!
! If you modify this Program, or any covered work, by linking or combining it 
! with the Intel Math Kernel Library and/or the Watson Sparse Matrix Package 
! and/or the HSL Mathematical Software Library (or a modified version of them), 
! containing parts covered by the terms of their respective licenses, the
! licensors of this Program grant you additional permission to convey the 
! resulting work. 
!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

module poisson_discrete_integration_names
  use serial_names
  
  implicit none
# include "debug.i90"
  private
  type, extends(discrete_integration_t) :: poisson_discrete_integration_t
     integer(ip)              :: number_neumann_faces
     integer(ip), allocatable :: neumann_faces(:)
   contains
     procedure :: integrate
  end type poisson_discrete_integration_t
  
  public :: poisson_discrete_integration_t
  
contains
  
  subroutine integrate ( this, fe_space, assembler )
    implicit none
    class(poisson_discrete_integration_t), intent(in)    :: this
    class(serial_fe_space_t)             , intent(inout) :: fe_space
    class(assembler_t)                   , intent(inout) :: assembler
    ! Finite objects
    type(finite_element_t)    , pointer     :: fe
    type(finite_face_t)       , pointer     :: face
    ! Integrators
    type(volume_integrator_t) , pointer     :: vol_int
    type(face_integrator_t)   , pointer     :: face_int
    ! Elemental arrays
    real(rp)                  , allocatable :: elmat(:,:), elvec(:)
    real(rp)                  , allocatable :: facemat(:,:,:,:), facevec(:,:)
    ! Coordinate maps
    type(fe_map_t)            , pointer     :: fe_map
    type(face_map_t)          , pointer     :: face_map
    ! Element to dofs maps
    type(i1p_t)               , pointer     :: elem2dof(:)
    ! Quadrature
    type(quadrature_t)        , pointer     :: quad
    ! Test functions
    real(rp)                                :: shape_trial
    type(vector_field_t)                    :: grad_test, grad_trial
    ! Other locals
    integer(ip)                             :: qpoint, inode, jnode, npoint
    real(rp)                                :: space_factor
    integer(ip)               , allocatable :: number_nodes_per_field(:)
    integer(ip)                             :: number_fe_spaces
    integer(ip)               , pointer     :: field_blocks(:)
    logical                   , pointer     :: field_coupling(:,:)
    integer(ip)                             :: ielem, number_nodes
    integer(ip)                             :: iface, neighbour_id

    ! Getters
    number_fe_spaces =  fe_space%get_number_fe_spaces()
    
    field_blocks     => fe_space%get_field_blocks()
    field_coupling   => fe_space%get_field_coupling()
    
    fe               => fe_space%get_finite_element(1)
    number_nodes     =  fe%get_number_nodes()
    
    quad             => fe%get_quadrature()
  
    call memalloc ( number_fe_spaces, number_nodes_per_field, __FILE__, __LINE__ )
    call fe%get_number_nodes_per_field( number_nodes_per_field )
    
    ! Asserts
    assert( number_fe_spaces == 1 ) ! Only unknown (temperature) is a scalar field
    
    ! Allocate memory for elemental arrays
    call memalloc ( number_nodes, number_nodes, elmat, __FILE__, __LINE__ )
    call memalloc ( number_nodes, elvec, __FILE__, __LINE__ )
    call memalloc ( number_nodes, number_nodes, 2, 2, facemat, __FILE__, __LINE__ )
    call memalloc ( number_nodes, 2, facevec, __FILE__, __LINE__ )
    
    ! Initialize integration
    call fe_space%initialize_integration()
    npoint = quad%get_number_quadrature_points()
    
    ! LOOP OVER ELEMENTS
    elements: do ielem = 1, fe_space%get_number_elements()
    
       ! Get element and integrate if it is in the computational domain
       fe => fe_space%get_finite_element(ielem)
    
       ! Initialize arrays
       elmat = 0.0_rp
       elvec = 0.0_rp
       
       ! Elemental getters
       fe_map   => fe%get_fe_map()
       vol_int  => fe%get_volume_integrator(1)
       elem2dof => fe%get_elem2dof()      
       
       ! Update finite element structures
       call fe%update_integration()
       
       ! Loop over quadrature points
       do qpoint = 1,npoint
          
          ! |J]*wg
          space_factor  = fe_map%get_det_jacobian(qpoint) * quad%get_weight(qpoint)  
          
          ! Diffusive term: - div ( k(u^{n+theta}) grad(u^{n+theta}) )
          do inode = 1, number_nodes_per_field(1)
             call vol_int%get_gradient(inode,qpoint,grad_trial)
             do jnode = 1, number_nodes_per_field(1)
                call vol_int%get_gradient(jnode,qpoint,grad_test)
                elmat(inode,jnode) = elmat(inode,jnode) + space_factor * grad_test * grad_trial 
             end do
          end do
          
       end do
       
       ! Apply strong Dirichlet conditions and assemble elemental arrays
       call fe%impose_strong_dirichlet_bcs( elmat, elvec )
       call assembler%assembly( number_fe_spaces, number_nodes_per_field, elem2dof, field_blocks,  field_coupling, elmat, elvec )
       
    end do elements

    neumann_faces: do iface = 1, this%number_neumann_faces
    
       ! Face getters
       face => fe_space%get_finite_face(this%neumann_faces(iface))
       neighbour_id = 1
                         
       ! Initialize arrays
       facemat = 0.0_rp
       facevec = 0.0_rp

       face_map => face%get_map()
       face_int => face%get_face_integrator(neighbour_id)
       quad     => face%get_quadrature()
       npoint = quad%get_number_quadrature_points()

       ! Update finite face structures
       call face%update_integration()
       
       ! Loop over quadrature points
       do qpoint = 1, npoint
       
          ! |J]*wg
          space_factor = face_map%get_det_jacobian(qpoint) * quad%get_weight(qpoint)
          
          ! Integrate Neumann Boundary Condition
          do inode = 1, number_nodes_per_field(1)
             call face_int%get_value(inode,qpoint,neighbour_id,shape_trial)
             facevec(inode,neighbour_id) = facevec(inode,neighbour_id) + space_factor * shape_trial
          end do
       end do
       
       ! Assemble elemental arrays. RMK: Apply strong Dirichlet bcs?
       elem2dof => face%get_elem2dof(neighbour_id)
       call assembler%face_assembly(number_fe_spaces,number_nodes_per_field, number_nodes_per_field,  &
                                    &  elem2dof,elem2dof,field_blocks,field_coupling,                 &
                                    &  facemat(:,:,neighbour_id,neighbour_id), facevec(:,neighbour_id) )   
    
    end do neumann_faces
 
    ! Release memory
    call memfree ( elmat, __FILE__, __LINE__ )
    call memfree ( elvec, __FILE__, __LINE__ )
    call memfree ( facemat, __FILE__, __LINE__ )
    call memfree ( facevec, __FILE__, __LINE__ )
    call memfree ( number_nodes_per_field, __FILE__, __LINE__ )
  end subroutine integrate
end module poisson_discrete_integration_names

module vector_laplacian_single_discrete_integration_names
use serial_names

implicit none
# include "debug.i90"
private
type, extends(discrete_integration_t) :: vector_laplacian_single_discrete_integration_t
integer(ip) :: viscosity 
contains
procedure :: integrate
end type vector_laplacian_single_discrete_integration_t

public :: vector_laplacian_single_discrete_integration_t

contains
  subroutine integrate ( this, fe_space, assembler )
    implicit none
    class(vector_laplacian_single_discrete_integration_t), intent(in)    :: this
    class(serial_fe_space_t)                   , intent(inout) :: fe_space
    class(assembler_t)                         , intent(inout) :: assembler

    type(finite_element_t), pointer :: fe
    type(volume_integrator_t), pointer :: vol_int
    real(rp), allocatable :: elmat(:,:), elvec(:)
    type(fe_map_t), pointer :: fe_map
    type(quadrature_t), pointer :: quad
    integer(ip), allocatable :: number_nodes_per_field(:)

    integer(ip)  :: igaus,inode,jnode,ioffset,joffset,ngaus
    real(rp) :: factor

    type(vector_field_t) :: grad_test_scalar, grad_trial_scalar
    type(tensor_field_t) :: grad_test_vector, grad_trial_vector
    
    integer(ip) :: i, number_fe_spaces

    integer(ip), pointer :: field_blocks(:)
    logical, pointer :: field_coupling(:,:)

    integer(ip) :: ielem, number_nodes
    type(i1p_t), pointer :: elem2dof(:)

    number_fe_spaces = fe_space%get_number_fe_spaces()
    field_blocks => fe_space%get_field_blocks()
    field_coupling => fe_space%get_field_coupling()

    fe => fe_space%get_finite_element(1)
    number_nodes = fe%get_number_nodes()
    call memalloc ( number_nodes, number_nodes, elmat, __FILE__, __LINE__ )
    call memalloc ( number_nodes, elvec, __FILE__, __LINE__ )
    call memalloc ( number_fe_spaces, number_nodes_per_field, __FILE__, __LINE__ )
    call fe%get_number_nodes_per_field( number_nodes_per_field )
    
    call fe_space%initialize_integration()
    
    quad  => fe%get_quadrature()
    ngaus = quad%get_number_quadrature_points()
    do ielem = 1, fe_space%get_number_elements()
       elmat = 0.0_rp
       elvec = 0.0_rp

       fe => fe_space%get_finite_element(ielem)
       call fe%update_integration()
       
       fe_map            => fe%get_fe_map()
       vol_int           => fe%get_volume_integrator(1)
       elem2dof          => fe%get_elem2dof()

       do igaus = 1,ngaus
          factor = fe_map%get_det_jacobian(igaus) * quad%get_weight(igaus)
          do inode = 1, number_nodes
             call vol_int%get_gradient(inode,igaus,grad_trial_vector)
             do jnode = 1, number_nodes
                call vol_int%get_gradient(jnode,igaus,grad_test_vector)
                elmat(inode,jnode) = elmat(inode,jnode) + factor * double_contract(grad_test_vector,grad_trial_vector)
             end do
          end do
       end do
       
       call fe%impose_strong_dirichlet_bcs( elmat, elvec )
       call assembler%assembly( number_fe_spaces, number_nodes_per_field, elem2dof, field_blocks,  field_coupling, elmat, elvec )      
    end do
    call memfree ( number_nodes_per_field, __FILE__, __LINE__ )
    call memfree ( elmat, __FILE__, __LINE__ )
    call memfree ( elvec, __FILE__, __LINE__ )
  end subroutine integrate

end module vector_laplacian_single_discrete_integration_names
