subroutine update_integration( this )
  implicit none
  class(SB_finite_element_t), intent(inout) :: this 
  integer(ip)                      :: i
  real(rp), pointer :: coordinates(:,:)

  coordinates => this%fe_map%get_coordinates()
  call this%cell%get_coordinates(coordinates)

  call this%fe_map%update( this%quadrature)!, this%cell%coordinates )
  do i = 1, this%number_fe_spaces
     call this%volume_integrator(i)%p%update(this%fe_map)
  end do
end subroutine update_integration

subroutine fill_interior_dofs ( this, fe_space_id, dof_count )
  implicit none
  ! Parameters
  class(SB_finite_element_t), intent(inout)   :: this
  integer(ip)               , intent(in)      :: fe_space_id
  integer(ip)               , intent(inout)   :: dof_count

  ! Local variables
  integer(ip) :: inode, l_node, ivef
  
  if ( this%reference_fe_phy(fe_space_id)%p%get_continuity() ) then
      ivef = this%cell%num_vefs+1
      do inode = 1, this%reference_fe_phy(fe_space_id)%p%get_number_interior_nodes_vef(ivef)
         l_node = this%reference_fe_phy(fe_space_id)%p%get_interior_node_vef(inode,ivef)
         dof_count = dof_count +1
         this%elem2dof(fe_space_id)%p(l_node) = dof_count
      end do
  else
      do ivef = 1, this%cell%num_vefs+1
       do inode = 1, this%reference_fe_phy(fe_space_id)%p%get_number_interior_nodes_vef(ivef)
            l_node = this%reference_fe_phy(fe_space_id)%p%get_interior_node_vef(inode,ivef)
            dof_count = dof_count + 1
            this%elem2dof(fe_space_id)%p(l_node) = dof_count
        end do
      end do
   end if
end subroutine fill_interior_dofs

subroutine fill_dofs_on_vef ( this, vef_local_id, fe_space_id, dof_count )
  implicit none
  ! Parameters
  class(SB_finite_element_t), intent(inout)   :: this
  integer(ip)               , intent(in)      :: vef_local_id
  integer(ip)               , intent(in)      :: fe_space_id
  integer(ip)               , intent(inout)   :: dof_count

  ! Local variables
  integer(ip) :: inode, l_node

  do inode = 1,this%reference_fe_phy(fe_space_id)%p%get_number_interior_nodes_vef(vef_local_id)
    l_node = this%reference_fe_phy(fe_space_id)%p%get_interior_node_vef(inode,vef_local_id)
    if ( this%bc_code(fe_space_id)%p(l_node) == 0 ) then
      dof_count = dof_count + 1
      this%elem2dof(fe_space_id)%p(l_node) = dof_count
    end	if
  end do
end subroutine fill_dofs_on_vef

subroutine fill_dofs_on_vef_from_source_element ( this, &
                                                           target_vef_lid, &
                                                           source_fe, &  
                                                           source_vef_lid, &
                                                           fe_space_id, &
                                                           s2t_perm_for_interior_nodes_vef)
  implicit none
  ! Parameters
  class(SB_finite_element_t) , target, intent(inout) :: this
  integer(ip)                        , intent(in)    :: target_vef_lid
  type(SB_finite_element_t) , target , intent(in)    :: source_fe
  integer(ip)                        , intent(in)    :: source_vef_lid
  integer(ip)                        , intent(in)    :: fe_space_id
  integer(ip)                        , intent(out)   :: s2t_perm_for_interior_nodes_vef(:) ! Workspace

  ! Local variables
  integer(ip)                    :: interior_inode, l_node, interior_nnode, inode_source, inode_target, order
  class(reference_fe_t), pointer :: source_ref_fe, target_ref_fe
  
  source_ref_fe => source_fe%reference_fe_phy(fe_space_id)%p
  target_ref_fe => this%reference_fe_phy(fe_space_id)%p
  
  assert ( source_ref_fe%get_number_interior_nodes_vef(source_vef_lid) == target_ref_fe%get_number_interior_nodes_vef(target_vef_lid) )
  assert ( source_ref_fe%get_vef_dimension(source_vef_lid) == target_ref_fe%get_vef_dimension(target_vef_lid) )
  
  interior_nnode = source_ref_fe%get_number_interior_nodes_vef(source_vef_lid)
  if ( interior_nnode > 0 ) then
   order = source_ref_fe%get_order()
   order = order - 2 ! cG case SB.alert
   call target_ref_fe%permute_nodes_per_vef( source_ref_fe, &
                                             s2t_perm_for_interior_nodes_vef, &
                                             source_vef_lid, &
                                             target_vef_lid, &
                                             source_fe%cell%vefs, &
                                             this%cell%vefs, &
                                             source_ref_fe%get_vef_dimension(source_vef_lid), &
                                             order )
  
    do interior_inode = 1, interior_nnode
        inode_source = source_ref_fe%get_interior_node_vef(interior_inode,source_vef_lid)
        inode_target = target_ref_fe%get_interior_node_vef(s2t_perm_for_interior_nodes_vef(interior_inode),target_vef_lid)
        if ( this%bc_code(fe_space_id)%p(inode_target) == 0 ) then
           this%elem2dof(fe_space_id)%p(inode_target) = source_fe%elem2dof(fe_space_id)%p(inode_source)
        end	if
    end do 
 end if
 
end subroutine fill_dofs_on_vef_from_source_element

function get_number_nodes ( this )
  implicit none
  class(SB_finite_element_t), intent(in) :: this
  integer(ip)                            :: get_number_nodes
  get_number_nodes = this%number_nodes
end function get_number_nodes

function get_fe_map ( this )
  implicit none
  class(SB_finite_element_t), target, intent(in)  :: this
  type(fe_map_t)                        , pointer :: get_fe_map
  get_fe_map => this%fe_map
end function get_fe_map

function get_quadrature ( this )
  implicit none
  class(SB_finite_element_t), target, intent(in)  :: this
  type(SB_quadrature_t), pointer :: get_quadrature
  get_quadrature => this%quadrature
end function get_quadrature

function get_volume_integrator ( this, i )
  implicit none
  class(SB_finite_element_t), target, intent(in)  :: this
  integer(ip)                       , intent(in)  :: i
  type(SB_volume_integrator_t)      , pointer     :: get_volume_integrator
  get_volume_integrator => this%volume_integrator(i)%p
end function get_volume_integrator

function get_elem2dof( this )
  implicit none
  class(SB_finite_element_t), target, intent(in) :: this
  type(i1p_t), pointer :: get_elem2dof(:)
  get_elem2dof => this%elem2dof
end function get_elem2dof

function get_bc_code( this )
  implicit none
  class(SB_finite_element_t), target, intent(in) :: this
  type(i1p_t), pointer :: get_bc_code(:)
  get_bc_code => this%bc_code
end function get_bc_code

function get_bc_value( this )
  implicit none
  class(SB_finite_element_t), target, intent(in) :: this
  type(r1p_t), pointer :: get_bc_value(:)
  get_bc_value => this%bc_value
end function get_bc_value

subroutine get_number_nodes_per_field ( this, number_nodes )
  implicit none
  class(SB_finite_element_t), intent(in)  :: this
  integer(ip)               , intent(out) :: number_nodes(this%number_fe_spaces)
  integer(ip) :: i
  do i = 1, this%number_fe_spaces
     number_nodes(i) = this%reference_fe_phy(i)%p%get_number_nodes()
  end do
end subroutine get_number_nodes_per_field

function get_subset_id ( this )
  implicit none
  class(SB_finite_element_t), intent(in) :: this
  integer(ip)                            :: get_subset_id
  get_subset_id = this%cell%subset_id
end function get_subset_id

!**************************************************************************************************
! Update methods for fe_function_scalar/vector/tensor_t
!**************************************************************************************************

subroutine update_scalar_values (this, fe_function_scalar, vector_dof_values)
  implicit none
  class(SB_finite_element_t), intent(in)    :: this
  type(fe_function_scalar_t), intent(inout) :: fe_function_scalar
  class(vector_t)           , intent(in)    :: vector_dof_values

  real(rp), pointer :: nodal_values(:)
  real(rp), pointer :: quadrature_points_values(:)
  
  integer(ip)       :: fe_space_id, block_id, number_nodes_fe_space

  assert ( associated(this%field_blocks) )

  fe_space_id = fe_function_scalar%get_fe_space_id()
  call fe_function_scalar%set_current_number_quadrature_points( &
                          & this%quadrature%get_number_evaluation_points() )
  call fe_function_scalar%set_current_number_nodes(             &
                          & this%reference_fe_phy(fe_space_id)%p%get_number_nodes() )

  block_id = this%field_blocks(fe_space_id)
  nodal_values             => fe_function_scalar%get_nodal_values()
  quadrature_points_values => fe_function_scalar%get_quadrature_points_values()
  
  number_nodes_fe_space = this%reference_fe_phy(fe_space_id)%p%get_number_nodes()
  nodal_values(1:number_nodes_fe_space) = this%bc_value(fe_space_id)%p(:)

  call vector_dof_values%extract_subvector ( block_id, &
                                           & number_nodes_fe_space, &
                                           & this%elem2dof(fe_space_id)%p, &
                                           & nodal_values )
  
  call this%volume_integrator(fe_space_id)%p%evaluate_fe_function ( nodal_values, & 
                                                                  & quadrature_points_values )
  
end subroutine update_scalar_values

subroutine update_vector_values (this, fe_function_vector, vector_dof_values)
  implicit none
  class(SB_finite_element_t), intent(in)              :: this
  type(fe_function_vector_t), intent(inout)           :: fe_function_vector
  class(vector_t)           , intent(in)   , optional :: vector_dof_values

  real(rp), pointer             :: nodal_values(:)
  type(vector_field_t), pointer :: quadrature_points_values(:)
  
  integer(ip)                   :: fe_space_id, block_id, number_nodes_fe_space

  assert ( associated(this%field_blocks) )

  fe_space_id = fe_function_vector%get_fe_space_id()
  call fe_function_vector%set_current_number_quadrature_points( &
                          & this%quadrature%get_number_evaluation_points() )
  call fe_function_vector%set_current_number_nodes(             &
                          & this%reference_fe_phy(fe_space_id)%p%get_number_nodes() )

  block_id = this%field_blocks(fe_space_id)
  nodal_values             => fe_function_vector%get_nodal_values()
  quadrature_points_values => fe_function_vector%get_quadrature_points_values()

  number_nodes_fe_space = this%reference_fe_phy(fe_space_id)%p%get_number_nodes()
  nodal_values(1:number_nodes_fe_space) = this%bc_value(fe_space_id)%p(:)

  call vector_dof_values%extract_subvector ( block_id, &
                                           & number_nodes_fe_space, &
                                           & this%elem2dof(fe_space_id)%p, &
                                           & nodal_values )

  call this%volume_integrator(fe_space_id)%p%evaluate_fe_function ( nodal_values, & 
                                                                  & quadrature_points_values )
end subroutine update_vector_values

subroutine update_tensor_values (this, fe_function_tensor, vector_dof_values)
  implicit none
  class(SB_finite_element_t), intent(in)              :: this
  type(fe_function_tensor_t), intent(inout)           :: fe_function_tensor
  class(vector_t)           , intent(in)   , optional :: vector_dof_values

  real(rp), pointer             :: nodal_values(:)
  type(tensor_field_t), pointer :: quadrature_points_values(:)
  
  integer(ip)                   :: fe_space_id, block_id, number_nodes_fe_space

  assert ( associated(this%field_blocks) )

  fe_space_id = fe_function_tensor%get_fe_space_id()
  call fe_function_tensor%set_current_number_quadrature_points( & ! Set, but not used
                          & this%quadrature%get_number_evaluation_points() )
  call fe_function_tensor%set_current_number_nodes(             & ! Set, but not used
                          & this%reference_fe_phy(fe_space_id)%p%get_number_nodes() )

  block_id = this%field_blocks(fe_space_id)
  nodal_values             => fe_function_tensor%get_nodal_values()
  quadrature_points_values => fe_function_tensor%get_quadrature_points_values()

  number_nodes_fe_space = this%reference_fe_phy(fe_space_id)%p%get_number_nodes()
  nodal_values(1:number_nodes_fe_space) = this%bc_value(fe_space_id)%p(:)

  call vector_dof_values%extract_subvector ( block_id, &
                                           & number_nodes_fe_space, &
                                           & this%elem2dof(fe_space_id)%p, &
                                           & nodal_values )

  call this%volume_integrator(fe_space_id)%p%evaluate_fe_function ( nodal_values, & 
                                                                  & quadrature_points_values )
end subroutine update_tensor_values


