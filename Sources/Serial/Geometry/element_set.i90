! Templated implementation of an abstract element set and its iterator. 
! Templates are not natively supported by fortran.
! They are emulated using the preprocessor by including this file after a definition of:
!
! template_element_t
! template_element_set_t
! template_element_iterator_t
!
  type, abstract :: template_element_iterator_t
     private
   contains
     procedure(begin_interface)    , deferred :: begin
     procedure(finished_interface) , deferred :: finished
     procedure(current_interface)  , deferred :: current
     procedure(next_interface)     , deferred :: next
     procedure(create_id_interface), deferred :: create_id
     procedure(free_id_interface)  , deferred :: free_id
     procedure(get_id_interface)   , deferred :: get_id
     procedure(get_interface)      , deferred :: get
  end type template_element_iterator_t

  abstract interface
     ! Iteration over the set
     subroutine begin_interface(this) ! Position the pointer in the first item
       import :: template_element_iterator_t
       implicit none
       class(template_element_iterator_t), intent(inout) :: this
     end subroutine begin_interface
     function finished_interface(this) result(res)
       import :: template_element_iterator_t
       implicit none
       class(template_element_iterator_t), intent(inout) :: this
       logical :: res
     end function finished_interface
     subroutine next_interface(this)
       import :: template_element_iterator_t
       implicit none
       class(template_element_iterator_t), intent(inout) :: this
     end subroutine next_interface
     function current_interface (this) result(p)
       import :: template_element_iterator_t, template_element_t
       implicit none
       class(template_element_iterator_t), intent(inout)  :: this
       class(template_element_t)         , pointer        :: p
     end function current_interface
     ! Functions to manage IDs
     subroutine create_id_interface(this,res)
       import :: template_element_iterator_t, element_id_t
       implicit none
       class(template_element_iterator_t), intent(in)    :: this
       class(element_id_t)  , allocatable, intent(inout) :: res
     end subroutine create_id_interface
     subroutine free_id_interface(this,res)
       import :: template_element_iterator_t, element_id_t
       implicit none
       class(template_element_iterator_t), intent(in)    :: this
       class(element_id_t)  , allocatable, intent(inout) :: res
     end subroutine free_id_interface
     subroutine get_id_interface(this,res)
       import :: template_element_iterator_t, element_id_t
       implicit none
       class(template_element_iterator_t), intent(in)    :: this
       class(element_id_t)               , intent(inout) :: res
     end subroutine get_id_interface
     function get_interface (this,id) result(p)
       import :: template_element_iterator_t, element_id_t, template_element_t
       implicit none
       class(template_element_iterator_t), intent(in) :: this
       class(element_id_t)               , intent(in) :: id
       class(template_element_t)         , pointer    :: p
     end function get_interface
  end interface

  type, abstract :: template_element_set_t
   contains
     procedure(create_set_interface)     , deferred :: create
     procedure(free_set_interface)       , deferred :: free
     procedure(create_iterator_interface), deferred :: create_iterator
     procedure(free_iterator_interface)  , deferred :: free_iterator
  end type template_element_set_t

  ! Abstract interfaces
  abstract interface
     subroutine create_set_interface(this,size,mold)
       import :: template_element_set_t, ip, template_element_t
       implicit none
       class(template_element_set_t), intent(inout) :: this
       integer(ip)                  , intent(in)    :: size
       class(template_element_t)    , intent(in)    :: mold
     end subroutine create_set_interface
     subroutine free_set_interface(this)
       import :: template_element_set_t
       implicit none
       class(template_element_set_t), intent(inout) :: this
     end subroutine free_set_interface
     subroutine create_iterator_interface(this,iterator)
       import :: template_element_set_t, template_element_iterator_t
       implicit none
       class(template_element_set_t)     , target     , intent(in)  :: this
       class(template_element_iterator_t), allocatable, intent(out) :: iterator
     end subroutine create_iterator_interface
     subroutine free_iterator_interface(this,iterator)
       import :: template_element_set_t, template_element_iterator_t
       implicit none
       class(template_element_set_t)                  , intent(in)    :: this
       class(template_element_iterator_t), allocatable, intent(inout) :: iterator
     end subroutine free_iterator_interface
  end interface
