subroutine array_function_create( this, number_dimensions, number_components )
  class(array_function_t), intent(inout) :: this
  integer(ip)            , intent(in)    :: number_dimensions
  integer(ip)            , intent(in)    :: number_components
  this%number_dimensions = number_dimensions
  this%number_components = number_components
end subroutine array_function_create

function array_function_get_number_dimensions( this )
  class(array_function_t), intent(in) :: this
  integer(ip) :: array_function_get_number_dimensions
  assert ( this%number_dimensions >= 1 .and. this%number_dimensions <= 3  )
  array_function_get_number_dimensions = this%number_dimensions
end function array_function_get_number_dimensions

function array_function_get_number_components( this )
  class(array_function_t), intent(in) :: this
  integer(ip) :: array_function_get_number_components
  assert ( this%number_components >= 1  )
  array_function_get_number_components = this%number_components
end function array_function_get_number_components

subroutine array_function_get_component_value_space( this, point, component, result )
  class(array_function_t), intent(in) :: this
  type(point_t), intent(in)          :: point
  integer(ip), intent(in)             :: component
  real(rp), intent(inout)             :: result
  ! Error: Function f(x) not implemented 
  assert( .false. )
end subroutine array_function_get_component_value_space

subroutine array_function_get_component_value_space_time( this, point, time, component, result )
  class(array_function_t), intent(in) :: this
  type(point_t), intent(in)          :: point
  real(rp), intent(in)                :: time
  integer(ip), intent(in)             :: component
  real(rp), intent(inout)             :: result
  ! Error: Function f(x,t) not implemented 
  assert( .false. )
end subroutine array_function_get_component_value_space_time

subroutine array_function_get_component_values_set_space( this, point, component, result )
  class(array_function_t), intent(in) :: this
  type(point_t), intent(in)          :: point(:)
  integer(ip), intent(in)             :: component
  real(rp), intent(inout)             :: result(:)
  integer(ip) :: i, num_points
  num_points = size(point)
  do i = 1, num_points
     call this%get_component_value_space( point(i), component, result(i) )
  end do
end subroutine array_function_get_component_values_set_space

subroutine array_function_get_component_values_set_space_time( this, point, time, component, result )
  class(array_function_t), intent(in) :: this
  type(point_t), intent(in)          :: point(:)
  real(rp), intent(in)                :: time(:)
  integer(ip), intent(in)             :: component
  real(rp), intent(inout)             :: result(:,:)
  integer(ip) :: i, j, num_points, num_time_steps
  num_points = size(point)
  num_time_steps = size(time)
  do j = 1, num_time_steps
     do i = 1, num_points
        call this%get_component_value_space_time( point(i), time(j), component, result(i,j) )
     end do
  end do
end subroutine array_function_get_component_values_set_space_time

subroutine array_function_get_value_space( this, result, point )
  class(array_function_t), intent(in) :: this
  type(point_t), intent(in)          :: point
  real(rp), intent(inout)             :: result(:)
  integer(ip) :: i
  do i = 1, this%number_components
     call this%get_component_value_space( point, i, result(i) )
  end do
end subroutine array_function_get_value_space

subroutine array_function_get_value_space_time( this, point, time, result )
  class(array_function_t), intent(in) :: this
  type(point_t), intent(in)          :: point
  real(rp), intent(in)                :: time
  real(rp), intent(inout)             :: result(:)
  integer(ip) :: i
  do i = 1, this%number_components
     call this%get_component_value_space_time( point, time, i, result(i) )
  end do
end subroutine array_function_get_value_space_time

subroutine array_function_get_values_set_space( this, point, result )
  class(array_function_t), intent(in) :: this
  type(point_t), intent(in)          :: point(:)
  real(rp), intent(inout)             :: result(:,:)
  integer(ip) :: i
  do i = 1, this%number_components
     call this%get_component_values_set_space( point, i, result(i,:) )
  end do
end subroutine array_function_get_values_set_space

subroutine array_function_get_values_set_space_time( this, point, time, result )
  class(array_function_t), intent(in) :: this
  type(point_t), intent(in)          :: point(:)
  real(rp), intent(in)                :: time(:)
  real(rp), intent(inout)             :: result(:,:,:)
  integer(ip) :: i
  do i = 1, this%number_components
     call this%get_component_values_set_space_time( point, time, i, result(i,:,:) )
  end do
end subroutine array_function_get_values_set_space_time


